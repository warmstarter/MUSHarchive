@@ Box Security System coded by Enkil@CyberMush 
@@ Summer: bjmurray@artsci.wustl.edu
@@ School Year: N416%nemomus.bitnet@academic.nemostate.edu
@@ 
@@ This is a neat little home security system I thought would be neat to
@@ share.  The owner of the object must either own the room the system is 
@@ in or be a wizard for the system to properly "boot" a player.
@@ To access any features, the desired owner of the SYSTEM must type 'own'
@@ -----------------------------------------------------------------------
@create Box Security System=10
&CHECK Box Security System=^* has arrived.:@switch/first [match(type(owner(%0)),PLAYER)]/[match(type(%0),PLAYER)]/[hasflag(%0,wizard)]/[hasflag(%0,puppet)]/[gt(match(v(security_list),owner(%0)),0)]=0/*/*/*/*,{},1/1/0/*/1,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(player1)]},1/1/0/*/0,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(player2)];@wait 3=@tel %0=home},1/1/1/*/*,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(wizard)]},1/0/*/1/1,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(puppet1)];@wait 4=@emit [u(puppet2)]},1/0/*/1/0,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(puppet1)];@wait 4=@emit [u(puppet3)];@wait 5=@tel %0=home},1/0/*/0/1,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(object1)];@wait 4=@emit [u(object2)]},1/0/*/0/0,{@emit [setq(0,num(%0))][u(checking)];@wait 2=@emit [u(object1)];@wait 4=@emit [u(object3)];@wait 5=@tel %0=home};@trig [setq(1,num(%0))]me/away_log
&DROP_CHECK Box Security System=^* dropped *.:@switch/first [match(type(owner(%1)),PLAYER)]/[match(type(%1),PLAYER)]/[hasflag(%1,wizard)]/[hasflag(%1,puppet)]/[gt(match(v(security_list),owner(%1)),0)]=0/*/*/*/*,{},1/1/0/*/1,{@emit [setq(0,num(%1))][u(checking)];@wait 2=@emit [u(player1)]},1/1/0/*/0,{@emit [setq(0,num(%1))][u(checking)];@wait 2=@emit [u(player2)];@wait 3=@tel %1=home},1/1/1/*/*,{@emit [setq(0,num(%1))][u(checking)];@wait 2=@emit [u(wizard)]},1/0/*/1/1,{},1/0/*/1/0,{},1/0/*/0/1,{@emit [setq(0,num(%1))][u(checking)];@wait 2=@emit [u(object1)];@wait 4=@emit [u(object2)]},1/0/*/0/0,{@emit [setq(0,num(%1))][u(checking)];@wait 2=@emit [u(object1)];@wait 4=@emit [u(object3)];@wait 5=@tel %1=home};@trig [setq(1,num(%1))]me/away_log
&AWAY_LOG Box Security System=@switch/first [hasflag([first(v(isauth))],connected)]/[gt(words(v(xlog)),0)]/[gt(match([lcon(here)],[first(v(isauth))]),0)]=0/0/*,{&LOG1 me=[name(r(1))] entered this room on [time()].;&XLOG me=1},0/1/*,{&log[add(v(xlog),1)] me=[name(r(1))] entered this room on [time()].;&XLOG me=[add(v(xlog),1)]},1/0/0,{&log1 me=[name(r(1))] entered this room on [time()].;&XLOG me=1},1/1/0,{&log[add(v(xlog),1)] me=[name(r(1))] entered this room on [time()].;&XLOG me=[add(v(xlog),1)]},{}
&OWN Box Security System=$own:@switch/first [gt(words(v(isauth)),0)]/[match(first(v(isauth)),%#)]=0/0,{&isauth me=%#%b;&security_list me=%#%b;@pemit %#=Box Security System acknoledges you as it's owner.},1/1,{@pemit %#=You already own the Box Security System.},{@pemit %#=Box Security System already has an owner.}
&CHANGE_OWN Box Security System=$chown *:@switch/first [match(first(v(isauth)),%#)]/[match(type(*%0),PLAYER)]/[match(first(v(isauth)),num(*%0))]=0/*/*,{@pemit %#=Permission denied.},1/0/*,{@pemit %#=%0 is not a player.},1/1/1,{@pemit %#=You already own the Box Security System.},1/1/0,{&isauth me=[num(*%0)]%b;&security_list me=[num(*%0)]%b;@pemit %#=Owner changed to [name(num(*%0))]. Authrized players and security list reset.}
&RESET Box Security System=$reset:@switch [match(first(v(isauth)),%#)]=1,{&security_list me=;&isauth me=;@set me=!monitor;@pemit %#=Box Security System reset. Awaiting new owner.},{@pemit %#=Permission denied.}
&ARM Box Security System=$arm:@switch [match(first(v(isauth)),%#)]/[hasflag(me,monitor)]=1/1,{@pemit %#=Box Security System is already armed.},1/0,{@set/quiet me=monitor;@emit/room Box Security System armed.},{@pemit %#=Permission denied.}
&DISARM Box Security System=$disarm:@switch/first [match(first(v(isauth)),%#)]/[hasflag(me,monitor)]=1/0,{@pemit %#=Box Security System already disarmed.},1/1,{@set/quiet me=!monitor;@emit/room Box Security System disarmed.},{@pemit %#=Permission denied.}
&AUTHORIZE Box Security System=$authorize *:@switch/first [match(first(v(isauth)),%#)]/[match(type(*%0),PLAYER)]/[match(v(isauth),[num(*%0)])]=0/*/*,{@pemit %#=Permission denied.},1/0/*,{@pemit %#=%0 is not a player.},1/1/1,{@pemit %#=[name(num(*%0))] is already authorized.},1/1/0,{@edit me/isauth=$,[num(*%0)]%b;@pemit %#=[name(num(*%0))] authorized.}
&DEAUTHORIZE Box Security System=$deauthorize *:@switch/first [match(first(v(isauth)),%#)]/[match(type(*%0),PLAYER)]/[gt(match(v(isauth),[num(*%0)]),0)]=0/*/*,{@pemit %#=Permission denied.},1/0/*,{@pemit %#=%0 is not a player.},1/1/0,{@pemit %#=[name(num(*%0))] is not authorized.},1/1/1,{@edit me/isauth=[num(*%0)]%b;@pemit %#=[name(num(*%0))] is no longer authorized.}
&ADD_PLAYER Box Security System=$add *:@switch/first [match(v(isauth),[num(%#)])]/[match(type(*%0),PLAYER)]/[gt(match(v(security_list),[num(*%0)]),0)]=0/*/*,{@pemit %#=Permission denied.},1/0/*,{@pemit %#=%0 is not a player.},1/1/1,{@pemit %#=[name(num(*%0))] is already on the list.},1/1/0,{@edit me/security_list=$,[num(*%0)]%b;@pemit %#=Player [name(num(*%0))] added to security list.}
&REM_PLAYER Box Security System=$rem *:@switch/first [match(v(isauth),[num(%#)])]/[match(type(*%0),PLAYER)]/[gt(match(v(security_list),[num(*%0)]),0)]=0/*/*,{@pemit %#=Permission denied.},1/0/*,{@pemit %#=%0 is not a player.},1/1/0,{@pemit %#=[name(num(*%0))] is not on the list.},1/1/1,{@edit me/security_list=[num(*%0)]%b;@pemit %#=Player [name(num(*%0))] removed from security list.}
&READ_LOGS Box Security System=$see logs:@switch [gt(match(v(isauth),%#),0)]/[gt(words(v(log1)),0)]=1/1,{@pemit %#=The following logs have occurred:%r%b[iter([parse(lattr(me),[switch(##,log*,##)])],##:[u(me/##)]%r)]},1/0,{@pemit %#=No logs have occured.},0/*,{@pemit %#=Permission denied.}
&CLEAR_LOGS Box Security System=$clear logs:@switch [match(first(v(isauth)),%#)]=1,{@dolist [parse(lattr(me),[switch(##,log*,##)])]=&## me=;&xlog me=;@pemit %#=Logs cleared.},{@pemit %#=Permission denied.}
&SEE_LIST Box Security System=$see security list:@pemit %#=The following people are cleared on the security list:%r[setq(0,u(name_edit))][switch(words(r(0)),1,[u(name_restore,r(0))],2,[u(name_restore,[first(r(0))] and [rest(r(0))].)],[u(name_restore,[u(add_comma,[extract([r(0)],1,[sub(words(r(0)),1)])])])]{,} and [u(name_restore,[extract(r(0),[words(r(0))],1)].)])]
&AUTH_LIST Box Security System=$see authorize list:@pemit %#=The following people are cleared on the authorize list:%r[setq(0,u(name_edit2))][switch(words(r(0)),1,[u(name_restore,r(0))],2,[u(name_restore,[first(r(0))] and [rest(r(0))].)],[u(name_restore,[u(add_comma,[extract([r(0)],1,[sub(words(r(0)),1)])])])]{,} and [u(name_restore,[extract(r(0),[words(r(0))],1)].)])]
&CHECKING Box Security System=Checking security clearance for [name(r(0))]...
&PLAYER1 Box Security System=Welcome player [name(r(0))], you have been cleared.
&PLAYER2 Box Security System=Player [name(r(0))] not cleared. Goodbye [name(r(0))].
&PUPPET1 Box Security System=Checking clearance on puppet [name(r(0))]'s owner...
&PUPPET2 Box Security System=Owner [name(owner(r(0)))] cleared. Welcome puppet [name(r(0))].
&PUPPET3 Box Security System=Owner [name(owner(r(0)))] not cleared. Goodbye puppet [name(r(0))].
&OBJECT1 Box Security System=Checking security clearance on object [name(r(0))]'s owner...
&OBJECT2 Box Security System=Owner [name(owner(r(0)))] cleared. Object [name(r(0))] ok.
&OBJECT3 Box Security System=Owner [name(owner(r(0)))] not cleared. Goodbye object [name(r(0))].
&WIZARD Box Security System=Welcome wizard [name(r(0))], you have been cleared.
&LINE Box Security System=[repeat(-,78)]%r[center(Enkil's Box Security System v.1.0,78, )]%r[repeat(-,78)]
@Desc Box Security System=A small brown cardboard box marked Enkil's Box Security System. Type sshelp for more.
&NAME_EDIT Box Security System=[iter(v(security_list),[edit([name(##)],%b,!~#)])]
&NAME_RESTORE Box Security System=[edit(%0,!~#,%b)]
&ADD_COMMA Box Security System=[edit(%0,%b,[,%b])]
&NAME_EDIT2 Box Security System=[iter(v(isauth),[edit([name(##)],%b,!~#)])]
&HELP Box Security System=$sshelp:@pemit %#=[u(line)]%rThe Box Security System v.1.0 guards your room from people you don't want coming into it. It allows players (or their puppets and objects) on the security list to teleport, enter or drop things to the room. Also, when the system owner is not connected or not in the room with the system, the system logs who entered the room and when. Note: The security system only works if the owner of the object BOX owns the room the system is in or if the owner is a wizard.%r%rType sshelp2 for list of commands.%r%r[repeat(-,78)]
&HELP2 Box Security System=$sshelp2:@pemit %#=[u(line)]%r%r[ljust(own,25)]-Sets the executer as the%r[space(25)]owner(controller) of the system.%r[ljust(chown <player name>,25)]-Allows the owner to change to the owner%r[space(25)]of the system to <player name>.%r[ljust(reset,25)]-Allows the owner to reset the security%r[space(25)]system to accept a new owner.%r[ljust(arm,25)]-Allows owner to arm the security system.%r%r[ljust(disarm,25)]-Allows owner to disarm the security system.%r%r[ljust(add <player name>,25)]-Allows those authorized to add players%r[space(25)]to the security list.%r[ljust(rem <player name>,25)]-Allows those authorized to remove%r[space(25)]players from the security list.%r%rType sshelp3 for more commands.%r[repeat(-,78)]
&HELP3 Box Security System=$sshelp3:@pemit %#=[u(line)]%r%r[ljust(authorize <player name>,28)]-Allows owner to add player to the%r[space(28)]authorize list.%r[ljust(deauthorize <player name>,28)]-Allows owner to remove players from the %r[space(28)]authorize list.%r[ljust(see security list,28)]-Allows enactor to see who is on the %r[space(28)]security list.%r[ljust(see authorize list,28)]-Allows enactor to see who is on the %r[space(28)]authorize list.%r[ljust(see logs,28)]-Allows those on the authorize list to see the%r[space(28)]systems away logs.%r[ljust(clear logs,28)]-Allows owner to clear old away logs.%r%rtype box credits to see all credits for Box Security System%r[repeat(-,78)]
&CREDITS Box Security System=$box credits:@pemit %#=[u(line)]%r%r%tBox Security System was coded by Enkil@Cyber.%r%r%tSpecial thanks for advice and coding ideas to:%r%tdaemon@disc()rdia.%r%r%tThanks to the following people for help in testing:%r%tdaemon, Candra, Cygnus.%r%r%tEnkil can be reached for comment/bug report via email at:%r%t(summer) bjmurray@artsci.wustl.edu or%r%t(fall-spring) N416%%Nemomus.bitnet@academic.nemostate.edu%r%r%tRemember: Don't take this code and claim it is yours!%r%t[space(10)]Give credit where credit is due!%r%r[repeat(-,78)]
@@ --------------------------------------------------------------------------


@create Stunned Parent
@link Stunned Parent=*[owner(me)]
@lock Stunned Parent==*[owner(me)]
@set Stunned Parent=!NO_COMMAND
@set Stunned Parent=ENTER_OK
@set Stunned Parent=HALT
@set Stunned Parent=ROYALTY
@STARTUP Stunned Parent=@tr me/unstun_person=[v(victim_num)]
&DEVFLAGS Stunned Parent=stunned
@AUSE Stunned Parent=@pemit [v(VICTIM_NUM)]=%n has slit your throat!;@tel [v(VICTIM_NUM)]=[v(dead_room)];@parent me=[v(corpse_parent)];&WEAPON_STRING me=knife judging by a deep gash in the throat;@name me=Corpse of [v(VICTIM_NAME)];@halt me=@@
@OUSE Stunned Parent=pulls out a knife and slits [after(name(me),Stunned%b)]'s throat!
@USE Stunned Parent=You pull out a knife and slit [after(name(me),Stunned%b)]'s throat!
&GARBAGE_ROOM Stunned Parent=#859
&UNSTUN_PERSON Stunned Parent=@pemit %0=You finally wake up, to find yourself in:;@dolist lcon(me)=@tel ##=%0;@tel %0=[switch(loc(me),v(stun_room),,v(garbage_room),,here)];@oemit %0=[name(%0)] wakes up.;@tel me=[v(garbage_room)];@destroy me;@wait 5=@halt me
&STUN_PERSON Stunned Parent=@name me=Stunned [name(%0)];@set me=enter_ok;@pemit %0=You have been stunned!;@oemit %0=[name(%0)] has been stunned!;@dolist lcon(%0)=@switch [hasdevflag(##,nosteal)]=0, @tel ##=me;@tel me=[loc(%0)];@tel %0=[v(stun_room)];&VICTIM_NAME me=[name(%0)];&VICTIM_NUM me=%0;&VICTIM_SUBJ me=[subj(%0)];@wait %1=@tr me/unstun_person=%0
@DESCRIBE Stunned Parent=The body of [v(victim_name)]. It looks like [v(victim_subj)] was stunned by a [v(weapon_string)].%rCommands are:%r%r[u(help,get [after(name(me),Stunned%b)],{Picks up the body.})]%r[u(help,drop [after(name(me),Stunned%b)],{Drops the body.})]%r[u(help,get [after(name(me),Stunned%b)]'s <item>,{Takes an item from the body.})]%r[u(help,give [after(name(me),Stunned%b)]=<item>,{Puts an item in the body.})]%r[u(help,enter [after(name(me),Stunned%b)],{Starts manipulating the body contents.})]%r[u(help,leave,{Stops manipulating the body contents.})]%r[u(help,use [after(name(me),Stunned%b)],{Slits the person's throat! Use with caution!})]
&HELP Stunned Parent=[ljust(%0,30)] - [indent(33,45,{%1})]
&WIZ_HELP Stunned Parent=VICTIM_NAME is the name of the victim. VICTIM_SUBJ is subj(victim). WEAPON_STRING is something like "phaser blast", or "disruptor blast" etc.
&A_DESTROY Stunned Parent=@pemit [v(victim_num)]=[name(%0)] has vaporized you with a [name(##)]!;@tel [v(victim_num)]=[v(dead_room)];@destroy me
&DEAD_ROOM Stunned Parent=#854

@create Corpse of Parent
@link Corpse of Parent=*[owner(me)]
@lock Corpse of Parent==*[owner(me)]
@set Corpse of Parent=!NO_COMMAND
@set Corpse of Parent=ENTER_OK
@set Corpse of Parent=HALT
@set Corpse of Parent=WIZARD
@set Corpse of Parent=ROYALTY
&DEVFLAGS Corpse of Parent=corpse
&STUN_PARENT Corpse of Parent=#857
@AUSE Corpse of Parent=@dolist lcon(me)=@tel ##=[loc(%#)];@tel me=[v(garbage_room)];@destroy me
@OUSE Corpse of Parent=buries [name(me)].
@USE Corpse of Parent=You bury [name(me)].
&GARBAGE_ROOM Corpse of Parent=#0
&DEAD_ROOM Corpse of Parent=#854
&KILL_PERSON Corpse of Parent=@name me=Corpse of [name(%0)];@set me=enter_ok;@pemit %0=You have been killed!;@oemit %0=[name(%0)] has been killed!;@dolist lcon(%0)=@switch [hasdevflag(##,nosteal)]=0, @tel ##=me;@tel me=[loc(%0)];@tel %0=[v(dead_room)];&VICTIM_NAME me=[name(%0)];&VICTIM_NUM me=%0;&VICTIM_SUBJ me=[subj(%0)]
&WIZ_HELP Corpse of Parent=VICTIM_NAME is the name of the victim. VICTIM_SUBJ is subj(victim). WEAPON_STRING is something like "phaser blast", or "disruptor blast" etc.
&HELP Corpse of Parent=[ljust(%0,30)] - [indent(33,45,{%1})]
@DESCRIBE Corpse of Parent=The corpse of [v(victim_name)]. It looks like [v(victim_subj)] was killed by a [v(weapon_string)].%rCommands are:%r%r[u(help,get [after(name(me),Corpse of%b)],{Picks up the corpse.})]%r[u(help,drop [after(name(me),Corpse of%b)],{Drops the corpse.})]%r[u(help,get [after(name(me),Corpse of%b)]'s <item>,{Takes an item from the corpse.})]%r[u(help,give [after(name(me),Corpse of%b)]=<item>,{Puts an item in the corpse.})]%r[u(help,enter [after(name(me),Corpse of%b)],{Starts manipulating the corpse contents.})]%r[u(help,leave,{Stops manipulating the corpse contents.})]%r[u(help,use [after(name(me),Corpse of%b)],{Buries the corpse.})]
&DEST_ROOM Corpse of Parent=#0
&A_DESTROY Corpse of Parent=@nuke me

@create PARENT: Master Phaser
@link PARENT: Master Phaser=#940
@set PARENT: Master Phaser=!NO_COMMAD
@set PARENT: Master Phaser=HAVEN
@set PARENT: Master Phaser=OPAQUE
@set PARENT: Master Phaser=WIZARD
@set PARENT: Master Phaser=SAFE
@DESCRIBE PARENT: Master Phaser=A [name(me)]. Type `phelp' for help.%rCurrent setting: [v(setting)]/[v(maxsetting)].%rCurrent charge: [v(charge)]/[v(maxcharge)].
&DEVFLAGS PARENT: Master Phaser=Weapon
&SINFO_SETTING PARENT: Master Phaser=1
&SETTING PARENT: Master Phaser=1
&CHARGE PARENT: Master Phaser=1000000000
@CHARGES PARENT: Master Phaser=1000000000
&MAXCHARGE PARENT: Master Phaser=1000000000
&MAXSETTING PARENT: Master Phaser=16
&RECHARGE PARENT: Master Phaser=&Charge me=[v(MaxCharges)]
&STUN_TIME PARENT: Master Phaser=300
&EXIT_DAMAGE_THRESHOLD PARENT: Master Phaser=-20
&THING_DAMAGE_THRESHOLD PARENT: Master Phaser=-20
&STUNNED_PARENT PARENT: Master Phaser=#1018
&CORPSE_PARENT PARENT: Master Phaser=#1013
&BODY_NUM PARENT: Master Phaser=#1038
&TRASH_ROOM PARENT: Master Phaser=#940
&DEAD_ROOM PARENT: Master Phaser=#1023
&STUN_ROOM PARENT: Master Phaser=#1022
&WHELP PARENT: Master Phaser=[ljust(%0,20)] - [indent(23,65,%1)]
@VA PARENT: Master Phaser=$phelp:@pemit %#=[u(line)]%r[center(Help for [name(me)],79)]%r[u(line)]%r%r[u(whelp,SET <setting>,{Sets the weapon to <setting>.})]%r[u(whelp,SINFO <setting>,{Obtains information about <setting>.})]%r[u(whelp,RECHARGE,{Recharges the weapon, if a weapon recharger is present.})]%r[u(whelp,DRAW,{Draws the weapon.})]%r[u(whelp,HOLSTER,{Holsters the weapon.})]%r[u(whelp,FIRE <target>,{Fires the weapon at <target>.})]%r[u(line)]
@VB PARENT: Master Phaser=$set *:&setting me=[setq(0,switch(add(%0,0),#-1 ARGUMENTS MUST BE NUMBERS,0,%0))][switch(lt(%q0,v(minsetting)),1,v(minsetting),switch(gt(%q0,v(maxsetting)),1,v(maxsetting),%q0))];@pemit %#=[name(me)] set to [v(setting)].;@oemit %#=%n presses a few buttons on %p [name(me)].
@VC PARENT: Master Phaser=$recharge:@switch [u(recharger_present)]=0, {@pemit %#=There is no weapon recharger in this room.}, {@pemit %#=You connect [name(me)] to the weapon recharger.;@oemit %#=%n connects a [name(me)] to the weapon recharger.;&charge me=[v(maxcharge)];@pemit %#=Weapon recharged.}
@VD PARENT: Master Phaser=$DRAW:@set me=[switch(hasflag(me,DARK),1,!DARK[pemit(%#,You draw your [name(me)].)][oemit(%#,%n draws a [name(me)].)],[pemit(%#,Your [name(me)] is already drawn.)])]
@VE PARENT: Master Phaser=$HOLSTER:@set me=[switch(hasflag(me,DARK),0,DARK[pemit(%#,You holster your [name(me)].)][oemit(%#,%n holsters %p [name(me)].)],[pemit(%#,Your [name(me)] is already holstered.)])]
@VF PARENT: Master Phaser=$FIRE *:@tr me/[switch(hasflag(me,dark),1,u(error_holstered),[switch([setq(0,locate(%#,%0,PTEne))]%q0,#-1,u(error_dontsee),#-2,u(error_whichone,%0),switch(type(%q0),EXIT,u(shoot_exit,%q0),THING,u(shoot_thing,%q0),u(shoot_player,%q0)))])]=%#,[locate(%#,%0,PTEne)]
@VG PARENT: Master Phaser=$sinfo *:&sinfo_setting me=[setq(0,switch(add(%0,0),#-1 ARGUMENTS MUST BE NUMBERS,0,%0))][switch(lt(%q0,v(minsetting)),1,v(minsetting),switch(gt(%q0,v(maxsetting)),1,v(maxsetting),%q0))];@pemit %#=[u(line)]%r[center(Information for Weapon Setting [v(sinfo_setting)],79)]%r[u(line)]%r[u(sinfo_[v(sinfo_setting)])]%r[u(line)]
&SINFO_13 PARENT: Master Phaser=Explosive/Disruption Effects; discharge energy 720,000 for 0.82 seconds, SEM:NDF ratio 1:18. The damage index is 1,100; shielded matter exhibits minor vibrational heating effects. Medium geologic displacement; <=90 m^3 rock/ore of 6.0 g/cm^3 explosively uncoupled per discharge.
&SINFO_12 PARENT: Master Phaser=Explosive/Disruption Effects; discharge energy 540,000 for 0.82 seconds, SEM:NDF ratio 1:14. The damage index is 940; ultradense alloy structural materials absorb/rebound energy, 0.1 sec delayed reaction before material vaporizes. Medium geologic displacement; <=50 m^3 rock/ore of 6.0 g/cm^3 explosively uncoupled per discharge.
&SINFO_11 PARENT: Master Phaser=Explosive/Disruption Effects; discharge energy 300,000 for 0.78 seconds, SEM:NDF ratio 1:11. The damage index is 670; ultradense alloy structural materials absorb/rebound energy, 0.20 sec delayed reaction before material vaporizes. Light geologic displacement; <=10 m^3 rock/ore of 6.0 g/cm^3 explosively uncoupled per discharge.
&SINFO_10 PARENT: Master Phaser=Disruption Effects; discharge energy 125,000 for 1.3 seconds, SEM:NDF ratio 1:9. The damage index is 450; heavy alloy structural materials absorb or rebound energy, 0.55 sec delay before material vaporizes.
&SINFO_9 PARENT: Master Phaser=Disruption Effects; discharge energy 65,000 for 1.5 seconds, SEM:NDF ratio 1:7. The damage index is 300; medium alloy or ceramic structural materials over 100 cm thickness begin exhibiting energy rebound prior to vaporization.
&SINFO_8 PARENT: Master Phaser=Disruption Effects; discharge energy 15,000 for 1.75 seconds, SEM:NDF ratio 1:3. Cascadian disruption forces cause humanoid organisms to vaporize, as 50%% of affected matter transistions out of the continuum. The damage index is 120; all unprotected matter is affected and penetrated according to depth/time.
&SINFO_7 PARENT: Master Phaser=Disruption Effects; discharge energy 4900 for 1.75 secnds, SEM:NDF ratio 1:1. Organic tissue damage causes immediate cessation of life processes, since disruption effects become widespread. The damage index is 50.
&SINFO_6 PARENT: Master Phaser=Disruption Effects; discharge energy 2,700 for 1.75 seconds, SEM:NDF ratio 90:1. Organic tissues and structural materials exhibit comparable penetration and molecular damage effects as higher energies cause matter to dissociate rapidly. Familiar thermal effects begin decreasing at this level. The damage index is 15.
&SINFO_5 PARENT: Master Phaser=Thermal Effects; discharge energy 857.5 for 1.5 seconds, SEM:NDF ratio 250:1. Humanoid tissue experiences severe burn effects but, due to water content, deep layers will not char. Simple personnel forcefields are penetrated after five seconds. Large Away Team fields will not be affected. The damage index is 7.
&SINFO_4 PARENT: Master Phaser=Thermal Effects; discharge energy 515.75 for 1.5 seconds, SEM:NDF ratio not applicable. base humanoids experience extensive CNS damage and epidermal EM trauma. Structural materials exhibit visible thermal shock. Discharges of longer than five seconds produce deep heat storage effects within metal alloys. The damage index is 3.5.
&SINFO_3 PARENT: Master Phaser=Heavy Stun; discharge energy 160.65 for 1.025 seconds, SEM:NDF ratio not applicable. Base humanoids remain in a sleep state for approximately one hour, resistant bioforms for fifteen minutes. Single discharges raise 1cc of liquid water by 100 deg C. Structural samples experience significant levels of thermal radiation. The damage index is 1.
&SINFO_2 PARENT: Master Phaser=Medium Stun; discharge energy 45.30 for 0.75 seconds, SEM:NDF ratio not applicable. Base-type humanoids are rendered unconscious for up to fifteen minutes, resistant humanoids up to five minutes. L ong exposures produce low levels of irreversible CNS and epithelial damage. Structural materials are not affected, though higher levels of vibrational warming are evident. The damage index is zero.
&SINFO_1 PARENT: Master Phaser=Light Stun; discharge energy index 15.75 for 0.25 seconds, SEM:NDF ratio not applicable. This setting is calibrated for base humanoid physiology, and causes temporary central nervous system (CNS) impairment. Subjects remain unconscious for up to five minutes. Higher levels of reversible CNS damage result from repeated long exposures. The discharge energy index if related to RNE protonic charge levels. Standard median-density composite structural material samples are not permanently affected, although small vibrational warming will be detected. A standard composite sample consists of multiple layers of tritanium, duranium, cortenite, lignin, and lithium-silicon-carbon 372. A standardized damage index is derived for setting comparisons; each whole number represents the number of cm of material penetrated or molecularly damaged. The damage index for this setting is zero.
&BODYPARTS PARENT: Master Phaser=butt left_leg right_leg pelvis waist chest left_shoulder right_shoulder left_arm right_arm neck face head left_ear right_ear
&RANDOM_BODYPART PARENT: Master Phaser=[first(shuffle(v(Bodyparts)))]
&NONE_PLAYER PARENT: Master Phaser=@pemit %1=You withstand the hit without even wincing.;@oemit %1=[name(%1)] withstands the hit without even wincing.
&PAIN_PLAYER PARENT: Master Phaser=@pemit %1=You wince in pain from the hit.;@oemit %1=[name(%1)] winces in pain from the hit.
&TAG_PLAYER PARENT: Master Phaser=@pemit %1=The words "IT" are painted on your [u(Random_Bodypart)].;@oemit %1=The words "IT" are painted on [name(%1)]'s [u(Random_Bodypart)].
&STUN_PLAYER PARENT: Master Phaser=&body_num me=[create(stunned body)];@set [v(body_num)]=royalty;@parent [v(body_num)]=[v(stunned_parent)];@tr [v(body_num)]/stun_person=%1,[v(stun_time)];&weapon_string [v(body_num)]=[v(weapon_string)]
&VAPORIZE_PLAYER PARENT: Master Phaser=@pemit %1=You have been vaporized!;@oemit %1=[name(%1)] has been vaporized!;@dolist lcon(%1)=@switch [hasdevflag(##,nosteal)]=0, @tel ##=[loc(%0)];@tel %1=[v(dead_room)]
&EXPLODE_PLAYER PARENT: Master Phaser=@pemit %1=You instantly explode, sending your blood and guts spraying everywhere!;@oemit %1=[name(%1)] instantly explodes, sending [poss(%1)] blood and guts spraying everywhere!;@tel %1=[v(dead_room)]
&KILL_PLAYER PARENT: Master Phaser=&body_num me=[create(corpse body)];@set [v(body_num)]=royalty;@parent [v(body_num)]=[v(corpse_parent)];@tr [v(body_num)]/kill_person=%1;&weapon_string [v(body_num)]=[v(weapon_string)]
&SHOOT_EXIT PARENT: Master Phaser=[switch(lock(%q0),*UNLOCKED*,u(shoot_unlocked_exit,%0),u(shoot_locked_exit,%0))]
&TRIG_DESTROY_EXIT PARENT: Master Phaser=@lock/user:old %1=[lock(%1)];@unlock %1;@tr %1/a_destroy=%0
&MSG_DESTROY_EXIT PARENT: Master Phaser=[remit(loc(%#),The blast blows [name(%0)] wide open!)][remit(loc(%0),[name(%0)] from [name(home(%0))] is blown wide open by a blast from outside!)]
&MSG_DAMAGE_EXIT PARENT: Master Phaser=[remit(loc(%#),[name(%0)] withstands the hit.)][remit(loc(%0),[name(%0)] from [name(home(%0))] shakes as it withstands a hit from outside.)]
&DAMAGE_EXIT_EFFECT PARENT: Master Phaser=[setq(1,rest(grab(v(damage_index),[v(setting)] *,|)))][set(%0,hitpoints:[sub(get(%0/hitpoints),%q1)])][switch(gt(get(%0/hitpoints),v(exit_damage_threshold)),1,u(msg_damage_exit,%0),[u(msg_destroy_exit,%0)]trig_destroy_exit)]
&SHOOT_EXIT_ROLL PARENT: Master Phaser=1
&SHOOT_LOCKED_EXIT PARENT: Master Phaser=[switch(hasdevflag(%0,invincible),1,pemit(%#,[name(%0)] is set invincible.),switch(u(shoot_exit_roll),1,[u(msg_shoot_player,%0)][switch(lt(v(Charge),0),1,,u(damage_exit_effect,%0))],u(msg_miss_player,%0)))]
&SHOOT_THING_ROLL PARENT: Master Phaser=1
&TRIG_DESTROY_THING PARENT: Master Phaser=@tel %1=[v(trash_room)];@halt %1=@tr me/a_destroy=%0
&MSG_DESTROY_THING PARENT: Master Phaser=[remit(loc(%#),The hit causes [name(%0)] to explode!)]
&MSG_DAMAGE_THING PARENT: Master Phaser=[remit(loc(%#),[name(%0)] withstands the hit.)]
&SHOOT_THING PARENT: Master Phaser=[switch(hasdevflag(%0,invincible),1,pemit(%#,[name(%0)] is set invincible.),switch(u(shoot_thing_roll),1,[u(msg_shoot_player,%0)][switch(lt(v(Charge),0),1,,u(damage_effect,%0))],u(msg_miss_player,%0)))]
&TEMP_RECHARGER_PRESENT PARENT: Master Phaser=1
&DRAIN_CHARGES PARENT: Master Phaser=[set(me,Charge:[sub(v(Charge),u(Chargesused))])][switch(lt(v(Charge),0),1,[pemit(%#,Your [name(me)] fizzles as it runs out of charges.)][oemit(%#,%n's [name(me)] fizzles at it runs out of charges.)][set(me,Charge:-1)]DONT_TRIGGER)]
&COMMENT_EFFECTS PARENT: Master Phaser=TAG just tags the person on a random body part, STUN stuns the player, use Stun Parent. KILL kills the player, use Corpse Parent. VAPORIZE vaporizes the player, and drops posessions. EXPLODE blows up the player, does not drop posessions. Gut-splattering.
&SETTING_EFFECTS PARENT: Master Phaser=1 STUN 300|2 STUN 900|3 STUN 3600|4 KILL|5 KILL|6 KILL|7 KILL|8 VAPORIZE|9 VAPORIZE|10 VAPORIZE|11 EXPLODE|12 EXPLODE|13 EXPLODE|14 EXPLODE|15 EXPLODE|16 EXPLODE
&SETTING_EFFECT PARENT: Master Phaser=[setq(1,rest(grab(v(setting_effects),[v(setting)] *,|)))][switch(first(%q1),NONE,none_player,TAG,tag_player,PAIN,pain_player,STUN,[set(me,STUN_TIME:[rest(%q1)])]stun_player,KILL,kill_player,VAPORIZE,vaporize_player,EXPLODE,explode_player)]
&DAMAGE_EFFECT PARENT: Master Phaser=[setq(1,rest(grab(v(damage_index),[v(setting)] *,|)))][set(%0,hitpoints:[sub(get(%0/hitpoints),%q1)])][switch(gt(get(%0/hitpoints),v(thing_damage_threshold)),1,u(msg_damage_thing,%0),[u(msg_destroy_thing,%0)]trig_destroy_thing)]
&PEMIT_EXCEPT PARENT: Master Phaser=[remove(remove(%0,%1),%2)]
&CHARGESUSED PARENT: Master Phaser=[rest(grab(v(energy_used),[v(setting)] *,|))]
&MSG_MISS_PLAYER PARENT: Master Phaser=[pemit(%#,You fire your [name(me)] at [name(%0)] but miss.)][pemit(%0,[name(%#)] fires [poss(%#)] [name(me)] at you but misses.)][pemit(u(pemit_except,lcon(loc(%#)),%#,%0),[name(%#)] fires [poss(%#)] [name(me)] at [name(%0)] but misses.)][set(me,Charges:[sub(v(Charges),u(Chargesused))])]
&MSG_SHOOT_PLAYER PARENT: Master Phaser=[pemit(%#,You fire your [name(me)] at [name(%0)] and hit!)][pemit(%0,%n fires [poss(%#)] [name(me)] at you and hits!)][pemit(u(pemit_except,lcon(loc(%#)),%#,%0),%n fires [poss(%#)] [name(me)] at [name(%0)] and hits!)][u(drain_charges)]
&SHOOT_PLAYER_ROLL PARENT: Master Phaser=[gt(rand(100),30)]
&SHOOT_PLAYER PARENT: Master Phaser=[switch(hasdevflag(%0,invincible),1,pemit(%#,[name(%0)] is set invincible.), switch(u(shoot_player_roll),1,[u(msg_shoot_player,%0)][u(setting_effect,%0)],u(msg_miss_player,%0)))]
&ERROR_HOLSTERED PARENT: Master Phaser=[pemit(%#,You must DRAW your weapon first.)]
&SHOOT_UNLOCKED_EXIT PARENT: Master Phaser=[pemit(%#,The exit `[name(%0)]' is not locked.)]
&ERROR_WHICHONE PARENT: Master Phaser=[pemit(%#,I don't know which %0 you mean.)]
&ERROR_DONTSEE PARENT: Master Phaser=[pemit(%#,I don't see that here.)]
&WEAPON_STRING PARENT: Master Phaser=phaser blast
&ENERGY_USED PARENT: Master Phaser=1 15.75|2 45.30|3 160.65|4 515.75|5 857.5|6 2700|7 4900|8 15000|9 65000|10 125000|11 300000|12 540000|13 720000|14 930000|15 1170000|16 1550000
&DAMAGE_INDEX PARENT: Master Phaser=1 0|2 0|3 1|4 3.5|5 7|6 15|7 50|8 120|9 300|10 450|11 670|12 940|13 1100|14 1430|15 1850|16 2450
&RECHARGER_PRESENT PARENT: Master Phaser=[trim([set(me,temp_recharger_present:0)][iter(lcon(loc(%#)),switch(hasdevflag(##,recharger),1,set(me,temp_recharger_present:1)))][v(temp_recharger_present)])]
&LINE PARENT: Master Phaser=[repeat(=,79)]
&HOLDING PARENT: Master Phaser=[switch(%#,loc(me),1,0)]

@create PARENT: Tag Phaser
@link PARENT: Tag Phaser=#940
@parent PARENT: Tag Phaser=#1014
@set PARENT: Tag Phaser=!NO_COMMAND
@set PARENT: Tag Phaser=HAVEN
@set PARENT: Tag Phaser=HALT
@set PARENT: Tag Phaser=OPAQUE
@set PARENT: Tag Phaser=WIZARD
@set PARENT: Tag Phaser=SAFE
&BODYPARTS PARENT: Tag Phaser=butt left_leg right_leg pelvis waist chest left_shoulder right_shoulder left_arm right_arm neck face head left_ear right_ear
&RANDOM_BODYPART PARENT: Tag Phaser=[first(shuffle(v(Bodyparts)))]
&TAG_PLAYER PARENT: Tag Phaser=@pemit %1=The words "IT" are painted on your [u(Random_Bodypart)].;@oemit %1=The words "IT" are painted on [name(%1)]'s [u(Random_Bodypart)].
&SETTING_EFFECTS PARENT: Tag Phaser=1 TAG|2 STUN 10|3 STUN 15|4 STUN 20|5 STUN 30|6 STUN 40|7 STUN 60|8 STUN 120|9 STUN 240
@CHARGES PARENT: Tag Phaser=-94271
&MAXCHARGE PARENT: Tag Phaser=1000000
&BODY_NUM PARENT: Tag Phaser=#1039
&STUN_TIME PARENT: Tag Phaser=300
&CHARGE PARENT: Tag Phaser=100
&MAXSETTING PARENT: Tag Phaser=9
&WEAPON_STRING PARENT: Tag Phaser=phaser blast
&ENERGY_USED PARENT: Tag Phaser=1 0|2 0|3 0|4 0|5 0|6 0|7 0|8 0|9 0
&DAMAGE_INDEX PARENT: Tag Phaser=1 1|2 2|3 4|4 8|5 16|6 32|7 64|8 128|9 256
&SETTING PARENT: Tag Phaser=1
@DESCRIBE PARENT: Tag Phaser=A [name(me)]. Type `whelp' for help.%rCurrent setting: [v(setting)]/[v(maxsetting)].%rCurrent charge: [v(charge)]/[v(maxcharge)].

@create Type I Phaser Parent
@lock Type I Phaser Parent==*[owner(me)]
@link Type I Phaser Parent=*[owner(me)]
@parent Type I Phaser Parent=#716
@set Type I Phaser Parent=!NO_COMMAND
@set Type I Phaser Parent=WIZARD
&SINFO_SETTING Type I Phaser Parent=1
&SETTING Type I Phaser Parent=1
&CHARGE Type I Phaser Parent=7200000
&MINSETTING Type I Phaser Parent=1
&MAXSETTING Type I Phaser Parent=8
&MAXCHARGE Type I Phaser Parent=7200000

@create Type II Phaser Parent
@lock Type II Phaser Parent==*[owner(me)]
@link Type II Phaser Parent=*[owner(me)]
@parent Type II Phaser Parent=#716
@set Type II Phaser Parent=!NO_COMMAND
@set Type II Phaser Parent=WIZARD
&SINFO_SETTING Type II Phaser Parent=1
&SETTING Type II Phaser Parent=1
&CHARGE Type II Phaser Parent=45000000
&MAXCHARGE Type II Phaser Parent=45000000
&MAXSETTING Type II Phaser Parent=16
&MINSETTING Type II Phaser Parent=1

@create Type III Phaser Parent
@lock Type III Phaser Parent==*[owner(me)]
@link Type III Phaser Parent=*[owner(me)]
@parent Type III Phaser Parent=#716
@set Type III Phaser Parent=!NO_COMMAND
@set Type III Phaser Parent=WIZARD
&SINFO_SETTING Type III Phaser Parent=1
&SETTING Type III Phaser Parent=1
&MINSETTING Type III Phaser Parent=1
&MAXSETTING Type III Phaser Parent=16
&MAXCHARGE Type III Phaser Parent=67500000
&CHARGE Type III Phaser Parent=67500000

@create PARENT: Mine
@lock PARENT: Mine=#3490
@set PARENT: Mine=WIZARD
@set PARENT: Mine=!NO_COMMAND
@set PARENT: Mine=HALT
&CREDITS PARENT: Mine=Coded by D'derix@Aramut's MUSH
&DEARM PARENT: Mine=$disarm mine:@emit The red light on the mine deactivates, as %n disarms the mine.;&status me=disarmed
&DESTROY PARENT: Mine=@wait %0=@dol lexits(here)=@lock ##=mine;@wait %0=@dol lcon(here)=@set ##=fixed;@wait %0=@dol lcon(here)=@set ##=gagged;@wait %0=@dol entrances(here)=@lock ##=mine;@wait [add(%0,2)]=@nuke me;@wait [add(%0,2)]=@nuke me;@wait %0=@dol lexits(here)=@parent ##=#3470;@wait %0=@dol entrances(here)=@switch [type(##)]=EXIT,{@parent ##=#3564};@wait %0=&olddesc here=[get(here/rdesc)];@wait %0=&rdesc here=This room has been damaged by a high power explosive device.  There are burn marks on the wall, and a few broken items on the floor, along with dead bodies of people who were in here when it went off.
@AHEAR PARENT: Mine=@swi [v(status)]=armed,{@emit %n walks in, and triggers a mine! There is a large explosion, causing major damage, and everyone in the room dies!;@wait 2=@dol lexits(here)=@lock ##=mine;@wait 2=@dol lcon(here)=@set ##=fixed;@wait 2=@dol entrances(here)=@lock ##=mine;@wait 2=@dol lcon(here)=@set ##=gagged;@wait 4=@nuke me;@wait 4=@nuke me;@wait 2=@dol lexits(here)=@parent ##=#3470;@wait 2=@dol entrances(here)=@parent ##=#3564;@wait %0=&olddesc here=[get(here/rdesc)];@wait 2=&rdesc here=This room has been damaged by a high power explosive device.  There are burn marks on the wall, and a few broken items on the floor, along with dead bodies of people who were in here when it went off.;@wait 2=@dol lcon(here)=@unlock ##}
@LISTEN PARENT: Mine=* has arrived*
&SELF_DESTRUCT_SEQ PARENT: Mine=@wait %0=@emit The mine beeps the final beep and detonates.  There is a large explosion causing major damage.;@tr me/destroy=%0
&DESTRUCT_MINE PARENT: Mine=$destruct mine *:@emit Mine destruct sequence activated.  You have %0 seconds untill detonation.  This process is unstoppable.;&status me=self destruct;@tr me/self_destruct_seq=%0
&ARM_MINE PARENT: Mine=$arm mine:&status me=armed;@emit The mine emits a small beep, and a red light activates, indicating that the mine is now armed.
&LAYMINE PARENT: Mine=$lay mine:&status me=layed;@tel loc(%#);@wait 1=@emit %n lays a mine.

@create PARENT: Outside Mine Exit
@link PARENT: Outside Mine Exit=#86
@lock/Basic PARENT: Outside Mine Exit=#3469
@set PARENT: Outside Mine Exit=NO_COMMAND
&CREDITS PARENT: Outside Mine Exit=Coded by D'derix@Aramut's MUSH
@OSUCCESS PARENT: Outside Mine Exit=manages to push through the debris blocking the doorway.
@SUCCESS PARENT: Outside Mine Exit=You manage to push through the debris blocking the doorway.
@FAILURE PARENT: Outside Mine Exit=There is too much debris blocking the doorway!

@create PARENT: Destroyed Entrance
&CREDITS Mine Parent=Coded by D'derix@Aramut's MUSH
@FAILURE PARENT: Destroyed Entrance=The door is blocked by a pile of debris.
@OFAILURE PARENT: Destroyed Entrance=tries to push through the debris blocking [name(me)], but fails.
@SUCCESS PARENT: Destroyed Entrance=You manage to push through the debris blocking [name(me)].
@OSUCCESS PARENT: Destroyed Entrance=manages to push through the debris blocking [name(me)].
@ODROP PARENT: Destroyed Entrance=pushes through the debris, and enters from [name([home(me)])]

@create PARENT: Destroyed Exit
&CREDITS Mine Parent=Coded by D'derix@Aramut's MUSH
@FAILURE PARENT: Destroyed Exit=You are dead and can't move!
@ODROP PARENT: Destroyed Exit=pushes through the debris, and enters from [name([home(me)])].
@SUCCESS PARENT: Destroyed Exit=You manage to push through the debris blocking the door.
@OSUCCESS PARENT: Destroyed Exit=manages to push through the debris blocking the door.

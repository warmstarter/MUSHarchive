@@ Wings 2.0 coded by daemon (poyner@pc2.pc.maricopa.edu)
@@
@@ A teleportation device that takes advantage of a players powers (such as
@@ wizard powers, or owning a room).  It also uses 'switchless' code for 
@@ greater speed.  A good source for understanding higher coding as a
@@ wizard might do. 
@@ --------------------------------------------------------------------------
@create wings parent 2.0
@set wings parent 2.0=inherit
@lock wings parent 2.0=owner(me)
@lock/uselock wings parent 2.0=owner(me)
@Desc wings parent 2.0=[u(me/wings)]
&JOIN wings parent 2.0=$join *:@switch/first [match(%#,owner(me))]/[isdbref(num(*%0))]/[loc(*%0)]/[u(isjump, *%0)]=0/*/*/*,{@pemit %#=those aren't your wings;@pemit owner(me)=%n is messing with your wings},1/0/*/*,{@pemit %#=Wings 2.0 --> I don't understand who %0 is},1/1/#-1/*,{@pemit %#=Wings 2.0 --> [name(*%0)] is set unfindable :(},1/1/*/0,{@pemit %#=Wings 2.0 --> [name(*%0)] is in a room that isn't jump okay :(},1/1/*/1,{@femit %#=u(%#/lmesg);@for %#=@tel loc(*%0);@wait 1=@femit %#=u(%#/emesg)}
&PEOPLE wings parent 2.0=$people:@pemit %#=Wings 2.0 --> you may join [setq(0,u(people2))][switch(words(r(0)),1,{[u(spaces,r(0))].},2,{[u(spaces,[first(r(0))] and [rest(r(0))])].},{[u(spaces,[u(comma,[extract([r(0)],1,[sub([words(r(0))],1)])])])] and [u(spaces,[extract(r(0),[words(r(0))],1)])]}.)]
&TAKE wings parent 2.0=$save *:&loc-[setq(0,[edit([secure(%0)],[space(1)],)])][r(0)] me=%l;@set me/loc-[r(0)]=no_inherit;@switch/first [strlen([get(me/loc-[r(0)])])]/[strlen(r(0))]=*/0,{@pemit %#=Wings 2.0 --> that name is essentially null, aborted;&loc- me=},0/*,{@pemit %#=Wings 2.0 --> "[r(0)]" isn't a valid name for an attribute.. try without punctuation},{@pemit %#=Wings 2.0 --> [name(%l)] saved as [r(0)].}
&LOCATIONS wings parent 2.0=$Locations:@pemit %#=-[ljust(Alias,15,-)][ljust(Location,30,-)][ljust(Number,15,-)]Owner-----------%r%b[iter([lattr(me/loc-*)],[ljust([lcstr([delete([delete(##,0,4)],13,99)])],15)][ljust([delete([name([u(me/##)])],28,99)],30)][ljust(%([delete([u(me/##)][flags([u(me/##)])],12,99)]%),15)][name([Owner([u(me/##)])])]%r)][repeat(-,77)]
&CHGEMES wings parent 2.0=$emesg *:&emesg %#=%0;@pemit %#=Wings 2.0 --> enter message now set as "%0"
&CHGLMES wings parent 2.0=$lmesg *:&lmesg %#=%0;@pemit %#=Wings 2.0 --> leave message now set as "%0"
&CLRLMES wings parent 2.0=$no lmesg:&lmesg %#=;@pemit %#=Wings 2.0 --> leave message cleared
&CLREMES wings parent 2.0=$no emesg:&emesg %#=;@pemit %#=Wings 2.0 --> enter message cleared
@Aclone wings parent 2.0=@pemit %#=cloning the wings might cause problems.. i wouldn't suggest it -daemon;@destroy me
&DEL wings parent 2.0=$unsave *:@switch [strlen(get(me/loc-%0))]=0,{@pemit %#=Wings 2.0 --> I don't have a location '%0'.},{@pemit %#=Wings 2.0 --> %0 removed from the wings;&loc-%0 me=}
&ISFIND wings parent 2.0=[not([match(loc(%0),#-1)])]
&ISJUMP wings parent 2.0=[or([hasflag(loc(%0),jump)],[match(owner(me),owner(loc(%0)))],[hasflag(owner(me),wizard)])]
&PEOPLE2 wings parent 2.0=[iter([filter(isfind, [filter(isjump, lwho())])],[edit(name(##),[space(1)],^-#)])]
&ISJUMP2 wings parent 2.0=[or([hasflag(%0,jump)],[match(owner(me),owner(%0))],[hasflag(owner(me),wizard)])]
&SPACES wings parent 2.0=[edit(%0,^-#,%b)]
&COMMA wings parent 2.0=[edit(%0,%b,[[,]%b])]
&TEL wings parent 2.0=$tel *:@switch/first [u(isjump2,[get(me/loc-%0)])]/[strlen([get(me/loc-%0)])]=*/0,{@pemit %#=Wings 2.0 --> I don't have a location "%0".},0/*,{@pemit %#=Wings 2.0 --> that location isn't jump okay :(},1/*,{@femit %#=[u(%#/lmesg)];@for %#=@tel [get(me/loc-%0)];@wait 1=@femit %#=[u(%#/emesg)]}
&WINGS wings parent 2.0=[repeat(-,77)]%r[center(Wings 2.0 - [faster, simpler, and more efficient],77)]%r[repeat(-,77)]%r%r%tThese wings make teleporting much easier, by allowing you to%r%b%bteleport to a person by his or her name. Another advantage is that you%r%b%bmay save a location by a name you choose, and teleport there using that%r%b%bname. You may also choose an enter or exit message.%r%r%tNOTE: jump_ok does *not* mean "come on over." ask first.%r%r%twings setup %b%b- my wings don't work, what now?%r%twings join %b%b%b- help on how to join a person by name%r%twings tel %b%b%b%b- help on how to teleport to a saved location%r%twings mesg %b%b%b- setting leave and entrance messages%r%twings credits - who made these things?%r%r[repeat(-,77)]
&MESGHELP wings parent 2.0=$wings mesg:@pemit %#=[repeat(-,77)]%r[center(Wings 2.0 - message help,77)]%r[repeat(-,77)]%r%r%tWhenever you teleport using the wings, you have the option of%r%b%bemitting a entrance and/or leave message. The advantage of this over%r%b%b@oxtport is that it doesn't have to be prefixed by your name. If%r%b%byou want to use %%'s, be sure to double them (example %%%%n).%r%r%tlmesg <message> -- sets your leave message to <message>%r%temesg <message> -- sets your enter message to <message>%r%tno lmesg -- clears your leave message%r%tno emesg -- clears your enter message%r%r[repeat(-,77)]
&JOINHELP wings parent 2.0=$wings join:@pemit %#=[repeat(-,77)]%r[center(Wings 2.0 - join help,77)]%r[repeat(-,77)]%r%r%tJoin allows you to teleport to a person using that person's%r%b%bname. The wings will automatically check to see if you are allowed%r%b%bto teleport there, such as if the person is unfindable or in a%r%b%b!jump_ok room.%r%r%tjoin <person> - teleport to a person (ex: join %n)%r%tpeople - lists people from WHO that are 'join'able%r%r[repeat(-,77)]
&TELHELP wings parent 2.0=$wings tel:@pemit %#=[repeat(-,77)]%r[center(Wings 2.0 - tel help,77)]%r[repeat(-,77)]%r%r%tWhile you are standing in a room, you can save that room to%r%b%byour wings using a name you choose. Then you may teleport to that%r%b%broom using the tel command.%r%r%tsave <name> - save your current room under <name>%r%tunsave <name> - delete the location <name> from wings%r%ttel <name> - teleport to <name>%r%tlocations - list the locations you have saved%r%r[repeat(-,77)]
&WINGSHELP wings parent 2.0=$wings setup:@pemit %#=[repeat(-,77)]%r[center(Wings 2.0 - setup help,77)]%r[repeat(-,77)]%r%r%tIf your wings don't work at all, make sure you or the wings%r%b%baren't set halted. The commands are %r%r%t%t%t@set me=!halted%r%t%t%t@set wings=!halted.%r%r%b%bAnother thing is that the wings require your permission to control%r%b%b(@teleport) you. To let the wings inherit your powers, type%r%b%b%r%t%t%t@set wings=inherit.%r%r%b%bIf all this still doesn't work, maybe you don't own these wings.%r%b%bTry @chown wings. If this is successful, do the above commands.%r%r[center(%[continued in wings setup2%],77)]%r%r[repeat(-,77)]
&WINGSHELP2 wings parent 2.0=$wings setup2:@pemit %#=[repeat(-,77)]%r[center(Wings 2.0 - setup help 2,77)]%r[repeat(-,77)]%r%r%tTo keep others from picking up or attempting to use your wings,%r%b%btype the following commands%r%r%t%t%t@lock wings=me%r%t%t%t@lock/uselock wings=me%r%r%b%bIf you are still having trouble, contact a wizard, a coder, or check%r%b%bwings credits for the coder's e-mail address.%r%r[repeat(-,77)]
@Use wings parent 2.0=[u(wings)]
&CREDITHELP wings parent 2.0=$wings credits:@pemit %#=[repeat(-,77)]%r[center(Wings 2.0 - credits,77)]%r[repeat(-,77)]%r%r%tWings 2.0 were coded by daemon with the help of other wizards%r%b%bof disc()rdiaMUSH. Ideas, coding help, and beta testing by Trey,%r%b%bArabella, Ren, RatCatcher, and others. daemon can be reached by%r%b%bemail at poyner@pc2.pc.maricopa.edu.%r%r%tThis code is freeware and should be avilable via anonymous%r%b%bftp on caisr2.caisr.cwru.edu. Please do not use this code and%r%b%bclaim that it is yours.%r%r[repeat(-,77)]















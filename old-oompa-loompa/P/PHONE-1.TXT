@create Jesse's Cellular Phone
@Desc Jesse's Cellular Phone=This is Jesse's Cellular Phone
&RING Jesse's Cellular Phone=$ring:@pemit/c [loc(me)]=[switch(rand(4),0,{%N's cellular suddenly begins to ring!!},1,{A nearby phone begins to ring - it seems to be coming from %N.},2,{A phone somewhere nearby begins to ring - it appears to be %N's.},{%n's Cellular Phone begins to ring.})]
&ANSWER Jesse's Cellular Phone=$answer *:@switch/first [setq(0,num(*%0))]%q0:[type(%q0)]=#-1:*,{@pemit %#=OOC: I can't find a player named `%0'.},%#:*,{@pemit %#=OOC: You cannot call yourself.},*:PLAYER,{@switch hasflag(%q0,CONNECTED)=0,{@pemit %#=OOC: That person is currently disconnected.},{@pemit/c [loc(me)]=%N removes %p cellular, flipping it open to answer the call.;@pemit %#=<Phone> You hear a click as the line is established.;@pemit %q0=<Phone> The phone is picked up at the far end.;&status me=active;&called me=%q0}},{@pemit %#=OOC: I can't find a player named `%0'.}
&BUSY Jesse's Cellular Phone=$busy *:@switch/first [setq(0,num(*%0))]%q0:[type(%q0)]=#-1:*,{@pemit %#=OOC: I can't find a player named `%0'.},%#:*,{@pemit %#=OOC: You can't do that.},*:PLAYER,{@pemit %#=OOC: Your line is busy.;@pemit %q0=<Phone> After a moment, you receive a busy signal from the far end.%RPossibly %N's phone is in use of is off the hook.},{@pemit %#=OOC: I can't find a player named `%0'.}
&RINGS Jesse's Cellular Phone=$rings *:@switch/first [setq(0,num(*%0))]%q0:[type(%q0)]=#-1:*,{@pemit %#=OOC: I can't find a player named `%0'.},%#:*,{OOC: You can't do that.},*:PLAYER,{@pemit %#=[switch(v(message),,OOC: Your phone rings and rings - but no one's there to pick up.,OOC: Your phone rings and rings before the answering machine picks up.)];@pemit %q0=<Phone> The phone at the far end rings and rings, but there's no answer.[switch(v(message),,,{%bAfter a moment the answering machine picks up: (OOC: @mail %N to leave a message.)%R%R[space(5)]"[u(message)]"})]},{@pemit %#=OOC: I can't find a player named `%0'.}
&HANGUP Jesse's Cellular Phone=$hangup:@switch v(status)=inactive,{@pemit %#=OOC: Your cellular is already hung up.},{@pemit/c [loc(me)]=%N clicks %p cellular to disconnect the call and hangs up.;@pemit [v(called)]=<Phone> There is a click as the phone at the far end is hung up.;&called me;&status me=inactive}
&OPTIONS Jesse's Cellular Phone=$options:@pemit %#=[repeat(=,78)]%ROptions: On Receiving a Call%R%RAnswer <person>%t%tAnswers the phone when <person> calls you.%RRing%t%t%tEmit _Your_ phone ringing if someone calls you.%r%rBusy <person>%t%tIf your phone is busy/off the hook ICly.%RRings <person>%t%tIf there's no answer and they should leave a message.%RUnlisted <person>%tIf your number is unlisted.%R%RNote: <person> for all of these cases should be the person that called you.%R[repeat(=,78)]
&PHONEHELP Jesse's Cellular Phone=$phonehelp:@pemit %#=[repeat(=,78)]%R[center(- - - Cellular Phone Commands - - -,78)]%R[repeat(-,78)]%RDial <person>%t%tCall someone on your cellular.%R%RAnswer <person>%t%tAnswer the phone when <person> calls you.%RRing%t%t%tEmit _your_ phone ringing for anyone nearby.%RBusy <person>%t%tIf your pehone is busy/off the hook when called.%RRings <person>%t%tIf there's no answer at your phone when called.%RUnlisted <person>%tIf your number is unlisted when called.%RTalk <pose/speak>%tTalk into your cellular (you can be overheard).%RTalkq <pose/speak>%tTalk quietly into your cellular (cannot be overheard).%RGruff <pose/speak>%tTalk in a unrecognizable voice%R%RHangup%t%tHang up your phone (emits to both ends of the line).%RHangupq%t%tHangup the phone (emits to your end only)%R%RType `phonehelp2' for further information.%R[repeat(=,78)]
&PHONEHELP2 Jesse's Cellular Phone=$phonehelp2:@pemit %#=[repeat(=,78)]%R[center(- - - Cellular Phone Notes - - -,78)]%R[repeat(-,78)]%R* Talking on the Phone *%RAll of the talking commands: talk, talkq, and gruff make use of the standard pose/emit characters - namely (:,;,|) in the same manner as tabletalk. This allows you to pose and emis across the phone line.%R%R* Bugging & Being Overheard *%RCurrently no one can overhear you (unless you want them to) on the phone. Phones are not code-bugged in any way and are solely designed as a RP aid. It is always assumed, however, that the Garda can ICly bug phones as they see fit and should be cooperated with.%R%R* Leaving Messages *%RThis code assumes the use of the @mail system for leaving messages on people's machines, To leave a message type: @mail <person>=phone message%R%R* Anonymous Calls *%RAlthough fairly secure, there are ways to OOCly ascertain identity through this phone system. At minimum if you want to make a anonymous call,  you should probably change your name (and +finger info) prior to making it. Gruff is also a good command for this sort of thing.%R%RType `phonehelp3' for configuration information.%R[repeat(=,78)]
&PHONEHELP3 Jesse's Cellular Phone=$phonehelp3:@pemit %#=[repeat(=,78)]%R[center(- - - Cellular Phone Configuration - - -,78)]%R[repeat(-,78)]%RThis code is designed with the intent to simulate personal cellular phones that would normally be carried by a individual. Phones can be @desc-ed to look like any style you want. All phones should be uselocked (see below) to keep other characters from using them inadvertantly. The MESSAGE attribute below is optional, if you'd like to have a message emitted to people calling you on the `rings' command.%R%RMESSAGE phone=<message on your answering machine, if you have one>
@Adesc Jesse's Cellular Phone=@pemit %#=%R[center(- - - Type `PHONEHELP' for commands. - - -,78)]%R
&MESSAGE Jesse's Cellular Phone=We'd like to thank you for calling the number you just dialed.... but we can't. <BEEP!>
&DIAL Jesse's Cellular Phone=$dial *:@switch/first [setq(0,num(*%0))]%q0:[type(%q0)]=#-1:*,{@pemit %#=OOC: I can't find a player named `%0'.},%#:*,{@pemit %#=OOC: You cannot call yourself.},*:PLAYER,{@pemit/c [loc(me)]=%N picks up %p cellular phone and begins to dial.;@wait 4=@pemit %#=<Phone> You hear the phone ringing at the other end.;@wait 6=@switch hasflag(%q0,CONNECTED)=0,{@pemit %#=<Phone> The phone rings and rings at the other end, but there's no answer.%ROOC: [name(%q0)] is currently disconnected.},{@pemit %q0=[repeat(-,78)]%RYour phone (or possibly another nearby) rings if you are ICly anywhere near it.%ROOC: Type `answer %N' - or `options' - if you have a coded cellular, or page %N to reply. Note that those about you cannot hear this message and you may wish to RP the phone ringing.%R[repeat(-,78)];&status me=active;&called me=%q0}},{@pemit %#=OOC: I can't find a player called `%0'.}
&UNLISTED Jesse's Cellular Phone=$unlisted *:@switch/first [setq(0,num(*%0))]%q0:[type(%q0)]=#-1:*,{@pemit %#=OOC: I can't find a player named `%0'.},%#:*,{@pemit %#=OOC: You can't do that.},*:PLAYER,{@pemit %#=OOC: [name(%q0)] is unable to get through as you're unlisted.;@pemit %q0=<Phone> An operator picks up: "I'm very sorry, but if the person you're trying to reach is one of our customers, they've asked to be unlisted for privacy reasons."},{@pemit %#=OOC: I can't find a player named `%0'.}
&TALK Jesse's Cellular Phone=$talk ?*:@switch [setq(0,switch(%0,:,%N %1,\;,%N%1,|,%1,%N says "%0%1"))]v(status)=active,{@pemit/c [loc(me)]=(Directed into the phone) %q0;@pemit [v(called)]=<Phone> %q0},{@pemit %#=OOC: You haven't called anyone yet!}
&TALKQ Jesse's Cellular Phone=$talkq ?*:@switch v(status)=active,{@pemit %#=(Quitely into the phone) [switch(%0,:,%N %1,\;,%N%1,|,%1,You say "%0%1")];@pemit [v(called)]=<Phone> [switch(%0,:,%N %1,\;,%N%1,|,%1,%N says quietly "%0%1")]},{@pemit %#=OOC: You haven't called anyone yet.}
&GRUFF Jesse's Cellular Phone=$gruff ?*:@switch v(status)=active,{@pemit %#=(Gruffly into the phone) [switch(%0,:,%N %1,\;,%N%1,|,%1,You say "%0%1")];@pemit [v(called)]=<Phone> [switch(%0,:,Someone %1,\;,Someone%1,|,%1,A gruff voice says "%0%1")]},{@pemit %#=OOC: You haven't called anyone yet.}
&HANGUPQ Jesse's Cellular Phone=$hangupq:@switch v(status)=inactive,{@pemit %#=OOC: You're cellular is already hung up.},{@pemit/c [loc(me)]=%N clicks %p cellular to disconnect the call and hangs up.;&called me;&status me=inactive}
&STATUS Jesse's Cellular Phone=inactive
@lock Jesse's Cellular Phone=*Jesse
@set Jesse's Cellular Phone=OPAQUE
@set Jesse's Cellular Phone=SAFE

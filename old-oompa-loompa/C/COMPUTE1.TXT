@create Ship Computer Parent
@Desc Ship Computer Parent=This is the OOC computer parent for all Ferengi ships.
@Adesc Ship Computer Parent=@pemit %#=Alert Status: [get(v(core)/alertstatus)]
&COLOR Ship Computer Parent=hb
&INFO Ship Computer Parent=This is the Ferengi ship computer parent. It is easily installed on any ship by following these simple instructions. First, build your ship according to proper specifications. Your ZMO parent must have a ZONES attribute. Then type:%r%R@create Computer Core%R@parent <DBREF of Computer Core>=#4139%R@parent <DBREF of ZMO>=#6139%R&core <DBREF of ZMO>=<DBREF of Computer Core>%R&ship_name <DBREF of ZMO>=<NAME of your ship>%R&captain <DBREF of Computer Core>=<DBREF of ship's captain>%R&brigin <DBREF of Computer Core>=<DBREF of brig entrance>%R&brigout <DBREF of Computer Core>=<DBREF of brig exit>%r%RFinally, type [ansi(hb,'INSTALL COMPUTER')] and everything else will be set up for you!%r Read [ansi(r,COMPUTER HELP)] for info on how to use the computer.%RBrought to you by code warriors [ansi(hb,Hugo)] and [ansi(hr,Kash'tal)]
&HELP Ship Computer Parent=$computer help:@remit loc(%#)=%n says, "Computer, display main help files.";@fo me=@pemit %#=[v(mainhelp)]
&INSTALL Ship Computer Parent=$install computer:@switch %#=owner(me),@trig me/doinstall=%#,{@pemit %#=%cbYou are not authorized to install this computer!%cn}
&COM Ship Computer Parent=$com *=*:@switch pmatch(%0)=#-1 NO MATCH,{@remit loc(%#)=The computer says, "'%0' is not a name that is recognized by the database of the [v(SHIP_NAME)]."},{@switch match([iter([get([v(core)]/room_list)],lcon(##))],[pmatch(%0)])=0,{@remit loc(%#)=The computer says, "[name([pmatch(%0)])] is not aboard the [v(SHIP_NAME)]."},{@remit loc(%#)=%n taps a button on the wall and says, "[name([pmatch(%0)])], {%1}";@remit loc([pmatch(%0)])=%cb[ucstr(v(SHIP_NAME))] ONBOARD COM: %cn{%1}%r(%cg[name(%#)]%cn to %cg[name([pmatch(%0)])]%cn)}}
&TIME Ship Computer Parent=$computer help time:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,TIME Help Screen)]%r%r[ansi(r,TIME)] displays the current date and time.
&CLOSED Ship Computer Parent=Three separate forcefields protect the rest of the ship from the contents of the brig.
&OPEN Ship Computer Parent=The brig is unsecured, its forcefields lowered.
&ADDCREW Ship Computer Parent=$crewadd *:@switch [not(%#=[get(v(core)/CAPTAIN)])]=1,{@remit loc(%#)=The computer says, "You are not authorized to edit the crew compliment of this ship."},{&ROSTER [v(core)]=[get([v(core)]/roster)] [num(*%0)];&CREW [v(core)]=[get([v(core)]/crew)] [num(*%0)];@remit loc(%#)=The computer says, "[name(*%0)] is now considered by all systems of the [v(SHIP_NAME)] to be an official crewmember."}
&SECADD Ship Computer Parent=$secadd *:@swi [not(%#=[get(v(core)/CAPTAIN)])]=1,{@remit loc([not(%#)=The computer says, "You are not authorized to edit the security files of the [v(SHIP_NAME)]."},{&SECLIST [v(core)]=[get([v(core)]/seclist)] [num(*%0)];&SECURITY [v(core)]=[get([v(core)]/security)] [num(*%0)];@remit loc([not(%#)=The computer says, "[name(*%0)] is now granted Security-level clearance to all systems of the [v(SHIP_NAME)]."}
&LOCATE Ship Computer Parent=$locate *:@switch pmatch(%0)=#-1 NO MATCH,{@remit loc(%#)=The computer says, "'%0' is not a name that is recognized by the database of the [v(SHIP_NAME)]."},{@switch match([iter([get([v(core)]/room_list)],lcon(##))],[num(*%0)])=0,{@remit loc(%#)=The computer says, "[name(*%0)] is not aboard the [v(SHIP_NAME)]."},{@remit loc(%#)=The computer says, "[name(*%0)] is in [name(loc(*%0))]."}}
&ALLCOM Ship Computer Parent=$comall *:@swi member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "Only crew of the [v(SHIP_NAME)] may use shipwide communications."},{@dol [get(v(core)/room_list)]=@remit ##=%cb[ucstr([v(SHIP_NAME)])] SHIPWIDE COM%cn <%cg[name(%#)]%cn>: {%0}}
&CREWLIST Ship Computer Parent=$roster:@switch member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "You are not a member of the crew of the [v(SHIP_NAME)]. This command is restricted."},{@pemit %#=[ansi(hb,[ucstr(v(SHIP_NAME))])] -- [ansi(hr,ACTIVE ROSTER)];@dolist [get([v(core)]/roster)]=@pemit %#=%cb[rank(##)] [name(##)]%t%t%cr<[get(##/last)]>}
&SECLIST Ship Computer Parent=$sec roster:@switch member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "Only authorized crew may display a list of Security-Clearance personnel."},{@pemit %#=[ansi(hb,[ucstr(v(SHIP_NAME))])] -- [ansi(hr,SECURITY PERSONNEL)];@dolist [get([v(core)]/seclist)]=@pemit %#=%cb[rank(##)] [name(##)] %t%t%cr<[get(##/last)]>}
&TEL Ship Computer Parent=$tel *:@switch member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "Only crewmembers of the [v(SHIP_NAME)] may access internal transporters."},{@switch member([get([v(core)]/room_alias)],%0)=0,{@remit loc(%#)=The computer says, "'%0' is not a valid transportation alias."},{@remit loc(%#)=%n says, "Computer, transport me to %0."%r%n vanishes in the shimmer of a transport beam.;@remit [extract([get([v(core)]/room_all)],match([get([v(core)]/room_alias)],%0),1)]=%n arrives in the shimmer of a transport beam.;@tel %#=[extract([get([v(core)]/room_all)],match([get([v(core)]/room_alias)],%0),1)]}}
&LISTTEL Ship Computer Parent=$list tel:@pemit %#=%crVALID TRANSPORTER ALIASES:%r%r%cb[get(v(core)/room_alias)]
&TELO Ship Computer Parent=$computer help telo:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,TELO Help Screen)]%r%r[ansi(r,TELO <name or object>=<tel alias>)] is a crew-only command that will transport any person from anywhere aboard the [v(SHIP_NAME)] to a location specified by the teleport alias (see [ansi(r,LIST TEL)]) of choice. It will also teleport an object as long as the person issuing the command is in the same room as the thing to be teleported.
&CREWONLY Ship Computer Parent=The computer says, "This function is only available to crew of the [v(SHIP_NAME)]."
&TRANSALIAS Ship Computer Parent=The computer says, "That is not a proper transporter alias."
&NOTHERE Ship Computer Parent=The computer says, "%0 is not aboard the [v(SHIP_NAME)]."
&MAINHELP Ship Computer Parent=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,Main Help Screen)]%rHelp is available on the following topics (type [ansi(hr,COMPUTER HELP <TOPIC>)] for details):%r%r%cr%tTEL%t%tTELO%t%tLIST TEL%tEDITING%r%tSECURE%t%tRELEASE%t%tSHIPSCAN%tTIME%r%tLOCATE%t%tROSTER%t%tSEC ROSTER%tALERT%r%tCREWADD%t%tCREWDEL%t%tSECADD%t%tSECDEL%cn
&HELPTEL Ship Computer Parent=$computer help tel:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,TEL Help Screen)]%r%r[ansi(r,TEL)] is a crew-only command that allows any member of the [v(SHIP_NAME)] staff to access internal teleportation mode. Type [ansi(r,LIST TEL)] for a display of all available teleport aliases and their destination aboard the [v(SHIP_NAME)]. [ansi(r,TEL)] is a case-sensitive command, and all aliases are given in lower case for convenience.%r%r%cbSEE ALSO: %crEDITING[space(4)]TELO[space(4)]LIST TEL%cn
&TIME1 Ship Computer Parent=$time:@remit loc(%#)=The computer says, "[ansi(b,Date and time:)] [ansi(r,[time()])]"
&HELPLOCATE Ship Computer Parent=$computer help locate:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,LOCATE Help Screen)]%r%r[ansi(r,LOCATE <name>)] is a public command that will give the location of any person aboard the [v(SHIP_NAME)].
&ROSTERHELP Ship Computer Parent=$computer help roster:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,ROSTER Help Screen)]%r%r[ansi(r,ROSTER)] displays the crew roster of the [v(SHIP_NAME)].
&HELPSECROSTER Ship Computer Parent=$computer help sec roster:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,SEC ROSTER Help Screen)]%r%r[ansi(r,SEC ROSTER)] displays a list of the Security-Clearance personnel aboard the [v(SHIP_NAME)].
&HELPCOM Ship Computer Parent=$computer help com:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,COM Help Screen)]%r%r[ansi(r,COM <name>=<msg>)] is a public command that will allow the speaker to send a message to any person aboard the [v(SHIP_NAME)].%r%r%crCOMALL <msg>%cn is a crew-only command that broadcasts <msg> to all personnel aboard ship.
&HELPRULES Ship Computer Parent=$computer help rules:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,RULES Help Screen)]%r%r[ansi(r,RULE <##>)] will display one of the 285 Ferengi Rules of Acquisition. Not all of these Rules have been installed in the [v(SHIP_NAME)]'s computer system yet, so many <##>s typed will have no direct response.
&TELO1 Ship Computer Parent=$telo *=*:@switch member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "Access to internal teleportation grids is limited to the crew of the [v(SHIP_NAME)]."},{@switch member([get([v(core)]/room_alias)],%1)=0,{@remit loc(%#)=The computer says, "'%1' is not a valid teleportation alias. Check for case-sensitivity and try again."},{@switch type([pmatch(%0)])=PLAYER,{@switch match([iter([get([v(core)]/room_list)],lcon(##))],[pmatch(%0)])=0,{@remit loc(%#)=The computer says, "[name(pmatch(%0))] is not aboard the [v(SHIP_NAME)]."},{@remit loc(%#)=%n says, "Computer, teleport [name(pmatch(%0))] to %1.";@remit loc(pmatch(%0))=[name(pmatch(%0))] vanishes in the shimmer of a transporter beam.;@remit [extract([get([v(core)]/room_all)],match([get([v(core)]/room_alias)],%1),1)]=[name(pmatch(%0))] arrives in the shimmer of a transporter beam.;@tel [pmatch(%0)]=[extract([get([v(core)]/room_all)],match([get([v(core)]/room_alias)],%1),1)]}},{@remit loc(%#)=%n says, "Computer, teleport %0 to %1.";@fo loc(%#)=@tel %0=[extract([get([v(core)]/room_all)],match([get([v(core)]/room_alias)],%1),1)]}}}
&HELPSECADD Ship Computer Parent=$computer help secadd:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,SECADD Help Screen)]%r%r%crSECADD <name>%cn is a Security-Clearance command that grants <name> Security clearance aboard the [v(SHIP_NAME)].%r%r%crSECDEL <name>%cn deletes <name> from the Security roster. It may only be issued by the ship's commanding officer.
&HELPCREWADD Ship Computer Parent=$computer help crewadd:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,CREWADD Help Screen)]%r%r[ansi(r,CREWADD <name>)] is a Daimon-Clearance command that adds <name> to the active roster of the [v(SHIP_NAME)].%r%r%crCREWDEL <name>%cn deletes <name> from that roster.
&ALERT Ship Computer Parent=$alert:@remit loc(%#)=The computer says, "Current alert status is [get(v(core)/alertstatus)]."
&REDALERT Ship Computer Parent=$red alert:@swi member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "Only crew of the [v(SHIP_NAME)] can declare a red alert, %n."},{;@dolist get([v(core)]/room_list)=@remit ##=%cg%n%cn => %crEmergency red alert! All hands to battle stations!%cn;@lock [get(v(core)/brigin)]=#0;@desc [get(v(core)/brigin)]=[v(closed)];@desc [get(v(core)/brigout)]=[v(closed)];@lock [get(v(core)/brigout)]=#0;&ALERTSTATUS [v(core)]=[ansi(hr,Red)];@trig me/red2}
&DELCREW Ship Computer Parent=$crewdel *:@swi [member([get([v(core)]/security)],%#]=0,{@remit loc(%#)=The computer says, "That is a Security-Clearance only command, %n.},{@swi [member([get([v(core)]/crewlist)],[num(*%0)])]=0,{@remit %#=The computer says, "`%0' is not a member of the crew."},{&CREWLIST [v(core)]=[ldelete([get([v(core)]/crewlist],[member([get([v(core)]/crewlist)],[num(*%0)])])];&ROSTER [v(core)]=[ldelete([get([v(core)]/roster)],[match([get([v(core)]/roster)],[num(*%0)])])];@remit loc(%#)=The computer says, "Crewmember `[name(*%0)]' deleted, %n."}}
&SHIPSCAN Ship Computer Parent=$shipscan:@swi member([v(core)]/security)],%#)=0,{@remit loc(%#)=The computer says, "Only Security personnel may do a shipwide scan, %N."},{@pemit %#=%cb[ucstr(v(SHIP_NAME))] COMPUTER CORE %cn--%cr SHIPWIDE SCAN%cn%cr;@dolist iter([get(v(core)/room_list)],[lplayer(##)])=@pemit %#=%cr[name(##)] %cnis in %cg[name(loc(##))]%cn.}
&SHIPSCANHELP Ship Computer Parent=$computer help shipscan:@pemit %#=%cb[ucstr(v(SHIP_NAME))] COMPUTER CORE %cn-- [ansi(hb,SHIPSCAN Help Screen)] %r%r%cnAccessible only to Security personnel, %crSHIPSCAN%cn does a Level-Three diagnostic scan of the ship to identify the location of all beings aboard.
&RED2 Ship Computer Parent=@switch [get(v(core)/alertstatus)]=[ansi(r,Red)],{@dolist get([v(core)]/room_list)=@remit ##=%crRed Alert! All hands to battle stations!%cn;@wait 1000=@trig me/red2},X
&CANCELALERT Ship Computer Parent=$cancel alert:@swi member([get([v(core)]/security)],%#)=0,{@remit loc(%#)=The computer says, "Only Security personnel may cancel an alert, %n."},{@dolist get([v(core)]/room_list)=&DESCCOLOR ##=n;@dolist get([v(core)]/room_list)=@remit ##=%cg%n%cn => %cbAlert status ended. Stand down and return to normal duty.%cn;&ALERTSTATUS [v(core)]=[ansi(hg,Green)]}
&YELLOWALERT Ship Computer Parent=$yellow alert:@swi member([get([v(core)]/crew)],%#)=0,{@remit loc(%#)=The computer says, "Only crew of the [v(SHIP_NAME)] may declare a yellow alert."},{@dolist get([v(core)]/room_list)=@remit ##=%cg%n%cn => %cyWarning! Cautionary yellow alert! All hands prepare for possible dangerous situation!%cn;&ALERTSTATUS [v(core)]=[ansi(hy,Yellow)]}
&ALERTHELP Ship Computer Parent=$computer help alert:@pemit %#=%cb[ucstr(v(SHIP_NAME))] COMPUTER CORE %cn-- [ansi(hb,ALERT HELP FILES)]%cn%r%r%r%cgCANCEL ALERT%cn returns the ship to normal green status.%r%r%cyYELLOW ALERT%cn orders a cautionary alert and seals the brig.%r%r%crRED ALERT%cn orders a full battle alert, seals the brig, and routinely reminds personnel of the alert status.%r%r%cbALERT %cndisplays the current alert status.
&DOINSTALL Ship Computer Parent=&room_list [v(core)]=[iter(v(zones),[get(##/room_list)])];@dolist [iter(v(zones),[get(##/room_list)])]={@cpattr me/adesc=##/adesc;&core ##=[v(core)]};&security [v(core)]=%0 [v(captain)];&seclist [v(core)]=%0 [v(captain)];&roster [v(core)]=%0 [v(captain)];&crew [v(core)]=%0 [v(captain)];&ROOM_ALIAS [v(core)];&ROOM_ALL [v(core)]=None;&alertstatus [v(core)]=[ansi(hg,Green)];@pemit %0=Computer Installation complete.
&SECRETURN Ship Computer Parent=@lock [iter(lexits(loc(%0)),[switch(loc(##),%1,##)])]=#8;@remit home(iter(lexits(loc(%0)),[switch(loc(##),%1,##)]))=With a hiss, the door to the [name(loc(iter(lexits(loc(%0)),[switch(loc(##),%1,##)])))] slides shut.
&RELRETURN Ship Computer Parent=@unlock [iter(lexits(loc(%0)),[switch(loc(##),%1,##)])];@remit home(iter(lexits(loc(%0)),[switch(loc(##),%1,##)]))=The door to [name(loc(iter(lexits(loc(%0)),[switch(loc(##),%1,##)])))] chimes softly, indicating that it is now unlocked.
&SECURE Ship Computer Parent=$secure *:@switch [member([get([v(core)]/security)],%#)]=0,{@remit loc(%#)=The computer says, "%n, that is a Security-Clearance command."},{@swi member([get([v(core)]/room_alias)],%0)=0,{@remit loc(%#)=The computer says, "`%0' is not a valid alias, %n."},{@dolist lexits([extract([get([v(core)]/room_all)],[match([get([v(core)]/room_alias)],%0)],1)])={@trig me/secreturn=##,[extract([get([v(core)]/room_all)],[match([get([v(core)]/room_alias)],%0)],1)];@lock ##=#8;@remit home(##)=With a hiss, the door to [name(loc(##))] slides shut.};@remit loc(%#)=The computer says, "Location secured.";@swi %0=brig,{@desc [get(v(core)/brigin)]=[v(closed)];@desc [get(v(core)/brigout)]=[v(closed)]}}}
&RELEASE Ship Computer Parent=$release *:@swi [member([get([v(core)]/security)],%#)]=0,{@remit loc(%#)=The computer says, "%n, that is a Security-Clearance command."},{@swi member([get([v(core)]/room_alias)],%0)=0,{@remit loc(%#)=The computer says, "`%0' is not a valid room alias, %n."},{@dolist lexits([extract([get([v(core)]/room_all)],[match([get([v(core)]/room_alias)],%0)],1)])={@trig me/relreturn=##,[extract([get([v(core)]/room_all)],[match([get([v(core)]/room_alias)],%0)],1)];@unlock ##;@remit home(##)=The door to [name(loc(##))] chimes to indicate that it is unlocked.};@remit loc(%#)=The computer says, "Location released.";@swi %0=brig,{@desc [get(v(core)/brigin)]=[v(open)];@desc [get(v(core)/brigout)]=[v(open)]}}}
&HELPSECURE Ship Computer Parent=$computer help secure:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,SECURE and RELEASE Help Screen)]%r%r[ansi(r,SECURE <alias name>)] and [ansi(r,RELEASE <alias name>)] are Security-Clearance commands that seal or unseal all exits to the selected room.%r%r%cbSEE ALSO: %crLIST TEL%cn
&HELPCOMALL Ship Computer Parent=$computer help comall:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,COMALL Help Screen)]%r%r[ansi(r,COMALL <msg>)] is a crew-only command that broadcasts <msg> to all personnel on the ship.
&TELADD Ship Computer Parent=$addtel *:@swi %#=[get([v(core)]/captain)]={@swi member([get([v(core)]/room_all)],[loc(%#)])=0,{&ROOM_ALL [v(core)]=[get([v(core)]/room_all)] [loc(%#)];&ROOM_ALIAS [v(core)]=[get([v(core)]/room_alias)] %0;@remit loc(%#)=The computer says, "[name(loc(%#))] added to transporter alias list as '[%0].'"},{@remit loc(%#)=The computer says, "[name(loc(%#))] already has an alias, which is '[extract([get([v(core)]/room_alias)],[match([get([v(core)]/room_all)],[loc(%#)])],1)].'"}},{@remit loc(%#)=The computer says, "Interal tranporter buffers may only be edited by the commanding officer, %n."}
&HELPLISTTEL Ship Computer Parent=$computer help list tel:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,LIST TEL)]%r%r%crLIST TEL%cn displays a complete list of all the available transporter aliases that may be used in conjunction with the %crSECURE%cn and %crTEL%cn commands.
&ADDTEL Ship Computer Parent=$addtel *:@swi or([member([get([v(core)]/captain)],%#)],[member([owner(me)],%#)])=1,{@swi words(%0)=1,{@swi [get([v(core)]/room_all)]=None,{&ROOM_ALL [v(core)]=[loc(%#)];&ROOM_ALIAS [v(core)]=[%0];@remit loc(%#)=The computer says, "`%0' added to transporter and security list, %n."},{&ROOM_ALL [v(core)]=[get([v(core)]/room_all)] [loc(%#)];&ROOM_ALIAS [v(core)]=[get([v(core)]/room_alias)] [%0];@remit loc(%#)=The computer says, "`%0' added to transporter and security alias list, %n."}},{@remit loc(%#)=The computer says, "Transporter/security aliases can only be one word long."}},{@remit loc(%#)=The computer says, "Only the computer programmer and commanding officer of this ship may edit the transporter/security alias list, %n."}
&DELTEL Ship Computer Parent=$deltel *:@swi or([member([get([v(core)]/captain)],%#)],[member([owner(me)],%#)])=1,{@swi [member([get([v(core)]/room_alias)],%0)]=0,{@remit %#=The computer says, "'%0' is not a valid alias."},{&ROOM_ALL [v(core)]=[ldelete([get([v(core)]/room_all)],[member([get([v(core)]/room_alias)],%0)])];&ROOM_ALIAS [v(core)]=[ldelete([get([v(core)]/room_alias)],[member([get([v(core)]/room_alias)],%0)])];@remit loc(%#)=The computer says, "Alias `%0' deleted, %n."}},{@remit loc(%#)=The computer says, "Only the computer programmer and commanding officer of this ship may edit the transporter/security alias list, %n."}
&CLEARTEL Ship Computer Parent=$cleartel:@swi or([member([get([v(core)]/captain)],%#)],[member([owner(me)],%#)])=1,{@remit loc(%#)=The computer says, "Teleport aliases cleared.";&ROOM_ALIAS [v(core)];&ROOM_ALL [v(core)=None},{@remit loc(%#)=The computer says, "Only the computer programmer and commanding officer of this ship may edit the transporter/security alias list, %n."}
&HELPEDITING Ship Computer Parent=$computer help editing:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,EDITING Help Screen)]%r%rThese are all restricted commands.%r%r%crADDTEL <alias>%cn adds the current room to the transporter alias list.%r%crDELTEL <alias>%cn deletes a transporter alias.%r%crCLEARTEL%cn deletes all transporter aliases and resets the command.%r
&SECDEL Ship Computer Parent=$secdel *:@swi [get([v(core)]/captain)]=%#,{@remit loc(%#)=The computer says, "That is a command that may only be issued by the commanding officer, %n.},{@swi [member([get([v(core)]/security)],[num(*%0)])]=0,{@remit %#=The computer says, "`%0' is not a member of Security."},{&SECLIST [v(core)]=[ldelete([get([v(core)]/crewlist],[member([get([v(core)]/seclist)],[num(*%0)])])];&SECURITY [v(core)]=[ldelete([get([v(core)]/roster)],[match([get([v(core)]/security)],[num(*%0)])])];@remit loc(%#)=The computer says, "Security personnel '[name(*%0)]' deleted, %n."}}
&HELPCREWDEL Ship Computer Parent=$computer help crewdel:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,CREWDEL Help Screen)]%r%r%crCREWADD <name>%cn is a commanding officer-only command that adds <name> to the active roster.%r%r%crCREWDEL <name>%cn removes <name> from that roster.
&HELPSECDEL Ship Computer Parent=$computer help secdel:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,SECDEL Help Screen)]%r%r%crSECADD <name>%cn is a Security-Clearance command that grants <name> Security clearance aboard the [v(SHIP_NAME)].%r%r%crSECDEL <name>%cn deletes <name> from the Security roster. It may only be issued by the ship's commanding officer.
&HELPRELEASE Ship Computer Parent=$computer help release:@pemit %#=[ansi(b,[ucstr(v(SHIP_NAME))] COMPUTER CORE)] -- [ansi(hb,SECURE and RELEASE Help Screen)]%r%r%crSECURE <alias> %cnand %crRELEASE <alias>%cn are Security-Clearance commands that seal or unseal all exits to the selected room.%r%r%cbSEE ALSO: %crLIST TEL%cn
@set Ship Computer Parent=VISUAL
@set Ship Computer Parent=PARENT_OK
@set Ship Computer Parent=!NO_COMMAND
@parent Ship Computer Parent=IC Zone Parent

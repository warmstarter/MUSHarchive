@power #874=!immutable
-

@fail #874 = 
  [name(me)] doesn't want to be picked up.
-

@desc #874 = 
 [desc(%!)]%r
 %r
 [pregnantdesc(%!)]%r%r
 [clothingdesc(%!)].
 [combatdesc(%!)]
 [damagedesc(%!)]
 %r%r
-

# This function will return the visible wounds on the player.
# %q0 = visible wounds that the player has, if any.
# %q1 = All of the covered bodylocs on the player.
# %q2 = Total damage points of the player.

&function-damagedesc #50=
 [setq(0, lattr(%0/damage-*-*))]
 [setq(0, filter(filter-invisible-wounds, %q0))]
 [setq(1, iter(lattr(%0/wear-*), trim(clip(##))))]
 [setq(0, filter(filter-covered-wounds, %q0))]
 [setq(2, get(%0/damage))]

 %b
 [iter(%q0, [name(%0)] has [art(u(u-damage-show, last(##, -)))] 
            [lcstr(u(u-damage-show, last(##, -)))] on 
            [poss(%0)] [lcstr(extract(##, 2, 1, -))].)]%b

 [switch(1, gt(%q2, 20), [name(%0)] looks half dead.,
                gt(%q2, 15), [name(%0)] looks to be in very poor health.,
                gt(%q2, 10), [name(%0)] looks to be in bad shape.,
                gt(%q2, 4), [name(%0)] looks a little beat up.,
                [name(%0)] looks to be in general good health.)]
 

-
# Changes damage 'word' to what should appear.
#    Broken becomes 'break'.

&u-damage-show #50=
  [switch(%0,broken,break,%0)]

-
# %q1 = list of wearlocs that the player has covered

&filter-covered-wounds #50=
[not(t(member(%q1, lcstr(trim(extract(%0, 2, 1, -))))))]

-

&filter-invisible-wounds #50=
[t(member(get(#15/visible-wounds), lcstr(last(%0, -))))]

-

&function-desc #50=
[setq(0, get(%0/sex))]
[setq(1, default(%0/features, none))]%r
[setq(2, default(%0/age, 20))]
[switch(1, 
          lte(%q2, 11), A little [switch(%q0, f*, girl, boy)],
          lte(%q2, 13), A [switch(%q0, f*, maiden, youth)],
          lte(%q2, 19), A young [switch(%q0, f*, woman, man)],
          lte(%q2, 28), A [switch(%q0, f*, woman, man)],
          lte(%q2, 35), A middle aged [switch(%q0, f*, woman, man)],
          lte(%q2, 50), An older [switch(%q0, f*, woman, man)],
          An elderly [switch(%q0, f*, woman, man)])] with 
          [switch(%q1, none, no distinguishing features, %q1)]%,%b
[name(%0)] has [trait(%0, eye color)] eyes, 
[trait(%0, hair length)] [trait(%0, hair color)] hair and 
[trait(%0, skin color)] skin.  [name(%0)] appears to weigh 
about [get(%0/weight)] pounds and stands about [get(%0/height)] 
tall.  
[udefault(%0/description, get_eval(%0/desc))]
-

@adesc #874 = 
  @looknotify %#=[num(me)];
  @bath %#
-
@asucc #874=@bath %#
-
###################################################################
# Run by the player that is being checked.
# %0 is the player the pemit goes to
###################################################################
# %q0 is the last time the player bathed (in secs)
# %q1 is how long ago it was that the player bathed (in secs)
# %q2 are the number of seconds in a day
###################################################################
# Messages:  Bathed between (in days)
#
# Fresh      0 - 1
#   None     1 - 3
# Dirty      3 - 7
# Needs bath 7 - 20
# Rank!      after 20
###################################################################

&cmd-@bath #7=$@bath *:
think setq(0, default(%#/bathed-last, 0));
think setq(1, sub(secs(), %q0));
think setq(2, mul(60,60,24));
@pemit %0=[switch(1, 
   lt(%q1, %q2), 
      %N smells fresh and clean like [subj(%#)]'s just been to the baths!,
   lt(%q1, mul(%q2,3)),
      ,
   lt(%q1, mul(%q2, 7)), 
      %N is looking a bit dirty., 
   lt(%q1, mul(%q2, 20)), 
      %N could use a bath., 
   %N is rank! %N is in dire need of a bath.)]
-
###################################################################
#               Strength Settings: What the players can lift      #
###################################################################

&strength-0 #874= 5
-
&strength-max-0 #874=25
-
&strength-1 #874= 15
-
&strength-max-1 #874= 50
-
&strength-2 #874= 25
-
&strenth-max-2 #874 = 90
-
&strength-3 #874= 35
-
&strength-max-3 #874=135
-
&strength-4 #874= 60
-
&strength-max-4 #874 = 200
-
&strength-5 #874 =80
-
&strength-max-5 #874 = 300
-

&strength-6 #874 = 100
-
&strength-max-6 #874 =  500
-

#################################################################
#            OTHER PLAYER SETTINGS                              #
#################################################################

&ic-home #874=#2948
-
@power #874=immutable












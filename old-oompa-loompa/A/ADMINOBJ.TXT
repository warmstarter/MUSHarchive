@create @Admin Object
@link @Admin Object = #2
@lock/Basic @Admin Object=#2
@lock/Use @Admin Object=Wiz/1|Roy/1|Can_Pcreate/1
@set @Admin Object = INHERIT
@set @Admin Object = OPAQUE
@set @Admin Object = WIZARD
&FLAG @Admin Object=[hasflag(*%#,Wr)]
&DO_UNSUMMON @Admin Object=$@unsummon *:[tel(*%0,[get(*%0/summon)])][pemit(%#,[ansi(w,SUMMON:)] [ansi(c,YOU UNSUMMON [ucstr([name([num(*%0)])])])])][pemit(*%0,[ansi(w,SUMMON:)] [ansi(c,YOU HAVE BEEN UNSUMMONED.)])]
&DO_JOIN @Admin Object=$@join *:@switch %0=me,{@pemit %#=[ansi(c,JOIN:)] [ansi(g,YOU CAN NOT JOIN YOURSELF.)]},{@switch [num(*%0)]=#-1,{@pemit %#=[ansi(c,JOIN:)] [ansi(g,PLAYER DOES NOT EXIST.)]},%#,{@pemit %#=[ansi(c,JOIN:)] [ansi(g,YOU CAN NOT JOIN YOURSELF.)]},{@switch [loc(*%0)]=%l,{@pemit %#=[ansi(c,JOIN:)] [ansi(g,PLAYER IS IN THE SAME LOCATION AS YOU.)]},{@switch [xget(*%0,join_ok)]=0,{@pemit %#=[ansi(c,JOIN:)] [ansi(g,PLAYER DOES NOT WISH TO BE JOINED.)];@pemit *%0=[ansi(c,JOIN:)] [ansi(g,PLAYER [ucstr(%N)] HAS ATTEMPTED TO JOIN YOU AND HAS FAILED.)]},{@switch [hasflag(*%0,CONNECTED)]=0,{&join *%#=%l;@set *%#/JOIN=WIZARD;@set *%#/JOIN=MORTAL_DARK;@tel *%#=[loc(*%0)];@pemit %#=[ansi(c,JOIN:)] [ansi(g,YOU HAVE SUCCESSFULLY JOINED [ucstr([name([num(*%0)])])])] [ansi(y,(NOT CONNECTED))]},{&join *%#=%l;@set *%#/JOIN=WIZARD;@set *%#/JOIN=MORTAL_DARK;@tel *%#=[loc(*%0)];@pemit %#=[ansi(c,JOIN:)] [ansi(g,YOU HAVE SUCCESSFULLY JOINED [ucstr([name([num(*%0)])])])];@pemit *%0=[ansi(c,JOIN:)] [ansi(g,YOU HAVE BEEN JOINED BY [ucstr(%N)].)]}
&DO_SUMMON @Admin Object=$@summon *:@switch %0=me,{@pemit %#=[ansi(c,SUMMON:)] [ansi(g,YOU CAN NOT SUMMON YOURSELF.)]},{@switch [num(*%0)]=#-1,{@pemit %#=[ansi(c,SUMMON:)] [ansi(g,PLAYER DOES NOT EXIST.)]},#1,{@pemit %#=[ansi(c,SUMMON:)] [ansi(hr,PERMISSION DENIED.)]},%#,{@pemit %#=[ansi(c,SUMMON:)] [ansi(g,YOU CAN NOT SUMMON YOURSELF.)]},{@switch [hasflag(*%0,CONNECTED)]=0,{@pemit %#=[ansi(c,SUMMON:)] [ansi(g,PLAYER IS NOT CONNECTED.)]},{@switch [loc(*%0)]=%l,{@pemit %#=[ansi(c,SUMMON:)] [ansi(g,PLAYER IS IN THE SAME LOCATION AS YOU.)]},{@switch [xget(*%0,summon_ok)]=0,{@pemit %#=[ansi(c,SUMMON:)] [ansi(g,PLAYER DOES NOT WISH TO BE SUMMONED.)];@pemit *%0=[ansi(c,SUMMON:)] [ansi(g,PLAYER [ucstr(%N)] HAS ATTEMPTED TO SUMMON YOU AND HAS FAILED.)]},{&summon *%0=[loc(*%0)];@set *%0/SUMMON=WIZARD;@set *%0/SUMMON=MORTAL_DARK;@tel *%0=%l;@pemit %#=[ansi(c,SUMMON:)] [ansi(g,YOU HAVE SUCCESSFULLY SUMMONED [ucstr([name([num(*%0)])])])];@pemit *%0=[ansi(c,SUMMON:)] [ansi(g,YOU HAVE BEEN SUMMONED BY [ucstr(%N)].)]}
&LINE @Admin Object=[ansi(hg,repeat(=,79))]
&GET_PLAYER_ATTR @Admin Object=[extract(v(Player_Attr_Vals),member(v(Player_Attrs),%0),1,|)]
&CAN_PCREATE @Admin Object=[or(or(hasflag(%#,Wizard),hasflag(%#,Royalty)),haspower(%#,Player_Create))]
&VIEW @Admin Object=$@reg*/view:@switch [u(Can_Pcreate)]=0,{@pemit %#=Permission denied.},{@pemit %#=[switch(words(v(Apps),|),0,There are currently no character registrations.,{[u(Line)]%r[ansi(hg,center(TFB MUSH Character Registrations,79))]%r[u(line)]%r[ansi(hy,Num%b%bCharacter Name %b%b%b%b%b%bE-mail Address)][setq(0,1)][iter(v(Apps),%r[ansi(hr,rjust(%q0,3))][setq(0,add(%q0,1))]%b%b[ansi(c,ljust(first(##,~),20))] [ansi(g,left(third(##,~),55))],|)]%r%r[ansi(hm,There are [words(v(Apps),|)] total pending character registration(s).)]%r[ansi(hc,Type @register/view <#> for a verbose display of an entry.)]%r[u(Line)]})]}
&VIEW2 @Admin Object=$@reg*/view *:@switch [u(Can_Pcreate)]=0,{@pemit %#=Permission denied.},{@switch [setq(1,extract(v(Apps),%1,1,|))]%q1=, {@pemit %#=[setq(0,words(v(Apps),|))][switch(%q0,0,There are currently no character registrations.,Valid entry numbers range from 1 to %q0.)]}, {@pemit %#=[u(Line)]%r[ansi(hg,center([mudname()] Character Registration,79))]%r[u(Line)]%r[ansi(hr,ljust(Number:,20))] [ansi(hc,add(%1,0))]%r[ansi(hr,ljust(Name:,20))] [ansi(c,first(%q1,~))]%r[ansi(hr,ljust(Password:,20))] [ansi(c,second(%q1,~))]%r[ansi(hr,ljust(E-mail Address:,20))] [ansi(c,third(%q1,~))]%r[ansi(hr,ljust(Registered By:,20))] [ansi(c,fourth(%q1,~))]%r[ansi(hr,ljust(Time:,20))] [ansi(c,fifth(%q1,~))]%r%r[ansi(y,Type @register/process [add(%1,0)] to process (and delete) this registration.)]%r[ansi(y,Type @register/delete [add(%1,0)] to delete this registration without processing.)]%r[u(line)]}
&PROCESS @Admin Object=$@reg*/proc* *:@switch [u(Can_Pcreate)]=0,{@pemit %#=Permission denied.},{@switch [setq(1,extract(v(Apps),%2,1,|))]%q1=, {@pemit %#=[setq(0,words(v(Apps),|))][switch(%q0,0,There are currently no character registrations.,Valid entry numbers range from 1 to %q0.)]}, {@pemit %#=Character Name: [first(%q1,~)]%rRequested Password: [second(%q1,~)]%rE-mail Address: [third(%q1,~)]%rRegistered By: [fourth(%q1,~)]%rTime: [fifth(%q1,~)]%rIf character creation is successful, please e-mail the initial password to the e-mail address above.;@force %#=+pcreate [first(%q1,~)]=[second(%q1,~)]/[third(%q1,~)];&Apps me=[remove(v(Apps),%q1,|)]}}
&DELETE @Admin Object=$@reg*/del* *:@switch [u(Can_Pcreate)]=0,{@pemit %#=Permission denied.},{@switch [setq(1,extract(v(Apps),%2,1,|))]%q1=, {@pemit %#=[setq(0,words(v(Apps),|))][switch(%q0,0,There are currently no character registrations.,Valid entry numbers range from 1 to %q0.)]}, {@pemit %#=Character registration #[add(%2,0)] deleted.;&Apps me=[remove(v(Apps),%q1,|)]}}
&REGISTER @Admin Object=$@register *=*,*:@switch [num(*%0)]=#-1,{&Apps me=[trim([v(Apps)]|%0~%1~%2~%n~[time()],|)];@pemit %#=Character Name: %0. Password: %1. E-Mail Address: %2.%rYour character request has been recorded in the database. Your character will be created with the requested password and a confirmation e-mailed to you within a few days.;@cemit Register=\[Register] %n has requested a character. Name: %0. Password: %1. E-Mail Address: %2.}, {@pemit %#=The name [name(*%0)] is already in use. Please choose another one.}
&REGISTER_HELP @Admin Object=$@reg*/help:@pemit %#=@register <name>=<password>,<email address>%r@reg/view%r@reg/view <registry #>%r@reg/del <registry #>%r@reg/proc <registry #>
@ACONNECT @Admin Object=@switch [and(u(Can_Pcreate),v(Apps))]=1, {@pemit %#=\[Register] There are [words(v(Apps),|)] pending character registration(s). Type @register/view to view them.}
@set @Admin Object/ACONNECT=no_command
&BEEP-1 @Admin Object=$+beep *:@pemit %#=[name(*%0)] has been +beeped.;@pemit *%0=[repeat(=,79)]%r[beep(5)]You have been beeped by [name(%#)]. Please Respond.[beep(5)]%r[repeat(=,79)]
&CBOOT @Admin Object=$@CBoot *=*:@Swi [orflags(%#,Wrj)]=1,{@swi [num(*%0)]=#-1,{@pe %#=#-1 THAT PLAYER DOES NOT EXIST},{@cemit %1=<%1>%b[ansi(hr,[name(*%0)] was Booted from this Channel by [name(%#)].)];@pe %#=[ansi(hr,[name(*%0)] has been removed from channel %1.)];@fo *%0=@Chan/Off %1;@clock/join %1=!*%0;@clock/speak %1=!*%0;@wait 35=@clock/join %1;@wait 35=@clock/speak %1}},{@pe %#=Permission denied.}

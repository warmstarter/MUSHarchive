
################################### bbpocket #############################
#120 is the bbpocket

@Desc #120=[iter(setdiff(lattr(me),Desc),ljust(##,18))]

&BBTIME #120=extract(time(), 1, 3)

&GET_GROUP #120=switch(isnum(%0),1,extract(v(groups),%0,1),locate(#120,%0,ni))

@@ &NXT_MESS #120=1

&VALID_GROUPS #120=iter(v(groups),switch(and(u(##/can%1,%0),not(member(get(%0/bb_omit),##))),1,##))

&ISGROUPREAD #120=switch(setdiff(%1,get(%0/bb_read)),,,U)

&UNREADNUMS #120=sort(iter(setdiff(get(%1/mess_lst),get(%0/bb_read)),member(get(%1/mess_lst),##)))

&FN_MAKELIST #120=map(me/fn_maplist,secure(%0))

@@ &FN_MAPLIST #120=switch(strmatch(%0,?*-*),1,lnum(before(%0,-),after(%0,-)),%0)

&FN_MAPLIST #120=switch(strmatch(%0,*-*),1,u(fn_lnum,before(%0,-),after(%0,-)),%0)

&FN_UNREAD_LIST #120=edit(sort(iter(%0,member(%1,##))),%b,\,%b)

&FN_LNUM #120=iter(lnum(add(sub(%1,%0),1)),add(##,%0))

&ADDEM #120=add(%0,%1)

&B36_DIGITS #120=0 1 2 3 4 5 6 7 8 9 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z

&B36_POWS #120=1 36 1296 46656 1679616 60466176

&FN_B36_MAGS #120=revwords(iter(v(b36_pows),switch([div(##,%0)]:[eq(##,%0)],0:*,##,*:1,##)))

&FN_B36-TO-B10 #120=[setq(0,ucstr(%0))][setq(1,strlen(%q0))][fold(me/addem,[iter(lnum(%q1),mul(sub(member(v(b36_digits),mid(%q0,##,1)),1),power(36,sub(sub(%q1,##),1))))],0)]

&FN_B10-TO-B36 #120=[setq(0,%0)][edit(iter(u(fn_b36_mags,%q0),[extract(v(b36_digits),add(div(%q0,##),1),1)][setq(0,sub(%q0,mul(##,div(%q0,##))))]),%b,)]

&FN_INC_NEXT_MESS #120=u(fn_b10-to-b36,add(u(fn_b36-to-b10,%0),1))

&VALID_CONFIGS #120=anonymous timeout

&VALID_GLOBAL_CONFIGS #120=timeout autotimeout

&CONFIG_TIMEOUT #120=2592000

&TR_TIMEOUT #120=@dolist v(groups)={@trigger me/tr_timeout_group=##}

&TR_TIMEOUT_GROUP #120=@dolist get(%0/mess_lst)={@switch and(extract(get(%0/hdr_##),5,1,|),gt(secs(),extract(get(%0/hdr_##),5,1,|)))=1,{&hdr_## %0; &bdy_## %0; &mess_lst %0=[remove(get(%0/mess_lst),##)]; &master_lst #120=[remove(get(#120/master_lst),##)]}}

&DO_TIMEOUTS #120=@trigger me/tr_timeout; @wait 86400={@trigger me/do_timeouts}

&TR_CONFIG_ANONYMOUS #120=&anonymous %0=[ifelse([%1],[%1],)]; @pemit %2=Posters on the [name(%0)] group will show up as [ifelse([%1],['%1'],themselves)].

&TR_CONFIG_TIMEOUT #120=&config_timeout %0=[mul(%1,86400)]; @pemit %2=New messages for group '[name(%0)]' have [ifelse(%1,a %1 day,no)] timeout.

&TR_GCONFIG_TIMEOUT #120=&config_timeout me=[mul(%0,86400)]; @pemit %1=BB global config parameter 'timeout': [ifelse(%0,%0 days,none)].%rAny new groups will have this value set as their default timeout.

&TR_GCONFIG_AUTOTIMEOUT #120=@switch [eq(hasattr(me,startup),setr(0,switch(%0,on,1,0)))]:%0=1:*,{@pemit %1=BB autotimeout is already [ifelse(%q0,on,off)].},0:on,{@startup me=@trigger me/do_timeouts; @trigger me/do_timeouts; @pemit %1=BB autotimeout turned on.},{@startup me; @pemit %1=BB autotimeout turned off.; @halt me}

&FN_MSG_FLAGS #120=switch([member(get(%0/bb_read),%2)]:[u(fn_timeout_close,index(get(%1/hdr_%2),|,5,1))],0:*,U,*:1,T)

&TR_POST_NOTIFY #120=@switch hasflag(%0,DARK)=0,{@pemit/list iter(remove(setunion(lwho(),lwho()),%0),switch(or(member([get(##/bb_omit)] [get(##/bb_silent)],%1),not(u(%1/canread,##))),0,##))=(New BB message ([member(v(groups),%1)]/%2) posted to '[name(%1)]' by [ifelse(hasattr(%1,anonymous),get(%1/anonymous),name(%0))]: %3)}

&FN_TIMEOUT_CLOSE #120=switch(%0:[lte(sub(%0,secs()),259200)],0:*,0,*:1,1,0)

&FN_HASCONFIG #120=or(hasattr(%0,anonymous),neq(get(%0/config_timeout),get(#120/config_timeout)))

&VERSION #120=4.0.6

&CMD_BB/NEWGROUP #119=$@bb/newgroup *:@switch hasflag(%#,wizard)=1, {@create %0; @wait 1={@switch [setr(0,num(%0))]=#-1,{@pemit %#=That's not a good name for a group.},{&groups #120=[switch(words(get(#120/groups)),0,,[get(#120/groups)]%b)]%q0; &own %q0=%#; @set %q0=safe; @set %q0=inherit; &last_mod %q0=[u(#120/bbtime)]; &CANREAD %q0=1; &CANWRITE %q0=1; &config_timeout %q0=[get(#120/config_timeout)]; @pemit %#=Group number [member(get(#120/groups),%q0)] added as '%0'. Messages will have [ifelse(get(%q0/config_timeout),a [div(get(%q0/config_timeout),86400)] day,no)] timeout.}}}, {@pemit %#=You can't add groups to the message base.}

&CMD_BB/LOCK #119=$@bb/lock *=*/*:@switch hasflag(%#,wizard)=1,{@switch member(get(#120/groups),setr(0,u(#120/get_group,%0)))=0,{@pemit %#=No such group as '%0'.},{@switch %1=flag,{&CANREAD %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANREAD %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @wait 1={&CANWRITE %q0=[get(%q0/CANREAD)]}; @pemit %#=Group '%0' locked. Only people with %1=%2 can access.}},{@pemit %#=You cannot lock message groups.}

&CMD_BB/CLEARGROUP #119=$@bb/cleargroup *:@switch and(hasflag(%#, wizard),member(get(#120/groups),setr(0,u(#120/get_group,%0))))=1, {&delete_grp #120=%q0; @pemit %#=%r[space(15)]******** Warning ********%r%rDeleting Group #%0 will also delete the [words(get(%q0/mess_lst))] messages in that group.%rIf you still wish to proceed, type '@bb/confirm %0'%r%r[space(15)][repeat(*,25)]%r}, {&delete_grp #120; @pemit %#=You can't remove that group (either that group does not exist, or you are not the owner).}

&DO_CLEARGROUP #119=$@bb/confirm *:@switch get(#120/DELETE_GRP)=[setr(0,u(#120/get_group,%0))], {&groups #120=[remove(get(#120/groups),%q0)]; &master_lst #120=[setdiff(get(#120/master_lst),get(%q0/mess_lst))]; @nuke %q0; &delete_grp #120; @pemit %#=Group number %0 removed.}, {&delete_grp #120; @pemit %#=That group has not been marked for deletion. Use '@bb/cleargroup <#>' to mark a group for deletion.}

&CMD_BB/POST #119=$@bb/post */*=*:@switch member(u(#120/valid_groups,%#,write),setr(0,u(#120/get_group,%0)))=0, {@pemit %#=Either you do not subscribe to Group #%0, or are unable to post to it.},{&mess_lst %q0=[cat(get(%q0/mess_lst),setr(1,get(#120/nxt_mess)))]; &nxt_mess #120=[ulocal(#120/fn_inc_next_mess,%q1)]; &master_lst #120=[cat(get(#120/master_lst),%q1)]; &hdr_%q1 %q0=[mid(%1, 0, 34)]|[u(#120/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[ifelse(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)]; &last_mod %q0=[u(#120/bbtime)]; &bdy_%q1 %q0=[%2][ifelse(and(hasattr(%#,bb_sig),not(hasattr(%q0,anonymous))),{%r[ulocal(%#/bb_sig)]},)]; @pemit %#=You post your note about '%1' in group [member(get(#120/groups),%q0)] ([name(%q0)]) as message #[member(get(%q0/mess_lst),%q1)][ifelse(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group, your BB_SIG was -not- appended.},)]; &bb_read %#=[setunion(get(%#/bb_read),%q1)]; @trigger #120/tr_post_notify=%#,%q0,[member(get(%q0/mess_lst),%q1)],[mid(%1, 0, 34)]}

&CMD_BB/REMOVE #119=$@bb/remove */*:@dolist [setq(0,u(#120/get_group,%0))][revwords(sort(u(#120/fn_makelist,%1)))]={@switch or(strmatch(index(get(%q0/HDR_[setr(1,extract(get(%q0/MESS_LST),##,1))]),|,4, 1),%#), hasflag(%#,wizard))=1, {&HDR_%q1 %q0; &BDY_%q1 %q0; &MESS_LST %q0=[remove(get(%q0/MESS_LST),%q1)]; &MASTER_LST #120=[remove(get(#120/MASTER_LST),%q1)]; @pemit %#=Message ## removed from group #[member(get(#120/groups),%q0)] ([name(%q0)]).}, {@pemit %#=Either message ## does not exist, or you were not the original poster.}}

&CMD_BB/READ2 #119=$@bb/read *:@switch strmatch([%0],*/*)=0,{@switch member(u(#120/valid_groups,%#,read),setr(0,u(#120/get_group,%0)))=0,{@pemit %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},{@pemit %#=[repeat(=,78)]%r[center(**** [name(%q0)] ****,78)]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[repeat(-,78)]; @dolist get(%q0/MESS_LST)={@pemit %#=[ljust([member(get(#120/groups),%q0)]/[member(get(%q0/MESS_LST),##)],6)][ljust(u(#120/fn_msg_flags,%#,%q0,##),2)][ljust(index(setr(1,get(%q0/HDR_##)),|,1,1),35)][ljust(index(%q1,|,2,1),14)][mid([index(%q1,|,3,1)][ifelse(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q1,|,4,1))]\),)],0,21)][switch(member(get(%q0/MESS_LST),##),words(get(%q0/MESS_LST)),%r[repeat(=,78)])]}}},{@switch member(u(#120/valid_groups,%#,read),setr(0,u(#120/get_group,setr(2,first(%0,/)))))=0,{@pemit %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},{@dolist [setq(4,member(get(#120/groups),%q0))][switch(rest(%0,/),u,u(#120/unreadnums,%#,%q0),u(#120/fn_makelist,rest(%0,/)))]={@switch [and(lte(##,words(get(%q0/MESS_LST))),isnum(##),gt(##,0))]=0,{@pemit %#=Message %q4/## ([name(%q0)]/##) does not exist.},{@pemit %#=%r[center(= [name(%q0)] =,78,=)]%r[ljust(Message: %q4/##[switch(u(#120/fn_timeout_close,index(setr(3,get(%q0/HDR_[extract(get(%q0/MESS_LST),##,1)])),|,5,1)),1,{%b(timeout warning)})],35)]Posted[space(8)]Author%r[ljust(index(%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)][ifelse(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q3,|,4,1))]\),)],0,29)]%r[repeat(-,78)]%r[get(%q0/BDY_[extract(get(%q0/MESS_LST),##,1)])]%r[repeat(=,78)]; &bb_read %#=[setunion(get(%#/bb_read),[extract(get(%q0/MESS_LST),##,1)])]}}; @wait 2={&bb_read %#=[setinter(get(%#/bb_read),get(#120/master_lst))]}}}

&CMD_BB/EDIT #119=$@bb/edit */*=*/*:@switch strmatch(index(get([setq(0,u(#120/get_group,%0))]%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),%1,1))]%q1),|,4, 1),%#)=1,{&BDY_%q1 %q0=[edit(get(%q0/BDY_%q1),{%2},{%3})]; @pemit %#=[repeat(=,78)]%rMessage [member(get(#120/groups),%q0)]/%1 ([name(%q0)]/%1) now reads:%r[repeat(-,78)]%r[get(%q0/BDY_%q1)]%r[repeat(=,78)]}, {@pemit %#=Either that message does not exist, or you were not the original poster.}

&CMD_BB/LEAVE #119=$@bb/leave *:@switch member(u(#120/valid_groups,%#,read),[setq(0,u(#120/get_group,%0))]%q0)=0,{@pemit %#=You aren't currently subscribing to Board #%0. No ommission necessary.},{&bb_omit %#=[setunion(get(%#/bb_omit),%q0)]; @pemit %#=You have removed yourself from the [name(%q0)] board.}

&CMD_BB/JOIN #119=$@bb/join *:@switch member(NULL [get(%#/bb_omit)],[setq(0,u(#120/get_group,%0))]%q0)=0,{@pemit %#=[switch(member(u(#120/valid_groups,%#,read),%q0),0,{Sorry, you don't have access to that board.},{You are already a member of the [name(%q0)] board.})]},{&bb_omit %#=[setdiff(get(%#/bb_omit),%q0)]; @pemit %#=You have joined the [name(%q0)] board.}

&CMD_BB/SCAN #119=$@bb/scan: @switch hasattr(#120, bb_post_hdr_%#) = 1, {@pemit %# = ** BB Warning: You are in the middle of writing a bbpost. **}; @switch words(setdiff(iter(u(#120/valid_groups, %#, read), get(##/mess_lst)), get(%#/bb_read))) = 0, {@pemit %# = There are no unread postings on the Global Bulletin Board.},{@pemit %# = [center(- Unread Postings on Global Bulletin Board -,78,-)]; @dolist [setq(0, u(#120/valid_groups, %#, read))][setq(1, get(%#/bb_read))]%q0 END = {@switch ## = END, {@notify me},{@switch [setr(2, words(setdiff(get(##/mess_lst), %q1)))] = 0, {@@ nothing @@},{@pemit %# = [rjust(member(get(#120/groups), ##),5)] [ljust(name(##),23)]has[rjust(%q2,4)] unread postings.}}}; @wait me = {@pemit %# = [center(- BBS at [round(mul(fdiv(strlen(get(#120/master_lst)), 8000), 100), 1)]\% capacity -, 78, -)]}}

@@ $@bb/scan: @pemit %#=[switch(hasattr(#120,BB_POST_HDR_%#),1,** BB Warning: You are in the middle of writing a bbpost. **%r)][switch(setdiff(iter(setr(0,u(#120/VALID_GROUPS,%#,read)),get(##/MESS_LST)),setr(1,get(%#/BB_READ))),,There are no unread postings on the Global Bulletin Board.,[center(- Unread Postings on the Global Bulletin Board -,78,-)][setq(9,get(#120/GROUPS))][trim(iter(%q0,switch(setr(2,setdiff(setr(3,get(##/MESS_LST)),%q1)),,,%r[name(##)] \(#[member(%q9,##)]\): [words(%q2)] unread \([u(#120/FN_UNREAD_LIST,%q2,%q3)]\))),b)]%r[center(- BBS at [round(mul(fdiv(strlen(get(#120/MASTER_LST)),get(#120/BUFFER_SIZE)),100),1)]\\% capacity -,78,-)])]

&CMD_BB/SCAN2 #119=$@bb: @switch hasattr(#120, bb_post_hdr_%#) = 1, {@pemit %# = ** BB Warning: You are in the middle of writing a bbpost. **}; @switch words(setdiff(iter(u(#120/valid_groups, %#, read), get(##/mess_lst)), get(%#/bb_read))) = 0, {@pemit %# = There are no unread postings on the Global Bulletin Board.},{@pemit %# = [center(- Unread Postings on Global Bulletin Board -,78,-)]; @dolist [setq(0, u(#120/valid_groups, %#, read))][setq(1, get(%#/bb_read))]%q0 END = {@switch ## = END, {@notify me},{@switch [setr(2, words(setdiff(get(##/mess_lst), %q1)))] = 0, {@@ nothing @@},{@pemit %# = [rjust(member(get(#120/groups), ##),5)] [ljust(name(##),23)]has[rjust(%q2,4)] unread postings.}}}; @wait me = {@pemit %# = [center(- BBS at [round(mul(fdiv(strlen(get(#120/master_lst)), 8000), 100), 1)]\% capacity -, 78, -)]}}

@@ @pemit %#=[switch(hasattr(#120,BB_POST_HDR_%#),1,** BB Warning: You are in the middle of writing a bbpost. **%r)][switch(setdiff(iter(setr(0,u(#120/VALID_GROUPS,%#,read)),get(##/MESS_LST)),setr(1,get(%#/BB_READ))),,There are no unread postings on the Global Bulletin Board.,[center(- Unread Postings on the Global Bulletin Board -,78,-)][setq(9,get(#120/GROUPS))][trim(iter(%q0,switch(setr(2,setdiff(setr(3,get(##/MESS_LST)),%q1)),,,%r[name(##)] \(#[member(%q9,##)]\): [words(%q2)] unread \([u(#120/FN_UNREAD_LIST,%q2,%q3)]\))),b)]%r[center(- BBS at [round(mul(fdiv(strlen(get(#120/MASTER_LST)),get(#120/BUFFER_SIZE)),100),1)]\\% capacity -,78,-)])]

&CMD_BB/LIST #119=$@bb/list:@pemit %#=[repeat(=,78)]%r[ljust(Available Bulletin Board Groups,37)]Member?[space(8)]Timeout (in days)%r[repeat(-,78)]%r%b[iter(get(#120/groups),switch(u(##/canread,%#),1,{%r%b[trim([ljust(member(get(#120/groups),##),5)][ljust(name(##),32)][ljust(switch(member(get(%#/bb_omit),##),0,Yes,No),19)][rjust(ifelse(get(##/config_timeout),div(get(##/config_timeout),86400),none),4)])]}))]%r[repeat(-,78)]%rTo join groups, type '@bb/join <group number or name>'%r[repeat(=,78)]

&CREDITS #119=Myrddin's BBS was written by Myrddin@Witchcraft/Dreaming/Elysium (email: merlin@firstmagic.com). The most recent version of this code can be found at http://www.firstmagic.com/~merlin/mushcode, or via anonymous ftp from ftp.firstmagic.com in the /pub/merlin directory.  Please refer interested parties to myself or those addresses, thank you. :)

&CMD_BB/MOVE #119=$@bb/move */* to *:@switch and(member(u(#120/valid_groups,%#,read),[setq(0,u(#120/get_group,%0))]%q0),member(u(#120/valid_groups,%#,write),[setq(2,u(#120/get_group,%2))]%q2),not(strmatch(%q0,%q2)))=0,{@pemit %#=One of the selected groups is not valid (either it doesn't exist, you can't write to it, or you tried to 'move' to the same group).},{@switch or(strmatch(index(get(%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),%1,1))]%q1),|,4, 1),%#), and(hasflag(%#,wizard),member(get(%q0/MESS_LST),%q1)))=1,{&HDR_%q1 %q2=[get(%q0/HDR_%q1)]; &BDY_%q1 %q2=[get(%q0/BDY_%q1)]; &MESS_LST %q2=[cat(get(%q2/mess_lst),%q1)]; &MESS_LST %q0=[remove(get(%q0/MESS_LST),%q1)]; &HDR_%q1 %q0; &BDY_%q1 %q0; @pemit %#=Message '%1' removed from group '%0' and added to group '%2' as message #[member(get(%q2/mess_lst),%q1)]},{@pemit %#=Not a valid message number for that group.}}

&CMD_BB/WRITELOCK #119=$@bb/writelock *=*/*:@switch hasflag(%#,wizard)=1,{@switch member(get(#120/groups),[setq(0,u(#120/get_group,%0))]%q0)=0,{@pemit %#=No such group as '%0'.},{@switch %1=flag,{&CANWRITE %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANWRITE %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @pemit %#=Group '%0' locked. Only people with %1=%2 can write.}},{@pemit %#=You cannot lock message groups.}

&CMD_BB/POST2 #119=$@bb/post *=*:@switch hasattr(#120,bb_post_hdr_%#)=1,{@pemit %#=You are already in the middle of writing a bbpost.},{@switch or(strmatch(%0,*/*),strmatch(%1,*=*))=0,{@switch member(u(#120/valid_groups,%#,write),[setq(0,u(#120/get_group,%0))]%q0)=0,{@pemit %#=Either you do not subscribe to Group #%0 or are unable to post to it.},{&bb_post_hdr_%# #120=%q0|[mid(%1,0,34)]; @pemit %#=%rYou start your posting to Group #[member(get(#120/groups),%q0)] ([name(%q0)]).%rYou can now compose the body of the post by using '@bb/write <text>'%ror '@bb <text>'.  When you are finished, type '@bb/post' by itself.[ifelse(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group. Your BB_SIG will -not- be appended.},)]}}}

&CMD_BB/WRITE #119=$@bb/write *:@switch hasattr(#120,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# #120=[get(#120/bb_post_bdy_%#)]%0; @pemit %#=Text added to bbpost.}

&CMD_BB/PROOF #119=$@bb/proof:@switch hasattr(#120,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{@pemit %#=[center(= BB Post in Progress =,78,=)]%rGroup: %b[name(index(get(#120/bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(get(#120/bb_post_hdr_%#),|,2,1)]%r[repeat(-,78)]%r[trim(get(#120/bb_post_bdy_%#))]%r[repeat(=,78)]}

&CMD_BB/POST-POST #119=$@bb/post:@switch [hasattr(#120,bb_post_hdr_%#)]:[hasattr(#120,bb_post_bdy_%#)]=0:0,{@pemit %#=You do not have a bbpost in progress.},1:0,{@pemit %#=Your post is empty. Please add text with the '@bb/write <text>' command or discard the posting with the '@bb/toss' command.},{&mess_lst [setr(0,index(get(#120/bb_post_hdr_%#),|,1,1))]=[cat(get(%q0/mess_lst),setr(1,get(#120/nxt_mess)))]; &nxt_mess #120=[ulocal(#120/fn_inc_next_mess,%q1)]; &master_lst #120=[cat(get(#120/master_lst),%q1)]; &hdr_%q1 %q0=[index(get(#120/bb_post_hdr_%#),|,2,1)]|[u(#120/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[ifelse(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)]; &last_mod %q0=[u(#120/bbtime)]; &bdy_%q1 %q0={[trim(get(#120/bb_post_bdy_%#))][ifelse(and(hasattr(%#,bb_sig),not(hasattr(%q0,anonymous))),{%r[ulocal(%#/bb_sig)]},)]}; @pemit %#=You post your note about '[index(get(#120/bb_post_hdr_%#),|,2,1)]' in group '[name(%q0)]' as message #[member(get(%q0/mess_lst),%q1)]; &bb_read %#=[setunion(get(%#/bb_read),%q1)]; @trigger #120/tr_post_notify=%#,%q0,[member(get(%q0/mess_lst),%q1)],[index(get(#120/bb_post_hdr_%#),|,2,1)]; &bb_post_bdy_%# #120; &bb_post_hdr_%# #120}

&CMD_BB/READ #119=$@bb/read:@pemit %#=[repeat(=,78)]%r[space(7)]Group Name[space(20)]Last Post[space(6)]# of messages%r[repeat(-,78)][iter(u(#120/valid_groups,%#,read),%r[rjust(member(get(#120/groups),##),2)][center([switch([get(##/CANREAD)]:[get(##/CANWRITE)]:[u(##/CANWRITE,%#)],1:1:1,,1:*:1,{(-)},1:*:0,-,*)],5)][ljust(name(##),30)][ljust(get(##/LAST_MOD),20)][rjust(words([setq(0,get(##/MESS_LST))]%q0),3)]%b[ljust(u(#120/isgroupread,%#,%q0),2)])]%r[repeat(-,78)]%r[center('*' = restricted[space(5)]'-' = read only[space(5)]'\(-\)' = read only\, but you can write,78)]%r[center(=[switch(hasflag(%#,wizard),1,{= BBS at [round(mul(fdiv(strlen(get(#120/master_lst)),get(#120/buffer_size)),100),1)]\\% capacity =},=)]=,78,=)]

&cmd_bb/abort #119=$@bb/abort:@fo %#=@bb/toss

&CMD_BB/TOSS #119=$@bb/toss:@switch hasattr(#120,bb_post_hdr_%#)=0,{@pemit %#=You do not have bbpost in progress.},{&bb_post_hdr_%# #120; &bb_post_bdy_%# #120; @pemit %#=Your bbpost has been discarded.}

&CMD_BB #119=$@bb *:@switch hasattr(#120,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# #120=[get(#120/bb_post_bdy_%#)]%b%0; @pemit %#=Text added to bbpost.}

&CMD_BB/EDIT2 #119=$@bb/edit *=*/*:@switch strmatch([setq(0,ucstr(%0))]%q0,*/*)=0,{@switch %q0=TEXT,{@edit #120/bb_post_bdy_%#={%1},{%2}},TITLE,{&bb_post_hdr_%# #120=[index(get(#120/bb_post_hdr_%#),|,1,1)]|[edit(index(get(#120/bb_post_hdr_%#),|,2,1),{%1},{%2})]}; @wait 0={@pemit %#=[center(= BB Post in Progress =,78,=)]%rGroup: %b[name(index(get(#120/bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(get(#120/bb_post_hdr_%#),|,2,1)]%r[repeat(-,78)]%r[trim(get(#120/bb_post_bdy_%#))]%r[repeat(=,78)]}}

&CMD_BB/HELP #119=$@bb/help:@pemit %#=Please use 'help @bb'.

&CMD_BB/CONFIG #119=$@bb/config:@pemit %#=[center(= Myrddin's Global BBS v[get(#120/version)] =,78,=)]%rGlobal Config Parameters:%r%r[rjust(timeout:,15)] [div(get(#120/config_timeout),86400)] days%r[rjust(autotimeout:,15)] [ifelse(hasattr(#120,startup),on,off)]%r[repeat(-,78)]%rBoard Config Parameters:%r%r%b[iter(filter(#120/fn_hasconfig,get(#120/groups)),%b%b[name(##)]%r[ifelse(hasattr(##,anonymous),[space(5)]anonymous: [get(##/anonymous)]%r,)][ifelse(neq(get(##/config_timeout),get(#120/config_timeout)),[space(5)]timeout: [div(get(##/config_timeout),86400)] days%r,)]%r)][repeat(=,78)]

&CMD_BB/CONFIG2 #119=$@bb/config *=*:@switch [strmatch(%0,*/*)]:[hasflag(%#,wizard)]=1:*,{},0:0,{@pemit %#=Huh?%b%b(Type "help" for help.)},{@switch [member(get(#120/valid_global_configs),lcstr(%0))]=0,{@pemit %#=Valid global config parameters are: [get(#120/valid_global_configs)]},{@trigger #120/tr_gconfig_%0=%1,%#}}

&CMD_BB/CONFIG3 #119=$@bb/config */*=*:@switch/first hasflag(%#,wizard)=0,{@pemit %#=Huh?%b%b(Type "help" for help.)},{@switch [setr(0,u(#120/get_group,%0))]:[member(get(#120/valid_configs),lcstr(%1))]=#-1:*,{@pemit %#='%0' is not a valid group name.},#-2:*,{@pemit %#='%0' is not specific enough.},*:0,{@pemit %#=Valid config parameters are: [get(#120/valid_configs)]},{@trigger #120/tr_config_%1=%q0,%2,%#}}

&CMD_BB/TIMEOUT #119=$@bb/timeout */*=*:@dolist [sort(u(#120/fn_makelist,%1))][setq(0,u(#120/get_group,%0))]={@switch [or(strmatch(index(get(%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),##,1))]%q1),|,4, 1),%#), hasflag(%#,wizard))]:[and(isnum(%2),or(and(gt(%2,0),lte(mul(%2,86400),get(%q0/config_timeout))),and(not(get(%q0/config_timeout)),gte(%2,0)),hasflag(%#,wizard)))]=1:1, {&hdr_%q1 %q0=[replace(get(%q0/hdr_%q1),5,ifelse(%2,add(secs(),mul(%2,86400)),0),|)]; @pemit %#=Message ## in group '[name(%q0)]' has [ifelse(%2,a %2 day,no)] timeout.}, 0:*,{@pemit %#=Either message ## does not exist, or you were not the original poster.},*:0,{@pemit %#=Sorry, '%2' is not a valid number of days for a timeout on the '[name(%q0)]' board. [ifelse(get(%q0/config_timeout),Maximum timeout is [div(get(%q0/config_timeout),86400)] days.,)]}}
-
# Returns the timeout (in days) of the post(s) entered for the board entered.

&cmd_bb/timeoutcheck #119=$@bb/timeoutcheck */*:
think setq(0, u(#120/get_group, %0)); 
think setq(1, sort(u(#120/fn_makelist, %1))); 
@dolist %q1={
  @pemit %#=ifelse(or(strmatch(index(get(%q0/HDR_
                               [setr(2,extract(get(%q0/MESS_LST),##,1))]),
                               |,4, 1),%#), 
                      hasflag(%#,wizard)), 
             Message ## in group '[name(%q0)]' has 
               [ifelse(setr(3,extract(get(%q0/hdr_%q2),5,1,|)), 
                       a [fdiv(sub(%q3, secs()), 86400)] day, 
                       no)] timeout., 
             Either message ## does not exist%, 
               or you were not the original poster.)}
-
# Old @bb/search -> @bb/author with a few modifications.

&CMD_BB/AUTHOR #119=$@bb/author */*: @switch 
  [setq(0,u(#120/get_group,%0))]
  [t(member(u(#120/valid_groups,%#,read),%q0))]:
#    [isdbref(setr(0, u(#120/get_group, %0)))]:
  [hasattr(%q0, anonymous)]:
  [isdbref(setr(1, locate(%#, *%1, p)))] =
0:*:*, {@pemit %# = You do not subscribe to that Group.},
1:1:*, {@pemit %# = BB Group '[name(%q0)]' is an anonymous group.},
1:0:0, {@pemit %# = No such player '%1'.},
{@switch words(setr(2, munge(munge-sort-num, map(map-bb-num,
    [setr(3,grep(%q0, hdr_*, |%q1|))]),%q3))) =
  0, {@pemit %# = [name(%q1)] hasn't posted any messages to the
    '[name(%q0)]' Group.},
  {@pemit %# = %r[repeat(=, 78)]%r[center(**** [name(%q0)] ****, 78)]%r
    [space(8)]Message[space(28)]Posted[space(8)]By%r[repeat(-, 78)];
    @dolist %q2 = {@pemit %# = [ljust([member(get(#120/groups), %q0)]/
      [member(get(%q0/mess_lst), last(##, _))], 6)]
      [ljust(u(#120/fn_msg_flags, %#, %q0, last(##, _)), 2)]
      [ljust(index([setr(3, get(%q0/##))], |, 1, 1), 35)]
      [ljust(index(%q3, |, 2, 1), 14)][mid([index(%q3, |, 3, 1)], 0, 21)]
      [switch(member(%q2, ##), words(%q2), %r[repeat(=, 78)])]}}}

-
# New @bb/search

&CMD_BB/search #119=$@bb/search */*: @switch 
  [setq(0,u(#120/get_group,%0))]
  [t(member(u(#120/valid_groups,%#,read),%q0))]:
#    [isdbref(setr(0, u(#120/get_group, %0)))]:
  [hasattr(%q0, anonymous)] =
0:*, {@pemit %# = You do not subscribe to that Group.},
1:1, {@pemit %# = BB Group '[name(%q0)]' is an anonymous group.},
{@switch words(setr(2, munge(munge-sort-num,
    map(map-bb-num,[setr(3,setunion(grepi(%q0,hdr_*,%1),
      edit(grepi(%q0,bdy_*,%1),BDY_,HDR_)))]),%q3))) =
  0, {@pemit %# = There are no posts with '%1' in them on the
    '[name(%q0)]' Group.},
  {@pemit %# = %r[repeat(=, 78)]%r[center(**** [name(%q0)] ****, 78)]%r
    [space(8)]Message[space(28)]Posted[space(8)]By%r[repeat(-, 78)];
    @dolist %q2 = {@pemit %# = [ljust([member(get(#120/groups), %q0)]/
      [member(get(%q0/mess_lst), last(##, _))], 6)]
      [ljust(u(#120/fn_msg_flags, %#, %q0, last(##, _)), 2)]
      [ljust(index([setr(3, get(%q0/##))], |, 1, 1), 35)]
      [ljust(index(%q3, |, 2, 1), 14)][mid([index(%q3, |, 3, 1)], 0, 21)]
      [switch(member(%q2, ##), words(%q2), %r[repeat(=, 78)])]}}}
-
# Old @bb/search

@@ &CMD_BB/SEARCH #119=$@bb/search */*:@switch [member(u(#120/valid_groups,%#,write),setr(0,u(#120/get_group,%0)))]:[hasattr(%q0,anonymous)]:[isdbref(setr(1,locate(%#,*%1,p)))]=0:*:*,{@pemit %#=BB Group '%0' is either an invalid Group, an unreadable Group, or a Group you do not subscribe to.},1:1:*,{@pemit %#=BB Group '[name(%q0)]' is an anonymous group.},1:0:0,{@pemit %#=No such player '%1'.},{@switch words(setr(2,grep(%q0,hdr_*,|%q1|)))=0,{@pemit %#=[name(%q1)] hasn't posted any messages to the '[name(%q0)]' Group.},{@pemit %#=%r[repeat(=,78)]%r[center(**** [name(%q0)] ****,78)]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[repeat(-,78)]; @dolist %q2={@pemit %#=[ljust([member(get(#120/groups),%q0)]/[member(get(%q0/mess_lst),last(##,_))],6)][ljust(u(#120/fn_msg_flags,%#,%q0,last(##,_)),2)][ljust(index([setr(3,get(%q0/##))],|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)],0,21)][switch(member(%q2,##),words(%q2),%r[repeat(=,78)])]}}}

&CMD_BB/SEARCH_NOGREP #119=$@bb/search */*:@switch [member(u(#120/valid_groups,%#,write),setr(0,u(#120/get_group,%0)))]:[hasattr(%q0,anonymous)]:[isdbref(setr(1,locate(%#,*%1,p)))]=0:*:*,{@pemit %#=You do not subscribe to that Group.},1:1:*,{@pemit %#=BB Group '[name(%q0)]' is an anonymous group.},1:0:0,{@pemit %#=No such player '%1'.},{@switch words(setr(2,u(#120/fn_grep,%q0,hdr_*,|%q1|)))=0,{@pemit %#=[name(%q1)] hasn't posted any messages to the '[name(%q0)]' Group.},{@pemit %#=%r[repeat(=,78)]%r[center(**** [name(%q0)] ****,78)]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[repeat(-,78)]; @dolist %q2={@pemit %#=[ljust([member(get(#120/groups),%q0)]/[member(get(%q0/mess_lst),last(##,_))],6)][ljust(u(#120/fn_msg_flags,%#,%q0,last(##,_)),2)][ljust(index([setr(3,get(%q0/##))],|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)],0,21)][switch(member(%q2,##),words(%q2),%r[repeat(=,78)])]}}}

&CMD_BB/NOTIFY #119=$@bb/notify *=*:@switch/first [member(u(#120/valid_groups,%#,read),setr(0,u(#120/get_group,%0)))]:[member(on off,lcstr(%1))]:[lcstr(%1)]=0:*:*,{@pemit %#=[switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)]},*:0:*,{@pemit %#=Invalid choice '%1'. Choices are 'on' or 'off'.},*:*:off,{&bb_silent %#=[setunion(get(%#/bb_silent),%q0)]; @pemit %#=Post notification for BB Group '[name(%q0)]' turned off. You will no longer be notified of new postings to that Group.},{&bb_silent %#=[setdiff(get(%#/bb_silent),%q0)]; @pemit %#=Post notification for BB Group '[name(%q0)]' turned on. You will now be notified of new postings to that Group.}

&cmd-bb-unall #119=$@bb/unall: @switch [hasattr(%#,BB_READ_BACKUP)]=1,{ &BB_READ %#=[get(%#/BB_READ_BACKUP)] ; &BB_READ_BACKUP %# ; @pemit %#=Bboards have been reset to before your last @bb/all or @bb/catchup. },{ @pemit %#=There is no bboard undo information stored for you! }

&cmd-bb-all #119=$@bb/all: &BB_READ_BACKUP %#=[get(%#/BB_READ)] ; think setq(1, u(#120/valid_groups, %#, read)); @dolist %q1 = {@force %# = @bb/read [name(##)]/u}; @pemit %#=ifelse(t(filter(#119/filter-hasunread,%q1)),,I'm sorry%, but you seem to have run out of bb reading material. May I suggest RP or the news files?)

&cmd_bb/catchup #119=$@bb/catchup *:@switch lcstr(%0)=all,{ &bb_read_backup %#=[get(%#/bb_read)] ; &bb_read %#=[get(#120/master_lst)]; @pemit %#=All postings on all boards marked as read.},{@dolist [setq(1,u(#120/valid_groups,%#,read))][u(#120/fn_makelist,%0)]={@switch member(%q1,[setq(0,u(#120/get_group,##))]%q0)=0,{@pemit %#=You do not subscribe to Group ##.},{ &bb_read_backup %#=[get(%#/bb_read)] ; &bb_read %#=[setunion(get(%#/bb_read),get(%q0/MESS_LST))]; @pemit %#=All postings on Group #[member(get(#120/groups),%q0)] ([name(%q0)]) marked as read.}}}

&CMD_BB-POSTAS #119=$@bb/postas *=*/*=*:think setq(9,pmatch(%0));@switch [strmatch(%q9,#-1)]=1,{ @pemit %#=No such user '%0'! }, {@switch member(u(#120/valid_groups,%#,write),setr(0,u(#120/get_group,%1)))=0, {@pemit %#=Either you do not subscribe to Group #%1, or are unable to post to it.},{&mess_lst %q0=[cat(get(%q0/mess_lst),setr(1,get(#120/nxt_mess)))]; &nxt_mess #120=[ulocal(#120/fn_inc_next_mess,%q1)]; &master_lst #120=[cat(get(#120/master_lst),%q1)]; &hdr_%q1 %q0=[mid(%2, 0, 34)]|[u(#120/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[ifelse(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)];&last_mod %q0=[u(#120/bbtime)]; &bdy_%q1 %q0=[%3]; @pemit %#=You post your note about '%1' in group [member(get(#120/groups),%q0)] ([name(%q0)]) as message #[member(get(%q0/mess_lst),%q1)][ifelse(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group, your BB_SIG was -not- appended.},)]; &bb_read %#=[setunion(get(%#/bb_read),%q1)]; @trigger #120/tr_post_notify=%#,%q0,[member(get(%q0/mess_lst),%q1)],[mid(%1, 0, 34)]}}

&cmd_bb/new #119=$@bb/new: think setq(0, first(filter(#119/filter-hasunread,u(#120/valid_groups,%#,READ)))); @switch [strmatch(%q0,)]=1,{ @pemit %#=No more unread posts.}, { think setq(9, first(u(#120/unreadnums, %#, %q0))); think setq(4, member(get(#120/groups), %q0)); @dolist %q9={ @pemit %#=%r[center(= [name(%q0)] =,78,=)]%r[ljust(Message: %q4/##[switch(u(#120/fn_timeout_close,index(setr(3,get(%q0/HDR_[extract(get(%q0/MESS_LST),##,1)])),|,5,1)),1,{%b(timeout warning)})],35)]Posted[space(8)]Author%r[ljust(index(%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)][ifelse(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q3,|,4,1))]\),)],0,29)]%r[repeat(-,78)]%r[get(%q0/BDY_[extract(get(%q0/MESS_LST),##,1)])]%r[repeat(=,78)]; &bb_read %# = [setunion(get(%#/bb_read),[extract(get(%q0/MESS_LST),##,1)])]}}

&filter-hasunread #119=t(words(u(#120/unreadnums, %#, %0)))

&cmd_bb/new-bb #119=$@bb/new *: think setq(0, u(#120/valid_groups, %#, READ)); think setq(2, u(#120/get_group, %0)); think setq(1, filter(#119/filter-hasunread, %q2)); @switch [gt(match(%q0, %q2), 0)][strmatch(%q1,)]=0?, {@pemit %#=You do not subscribe to that board.}, 11, {@pemit %#=No more unread posts.}, 10, {think setq(9, first(u(#120/unreadnums, %#, %q2))), {@pemit %#=An error has occured. Please let an admin know you typed @bb/new %0}; think setq(4, member(get(#120/groups), %q2)); @dolist %q9={ @pemit %#=%r[center(= [name(%q2)] =,78,=)]%r[ljust(Message: %q4/##[switch(u(#120/fn_timeout_close,index(setr(3,get(%q2/HDR_[extract(get(%q2/MESS_LST),##,1)])),|,5,1)),1,{%b(timeout warning)})],35)]Posted[space(8)]Author%r[ljust(index(%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)][ifelse(and(hasattr(%q2,anonymous),hasflag(%#,wizard)),%b\([name(index(%q3,|,4,1))]\),)],0,29)]%r[repeat(-,78)]%r[get(%q2/BDY_[extract(get(%q2/MESS_LST),##,1)])]%r[repeat(=,78)]; &bb_read %# = [setunion(get(%#/bb_read),[extract(get(%q2/MESS_LST),##,1)])]}}

<!DOCTYPE html>
<html lang="en" dir="ltr" class="client-nojs">
<head>
<meta charset="UTF-8" /><title>Mushpp - Gwen Morse's Wiki</title>
<meta name="generator" content="MediaWiki 1.22.8" />
<link rel="shortcut icon" href="/favicon.ico" />
<link rel="search" type="application/opensearchdescription+xml" href="/wiki/opensearch_desc.php" title="Gwen Morse's Wiki (en)" />
<link rel="EditURI" type="application/rsd+xml" href="http://www.gwen-morse.com/wiki/api.php?action=rsd" />
<link rel="alternate" type="application/atom+xml" title="Gwen Morse's Wiki Atom feed" href="/wiki/index.php?title=Special:RecentChanges&amp;feed=atom" />
<link rel="stylesheet" href="http://www.gwen-morse.com/wiki/load.php?debug=false&amp;lang=en&amp;modules=mediawiki.legacy.commonPrint%2Cshared%7Cskins.vector&amp;only=styles&amp;skin=vector&amp;*" />
<meta name="ResourceLoaderDynamicStyles" content="" />
<style>a:lang(ar),a:lang(ckb),a:lang(kk-arab),a:lang(mzn),a:lang(ps),a:lang(ur){text-decoration:none}
/* cache key: homesitewiki-mw_:resourceloader:filter:minify-css:7:caed6998fab899483c805c3e2c432ff5 */</style>

<script src="http://www.gwen-morse.com/wiki/load.php?debug=false&amp;lang=en&amp;modules=startup&amp;only=scripts&amp;skin=vector&amp;*"></script>
<script>if(window.mw){
mw.config.set({"wgCanonicalNamespace":"","wgCanonicalSpecialPageName":false,"wgNamespaceNumber":0,"wgPageName":"Mushpp","wgTitle":"Mushpp","wgCurRevisionId":2407,"wgRevisionId":2407,"wgArticleId":625,"wgIsArticle":true,"wgIsRedirect":false,"wgAction":"view","wgUserName":null,"wgUserGroups":["*"],"wgCategories":[],"wgBreakFrames":false,"wgPageContentLanguage":"en","wgPageContentModel":"wikitext","wgSeparatorTransformTable":["",""],"wgDigitTransformTable":["",""],"wgDefaultDateFormat":"dmy","wgMonthNames":["","January","February","March","April","May","June","July","August","September","October","November","December"],"wgMonthNamesShort":["","Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec"],"wgRelevantPageName":"Mushpp","wgIsProbablyEditable":false,"wgRestrictionEdit":[],"wgRestrictionMove":[]});
}</script><script>if(window.mw){
mw.loader.implement("user.options",function(){mw.user.options.set({"ccmeonemails":0,"cols":80,"date":"default","diffonly":0,"disablemail":0,"disablesuggest":0,"editfont":"default","editondblclick":0,"editsection":1,"editsectiononrightclick":0,"enotifminoredits":0,"enotifrevealaddr":0,"enotifusertalkpages":1,"enotifwatchlistpages":0,"extendwatchlist":0,"fancysig":0,"forceeditsummary":0,"gender":"unknown","hideminor":0,"hidepatrolled":0,"imagesize":2,"justify":0,"math":1,"minordefault":0,"newpageshidepatrolled":0,"nocache":0,"noconvertlink":0,"norollbackdiff":0,"numberheadings":0,"previewonfirst":0,"previewontop":1,"rcdays":7,"rclimit":50,"rememberpassword":0,"rows":25,"searchlimit":20,"showhiddencats":0,"shownumberswatching":1,"showtoc":1,"showtoolbar":1,"skin":"vector","stubthreshold":0,"thumbsize":2,"underline":2,"uselivepreview":0,"usenewrc":0,"vector-simplesearch":1,"watchcreations":0,"watchdefault":0,"watchdeletion":0,"watchlistdays":3,"watchlisthideanons":0,"watchlisthidebots":0,
"watchlisthideliu":0,"watchlisthideminor":0,"watchlisthideown":0,"watchlisthidepatrolled":0,"watchmoves":0,"wllimit":250,"useeditwarning":1,"prefershttps":1,"language":"en","variant-gan":"gan","variant-iu":"iu","variant-kk":"kk","variant-ku":"ku","variant-shi":"shi","variant-sr":"sr","variant-tg":"tg","variant-uz":"uz","variant-zh":"zh","searchNs0":true,"searchNs1":false,"searchNs2":false,"searchNs3":false,"searchNs4":false,"searchNs5":false,"searchNs6":false,"searchNs7":false,"searchNs8":false,"searchNs9":false,"searchNs10":false,"searchNs11":false,"searchNs12":false,"searchNs13":false,"searchNs14":false,"searchNs15":false,"variant":"en"});},{},{});mw.loader.implement("user.tokens",function(){mw.user.tokens.set({"editToken":"+\\","patrolToken":false,"watchToken":false});},{},{});
/* cache key: homesitewiki-mw_:resourceloader:filter:minify-js:7:5a69c7e0fa6557851b6dea8a62efaffb */
}</script>
<script>if(window.mw){
mw.loader.load(["mediawiki.page.startup","mediawiki.legacy.wikibits","mediawiki.legacy.ajax","skins.vector.js"]);
}</script>
<!--[if lt IE 7]><style type="text/css">body{behavior:url("/wiki/skins/vector/csshover.min.htc")}</style><![endif]--></head>
<body class="mediawiki ltr sitedir-ltr ns-0 ns-subject page-Mushpp skin-vector action-view vector-animateLayout">
		<div id="mw-page-base" class="noprint"></div>
		<div id="mw-head-base" class="noprint"></div>
		<div id="content" class="mw-body" role="main">
			<a id="top"></a>
			<div id="mw-js-message" style="display:none;"></div>
						<h1 id="firstHeading" class="firstHeading" lang="en"><span dir="auto">Mushpp</span></h1>
			<div id="bodyContent">
								<div id="siteSub">From Gwen Morse&#039;s Wiki</div>
								<div id="contentSub"></div>
												<div id="jump-to-nav" class="mw-jump">
					Jump to:					<a href="#mw-navigation">navigation</a>, 					<a href="#p-search">search</a>
				</div>
				<div id="mw-content-text" lang="en" dir="ltr" class="mw-content-ltr"><pre>
#!/usr/bin/perl -w

##############################################################################
#
# mushpp  -  Process formatted MUSH/MUX code with comments, defines and
#            macros into something a MUSH/MUX can handle.
#
# Copyright 2003-2010  Christian J. Robinson &lt;heptite@gmail.com&gt;
#
# Ideas, but no code borrowed from
# Unformat.pl &lt;http://adam.legendary.org/index.php/Unformat&gt;
# Written by Adam Dray &lt;adam@legendary.org&gt;
#
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 2 of the License, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for
# more details.
# 
# You should have received a copy of the GNU General Public License along with
# this program; if not, write to the Free Software Foundation, Inc., 675 Mass
# Ave, Cambridge, MA 02139, USA.
#
# Or on the web:
#  HTML: &lt;http://www.gnu.org/copyleft/gpl.html&gt;
#  Text: &lt;http://www.gnu.org/copyleft/gpl.txt&gt;
#
##############################################################################
#
# FIXME / TODO:
#  - Even though it works, this parser sucks. I should learn how to write a
#    &quot;real&quot; one.
#
##############################################################################


use 5.006;
use strict;
use File::Basename;
use Getopt::Long;

sub readfile($);
sub add_macro($$$);
sub process_macros($);
sub substitute_macro($$);
sub error(;$$);
sub output(;$$);
sub usage($);

my (%macros,&#160;%files,&#160;%opts);
my $BASENAME = basename($0);
my $VERSION = (split(' ', '$Revision: 1.31 $'))[1] . ' BETA';
my $success = 1;

$opts{'q'} = 0;

{
  # Make GetOptions use the error function (in case the -o option was used
  # first):
  local $SIG{'__WARN__'} = \&amp;error;

  Getopt::Long::config qw/bundling/;
  GetOptions(\%opts,
    'o|outputcommand=s',
    'f|finishmessage=s',
    'd|define:s%' =&gt; sub{
      if (!defined($_[1]))
      {
        warn &quot;Option $_[0] requires an argument\n&quot;;
        usage(1);
      }
      exit 1 unless add_macro('#define ' . $_[1] . ' ' . ($_[2]&#160;? $_[2]&#160;: 1), '', '')
    },
    'q|quiet'    =&gt; sub{++$opts{'q'}},
    'h|help'     =&gt; sub{usage(0);},
    'H|man'      =&gt; sub{exec(&quot;perldoc $0&quot;)},
    'v|version'  =&gt; sub{output &quot;$BASENAME version $VERSION&quot;; exit 0;},
  ) or usage(1);
}

if (scalar(@ARGV))
{
  foreach my $file (@ARGV)
  {
    $success = readfile($file) &amp;&amp; $success;
  }
} else {
  $success = readfile('-') &amp;&amp; $success;
}

if ($success)
{
  #output 'Uploaded.';
  output $opts{'f'} if defined $opts{'f'};
  exit 0;
} else {
  output 'Some errors/warnings occurred during processing.', 1;
  exit 2;
}

sub readfile($)
# Purpose:
#  Read a file, unformat it, and process it.  Output is printed when EOF or a
#  '-' line is reached.
# Arguments:
#  1 - String: The filename to be read.
# Return value:
#  0 if any errors were encountered, 1 otherwise.
{
  my $file = shift;
  my ($line, $what, $tmp, $directory);
  my $rval  = 1;
  my $depth = 0;
  my $text  = '';

  local *FILE;

  unless (open(FILE, $file))
  {
    error &quot;Can't open file \&quot;$file\&quot;: $!\n&quot;;
    return 0;
  }

  $file = $file eq '-'&#160;? '(stdin)'&#160;: $file;

  if ($files{$file})
  {
    error &quot;File \&quot;$file\&quot; already read.\n&quot;;
    close(FILE);
    return 0;
  } else {
    ++$files{$file};
  }

  $directory = dirname($file);

  LOOP: while (&lt;FILE&gt;)
  {
    chomp($line = $_);

    next LOOP if $line =~ /^$/;

    SWITCH: {
      $line =~ /^\s*#\s*ascii/i &amp;&amp; do
        {
          my $this_line   = $line;
          my $this_linenr = $.;
          my $tmp;

          ASCII: while (&lt;FILE&gt;)
          {
            chomp($line = $_);
            last ASCII if $line =~ /^\s*#\s*endascii(?:\s.*)?/i;

            $line =~ s/([\\\{\}\(\)\[\]\%])/\\$1/go;
            $line =~ s/\t/\%t/go;
            $line =~ s/\ /\%b/go;
            $line =~ s/\r//go;
            $tmp  .= (defined($tmp)&#160;? '%r'&#160;: '') . $line;
          }
          
          $rval = 0 unless add_macro($this_line . ' ' . $tmp, $file, $this_linenr);

          last SWITCH;
        };

      (($what, $tmp) = ($line =~ /^\s*#\s*(ifn?def)\s+(.*)\s*/i)) &amp;&amp; do
        {
          ++$depth;

          if ($what eq 'ifdef')
          {
            last SWITCH if defined $macros{$tmp};
          } else {
            last SWITCH unless defined $macros{$tmp};
          }

          $tmp = 1;

          while(&lt;FILE&gt;)
          {
            chomp($line = $_);
            ++$tmp if $line =~ /^\s*#\s*ifdef\s+(?:.*)\s*/i;
            if ($line =~ /^\s*#\s*endif(?:\s+(?:.*)\s*)?/i)
            {
              --$tmp;
              --$depth;
            }
            last SWITCH if $tmp == 1 &amp;&amp; $line =~ /^\s*#\s*else\s*/i;
            last SWITCH unless $tmp;
          }

          last SWITCH;
        };

      $line =~ /^\s*#\s*else\s*/i &amp;&amp; do
        {
          if ($depth)
          {
            $tmp = $depth;
            while(&lt;FILE&gt;)
            {
              chomp($line = $_);
              ++$depth if $line =~ /^\s*#\s*ifdef\s+(?:.*)\s*/i;
              --$depth if $line =~ /^\s*#\s*endif(?:\s+(?:.*)\s*)?/i;
              last SWITCH if $depth == $tmp - 1;
            }
          } else {
            error &quot;#else without #ifdef on line $. of file \&quot;$file\&quot;: $line\n&quot;;
            $rval = 0;
          }

          last SWITCH;
        };

      $line =~ /^\s*#\s*endif(?:\s+(?:.*)\s*)?/i &amp;&amp; do
        {
          if ($depth)
          {
            --$depth;
          } else {
            error &quot;#endif without #ifdef on line $. of file \&quot;$file\&quot;: $line\n&quot;;
            $rval = 0;
          }

          last SWITCH;
        };

      $line =~ /^\s*#\s*(eval_)?define/i &amp;&amp; do
        {
          MULTIDEF: while ($line =~ m/\\$/)
          {
            $line =~ s/\\$//;
            chomp($tmp = &lt;FILE&gt;);
            last MULTIDEF if eof FILE;
            $tmp =~ s/^\s*//;
            $line .= $tmp;
          }

          $rval = 0 unless add_macro($line, $file, $.);

          last SWITCH;
        };
      
      (($tmp) = ($line =~ /^\s*#\s*include(?:\s+(.+?)?\s*)?$/i)) &amp;&amp; do
        {
          unless ($tmp)
          {
            error &quot;Bad include on line $. of file \&quot;$file\&quot;: $line\n&quot;;
            $rval = 0;
            last SWITCH;
          }

          $tmp = &quot;$directory/$tmp&quot;
            if (! -e $tmp &amp;&amp; -e &quot;$directory/$tmp&quot;);
          $rval = 0 unless readfile($tmp);

          last SWITCH;
        };

      # Ignore comments (must start at the beginning of a line):
      $line =~ m/^#/ &amp;&amp; do
        {
          last SWITCH;
        };
      
      # '-' or 'EOL' on a line alone ends ends the current MUSHcode line:
      $line =~ /^(?:-|EOL)\s*$/ &amp;&amp; do
        {
          print process_macros($text) . &quot;\n&quot;;
          $text = '';

          last SWITCH;
        };
      
      # Default:
      $line =~ s/^\s*//;
      #$line =~ s|/@@.*@@/||;
      $line =~ s|/@@.*?@@/||;
      $text .= $line;
    }
  }
  print process_macros($text) . &quot;\n&quot; if $text ne '';

  close(FILE);

  return $rval;
}

sub add_macro($$$)
# Purpose:
#  Add a define/macro to the table.
# Arguments:
#  1 - String:  The full macro line.
#  2 - String:  The filename where the macro was found.
#  3 - Integer: The line number where the macro was found.
# Return value:
#  0 if any errors were encountered, 1 otherwise.
{
  my $define = shift;
  my $file   = shift;
  my $linenr = shift;
  my ($name, $info, $body, $err, $def);
  my $rval = 1;

  if ($file eq '')
  {
    $err = '';
    $def = ': ' . (split(/^#define /, $define, 2))[1];
  } else {
    $err = &quot; on line $linenr of file \&quot;$file\&quot;&quot;;
    $def = &quot;: $define&quot;;
  }

  if (defined $define &amp;&amp; $define =~ /#\s*define\s+[^(\s]+\s*\([\w, ]*?[^\w, )][\w, ]*?\)/)
  {
    error &quot;Bad define$err--bad parameter syntax$def\n&quot;;
    return 0;
  } elsif (defined $define &amp;&amp; $define =~ /#\s*define\s+[^(\s]+\s*\([^)]+$/)
  {
    error &quot;Bad define$err--missing parenthesis$def\n&quot;;
    return 0;
  } elsif (defined $define &amp;&amp; $define =~ /#\s*define\s+([^\W(]+)\s*(?:\((?:[^)]+)\))?\s*$/)
  {
    error &quot;Bad define$err--missing body$def\n&quot;;
    return 0;
  } elsif (defined $define &amp;&amp; $define =~ /#\s*define\s+([^\W(]+)\s*\(([^)]+)\)\s+(.+)$/)
  {
    $name = $1;
    $info = [ $file, $linenr, 'macro' ];
    $body = [ $3, split(/,\s*/, $2) ];
  } elsif (defined $define &amp;&amp; $define =~ /#\s*(define|ascii)\s+([^\s]+)\s+(.+)$/)
  {
    $name = $2;
    $info = [ $file, $linenr, $1 ];
    $body = $3;
  } elsif (defined $define &amp;&amp; $define =~ /#\s*eval_define\s+([^\s]+)\s+(.+)$/)
  {
    $name = $1;
    $info = [ $file, $linenr, 'eval_define' ];
    $body = process_macros($2);

    EVAL: {
      local $SIG{'__WARN__'} = sub{error $_[0], 2};
      $body = eval($body);

      if ($@)
      {
        error $@, 0;
        return 0;
      }
    }
  } else {
    $define =~ s/\s*(#\s*ascii)\s.*/$1/;
    error &quot;Bad define&quot; . $err . $def;
    return 0;
  }

  if (defined $macros{$name})
  {
    error &quot;Macro \&quot;$name\&quot; redefined on line $linenr of file \&quot;$file\&quot;.&quot;, 2;
    error &quot;Previous definition on line $macros{$name}{info}[1] of file \&quot;$macros{$name}{info}[0]\&quot;.&quot;, 2;
    $rval = 0;
  }

  $macros{$name}{'info'} = $info;
  $macros{$name}{'body'} = $body;

  return $rval;
}

sub process_macros($)
# Purpose:
#  Find and substitute definitions/macros on an unformatted line with their
#  defined values.
# Arguments:
#  1 - String: The line to be processed.
# Return value:
#  The line after it's been processed.
{
  my $text = shift;
  my ($i, $j, @defines, @macros, @ascii, $defines, $macros, $ascii, $match);
  my $tmp = '';

  foreach my $key (keys&#160;%macros)
  {
    #if (ref $macros{$key}{'body'})
    if ($macros{$key}{'info'}[2] eq 'macro')
    {
      push(@macros, $key);
    } elsif ($macros{$key}{'info'}[2] eq 'ascii')
    {
      push(@ascii, $key);
    } else {
      push(@defines, $key);
    }
  }

  $defines = '\Q' . join('\E|\Q', @defines) . '\E';
  $macros = '\Q' . join('\E|\Q', @macros) . '\E';
  $ascii = '\Q' . join('\E|\Q', @ascii) . '\E';

  $i = 0;
  while ($tmp ne $text)
  {
    $tmp = $text;
    eval '$text =~ s/\b(' . $defines . ')\b/$macros{$1}{body}/g; $match = $1;'
      if @defines;

    ++$i;
    if ($i &gt; 100)
    {
      error &quot;Too many recursions. Runaway macro? Last processed: $match\n&quot;, 1;
      exit 1;
    }

    $j = 0;
    while (@macros &amp;&amp; eval '($match) = ($text =~ m/\b(' . $macros . ')\s*\(/)')
    {
      $text = substitute_macro($text, $match);

      ++$j;
      if ($j &gt; 100)
      {
        error &quot;Too many recursions. Runaway macro? Last processed: $match\n&quot;, 1;
        exit 1;
      }
    }
    # If any macros were actually processed, strip backslashes from escaped
    # &quot;\&quot; and &quot;,&quot; characters:
    $text =~ s/\\([,\\])/$1/g if $j;
  }

  # Process ASCII defines here so they're not recursively examined:
  eval '$text =~ s/\b(' . $ascii . ')\b/$macros{$1}{body}/g' if @ascii;

  return $text;
}

sub substitute_macro($$)
# Purpose:
#  Substitute a macro on an unformatted line with its value.
# Arguments:
#  1 - String: The line to be processed.
#  2 - Macro:  Which macro to find and substitute.
# Return value:
#  The line after the first occurrence of the macro has been substituted.
{
  my ($text, $macro) = @_;
  my ($start, $i, $arg_index, $c,&#160;%args, $tmp, $paren);

  # Locate the start of the macro, and the opening parentheses in the line:
  return $text if $text&#160;!~ m/\b${macro}\s*(\()/;
  $start = $-[0];
  $i     = $-[1];

  $c = substr($text, $i, 1);
  
  $arg_index = $paren = 0; $tmp = '';

  # Find each argument passed to the macro, handling nested parentheses:
  LOOP: while(($c = substr($text, $i++, 1)) ne '')
  {
    if ($c eq '(')
    {
      ++$paren;
      $tmp .= $c if $paren &gt; 1;
    } elsif ($c eq ')')
    {
      --$paren;
      unless ($paren)
      {
        local $SIG{'__WARN__'} = sub{1};
        $args{$macros{$macro}{'body'}[$arg_index + 1]} = $tmp;
        ++$arg_index;
        last LOOP;
      }
      $tmp .= $c;
    } elsif ($c eq '\\')
    {
      # Handle escaped commas and backslashes:
      if (substr($text, $i, 1) eq ',')
      {
        # Preserve the backslash; they're stripped later to allow macro
        # nesting:
        $tmp .= '\\,';
        ++$i;
      } elsif (substr($text, $i, 1) eq '\\')
      {
        # Preserve the backslash; they're stripped later to allow macro
        # nesting:
        $tmp .= '\\\\';
        ++$i;
      } else {
        $tmp .= $c;
      }
    } elsif ($c eq ',')
    {
      if ($paren == 1)
      {
        local $SIG{'__WARN__'} = sub{1};
        $args{$macros{$macro}{'body'}[$arg_index + 1]} = $tmp;
        $tmp = '';
        ++$arg_index;
        ++$i while (substr($text, $i, 1) =~ m/\s/);
      } else {
        $tmp .= $c;
      }
    } else {
      $tmp .= $c;
    }
  }

  unless ($c eq ')')
  {
    error &quot;No closing parentheses for macro call: $macro\n&quot;, 1;
    exit 1;
  }

  if ($#{$macros{$macro}{'body'}}&#160;!= $arg_index)
  {
    error &quot;Expected $#{$macros{$macro}{body}} argument(s), got $arg_index for macro: $macro\n&quot;, 1;
    exit 1;
  }

  # Fill the macro's arguments with the supplied arguments:
  $tmp = '\Q' . join('\E|\Q', keys&#160;%args) . '\E';
  eval '($tmp = $macros{$macro}{&quot;body&quot;}[0] ) =~ s/\b(' . $tmp . ')\b/$args{$1}/g';

  # Replace the called macro in the line with the new value, and return it:
  return substr($text, 0, $start) . $tmp . substr($text, $i);
}

sub error(;$$)
# Purpose:
#  Create an error message and output it.
# Arguments:
#  1 - String, optional:  The error message.
#  2 - Integer, optional: 0 - error (default)
#                         1 - fatal error
#                         2 - warning
# Return value:
#  Nothing meaningful.
{
  my $message = shift;
  my $type    = shift;

  if (defined $type &amp;&amp; $type&#160;!= 0)
  {
    if ($type == 1)
    {
      $type = 'FATAL ERROR:';
    } elsif ($type == 2)
    {
      $type = 'WARNING:';
    }
  } else {
    $type = 'ERROR:';
  }

  chomp($message = $message || '&lt;Unknown error&gt;');

  $message =~ s/^/$type /mg;

  output $message, 1;
}

sub output(;$$)
# Purpose:
#  Create a status message and output it, prepending the output command if
#  necessary.
# Arguments:
#  1 - String, optional:  The message.
#  2 - Boolean, optional: The true value causes the message to be printed on
#                         STDERR, otherwise STDOUT is used.
# Return value:
#  Nothing meaningful.
{
  my $message = shift;
  my $err = shift;

  # Be nice if the user requested silent output:
  return if ($opts{'q'} &amp;&amp;&#160;!$err || $opts{'q'} &gt; 1);

  if ($message)
  {
    chomp($message);
  } else {
    $message = '&lt;Unknown message&gt;';
  }

  $message =~ s/^/$opts{o} /mg
    if defined($opts{'o'});

  if ($err)
  {
    print STDERR &quot;$message\n&quot;;
  } else {
    print STDOUT &quot;$message\n&quot;;
  }
}

sub usage($)
# Purpose:
#  Output a short help statement.
# Arguments:
#  1 - Integer: Zero causes output on STDOUT and a successful exit value,
#               otherwise STDERR is used and the value is used as the exit
#               value.
# Return value:
#  Nothing, it causes the script to exit.
{
  my $rval = shift;

  output &quot;Usage: $BASENAME [options] [files]&quot;, $rval;
  output &quot; Options:&quot;,                          $rval;
  output &quot; -q, --quiet&quot;,                       $rval;
  output &quot; -o, --outputcommand &lt;command&gt;&quot;,     $rval;
  output &quot; -f, --finishmessage &lt;message&gt;&quot;,     $rval;
  output &quot; -d, --define &lt;macro&gt;[=value]&quot;,      $rval;
  output &quot; -v, --version&quot;,                     $rval;
  output &quot; -h, --help&quot;,                        $rval;
  output &quot; &quot;,                                  $rval;
  output &quot;For details, see: $BASENAME --man&quot;,  $rval;

  exit $rval;
}

=head1 NAME

mushpp - Unformat and process MUSH/MUX code.

=head1 SYNOPSIS

B&lt;mushpp&gt; [options] [files]

=head1 DESCRIPTION

I&lt;Mushpp&gt; is a Perl script to process formatted MUSH/MUX code with comments,
defines and macros into something a MUSH/MUX can handle.

=head1 OPTIONS

=over 4

=item B&lt;-o&gt; I&lt;command&gt;, B&lt;--outputcommand&gt;=I&lt;command&gt;

Prefix all status or warning messages with 'I&lt;command&gt;', e.g.:

 mushpp --outputcommand=/echo fnord.mush

=item B&lt;-f&gt; I&lt;message&gt;, B&lt;--finishmessage&gt;=I&lt;message&gt;

Print I&lt;message&gt; when processing is finished, e.g.:

 mushpp --finishmessage=Uploaded. fnord.mush

This message isn't printed at all if the quiet option is used, and is prefixed
by the outputcommand, if specified.

=item B&lt;-d&gt;, B&lt;--define&gt;=I&lt;macro&gt;[=I&lt;value&gt;]

This adds a macro with with the specified value, with a default value of &quot;1&quot;.
This is useful for enabling C&lt;#ifdef&gt;-ed sections of files.  See L&lt;&quot;Defines&quot;&gt; and
L&lt;&quot;Conditionals&quot;&gt; below.

=item B&lt;-q&gt;, B&lt;--quiet&gt;

Quiet status messages such as the uploaded message.  Issue this option two or
more times to silence even error output.  (Be careful with this.)

=item B&lt;-v&gt;, B&lt;--version&gt;

Show the version and exit.

=item B&lt;-h&gt;, B&lt;--help&gt;

Show a short usage statement and exit.

=item B&lt;-H&gt;, B&lt;--man&gt;

Show this documentation.

=back

=head1 INPUT FILE FORMAT

The files processed by mushpp will be de-indented and newlines up to a line
containing only a '-' or 'EOL' will be compressed into one line.

Lines starting with C&lt;#&gt; are comments, except for some special cases.  No
leading white space is allowed. See L&lt;&quot;Preprocessor Directives&quot;&gt; below.

You can inline comments with C&lt;/@@ text @@/&gt;, but they can't span lines like
C's C&lt;/* */&gt; comments.

Example:

 &amp;foo me=$foo *:
   @switch&#160;%0=bar,{ /@@ Correct 'password'. @@/
     @pemit me=Foo
   },{ /@@ Incorrect 'password'. @@/
     @pemit me=Huh?
   }
 -

 @pemit me=Don't forget that anybody can use the foo%b
           command if they're in the same location as%b
           you or in your inventory!
 -

When run through mushpp will look like:

 &amp;foo me=$foo *:@switch&#160;%0=bar,{ @pemit me=Foo},{ @pemit me=Huh?}
 @pemit me=Don't forget that anybody can use the foo%bcommand if they're in the same location as%byou or in your inventory!

=head2 Preprocessor Directives

=head3 Defines

You can use C&lt;#define&gt; in a similar way to the C preprocessor.  Be aware macros
are case sensitive.  Leading white space is allowed. For example:

 #define FOO BAR
  # define   BAR   BAZ
 
 @pemit me=FOO
 -
 @pemit me=foo
 -

Will result in:

 @pemit me=BAZ
 @pemit me=foo

=head3 Macros

You can also define macros:

 #define PEM(BAR, BAZ) switch(isdbref(BAR),1,pemit(BAR,BAZ),0,pemit(*BAR,BAZ))
 
 think [PEM(me, fnord)]
 -
 think [PEM(
     #1234,
         fnord
         fnord
 )]
 -

Will result in:

 think [switch(isdbref(me),1,pemit(me,fnord),0,pemit(*me,fnord))]
 think [switch(isdbref(#1234),1,pemit(#1234,fnordfnord),0,pemit(*#1234,fnordfnord))]

You may have a macro that spans multiple lines if you use a backspace to
continue the line to the next line.  Leading whitespace from continued lines
is removed.  For example:

 #define LONGMACRO(_string_) This is a long macro \
                             that prints _string_.
 LONGMACRO(some random string)

Will result in:

 This is a long macro that prints some random string.

There is no check for a \\ type pattern at the end of a line, so you can't
&quot;escape&quot; the backslash at the end of a macro definition.

During macro substitution, there I&lt;is&gt; a special check for &quot;\,&quot; strings, which
allows you to have a comma in a provided argument.  Doubled backslashes are
reduced to one backslash, thus allowing you to get a literal &quot;\,&quot; string by
using &quot;\\\,&quot;.  Any other backslashed character will result in a literal
backslash and that character.

=head3 ASCII defines

The special directive C&lt;#ascii I&lt;name&gt; ... #endascii&gt; will MUSH-escape the
block of text between C&lt;#ascii&gt; and C&lt;#endascii&gt; and assign it to the I&lt;name&gt;
definition.  For example:

 #ascii BUTTERFLY

  .-.   .-. 
 .   \o/   .
 `._  U  _.'
  .'  U  `.
  `--'U`--'

 #endascii

 BUTTERFLY

Will result in:

 %r%b.-.%b%b%b.-.%b%r.%b%b%b\\o/%b%b%b.%r`._%b%bU%b%b_.'%r%b.'%b%bU%b%b`.%r%b`--'U`--'%r

Note that there's a leading and trailing blank line in this example; if you
don't include them there will be no leading and trailing C&lt;%r&gt; on the output
whenever C&lt;BUTTERFLY&gt; is used, which may or may not be desired.

Also note that ASCII blocks are I&lt;not&gt; examined for other defines and macros.
They are processed and output as-is.

=head3 Evaluated defines

The special directive C&lt;#eval_define&gt; uses Perl to evaluate the definition and
put the result into the definition name, but you can not use this to create
macros.  Be careful with this, as there's no input sanity checking; the code
is procesed for macros then pased directly to the Perl interpreter.  Example:

 #eval_define FOO  0x1
 #eval_define BAR  0x2
 #eval_define BAZ  0x4
 #eval_define ALL  FOO | BAR | BAZ

 FOO BAR BAZ ALL

Will result in:

 1 2 4 7

=head3 Conditionals

You can use C&lt;#ifdef&gt;/C&lt;#ifndef&gt; ... [C&lt;#else&gt; ...] C&lt;#endif&gt; to conditionally
include sections in the processed output.  Conditionals can be nested within
each other.  Example:

 #define FOO 1
 
 #ifdef FOO
 foo is defined
 -
 #endif
 
 #ifndef BAR
 bar is not defined
 -
 #endif

 #ifdef BAZ
 baz is defined
 #else
 baz is not defined
 #endif

This will result in:

 foo is defined
 bar is not defined
 baz is not defined

Currently there are no C&lt;#if&gt;, C&lt;#elif&gt;, or C&lt;#elifdef&gt; directives.

=head3 Includes

You can use C&lt;#include&gt; to include another file for processing.  The file is
first looked for in the current directory, then in the directory the current
file being processed resides in.  For example, if &quot;F&lt;defines.mux&gt;&quot; contained:

 #define MYNAME Foo
 #define MYDBREF #1234

And you processed the following:

 #include defines.mux
 
 @pemit me=
  My name: MYNAME%r
  My dbref: MYDBREF
 -

This will result in:

 @pemit me=My name: Foo%rMy dbref: #1234

=head1 USE WITH TINYFUGUE

You can fairly easily use this within TinyFugue with the following macro:

 /def upload = /quote -dexec -0&#160;!mushpp -o /echo&#160;%*

Then from within TinyFugue you can simply issue a command such as &quot;C&lt;/upload
fnord.mux&gt;&quot; and F&lt;fnord.mux&gt; would be processed and sent to your current
(foreground) world.

A more complex TinyFugue macro that lets you either process data directly from
the TinyFugue input area or a file if a filename is provided as an argument:

 /if ( {TMPDIR} =~ &quot;&quot; ) \
   /setenv TMPDIR=/tmp&#160;%;\
 /endif

 /def -i mushpp = \
   /if ({#} &gt;= 1) \
     /quote -dexec -0&#160;!~/bin/mushpp -o /echo -f Uploaded.&#160;%*&#160;%; \
     /return&#160;%; \
   /endif&#160;%; \
   /let tmpfile=$[strcat({TMPDIR}, '/tf-tmp.', rand(10000, 99999))]&#160;%; \
   /let f=$[tfopen({tmpfile}, 'w')]&#160;%; \
   /if ({f} == -1) \
     /echo -e&#160;%% Unable to open&#160;%tmpfile for writing.&#160;%; \
     /return&#160;%; \
   /endif&#160;%; \
   /echo -e&#160;%% Entering pipe mode.  Type &quot;.&quot; to end.&#160;%; \
   /let _line=%; \
   /while ((tfread(_line)) &gt;= 0 &amp; _line&#160;!/ &quot;.&quot;) \
     /@test tfwrite({f}, strcat(_line, \n))&#160;%; \
   /done&#160;%; \
   /test $[tfclose({f})]&#160;%; \
   /quote -dexec -S&#160;!~/bin/mushpp -o /echo&#160;%tmpfile&#160;%; \
   /quote -decho -S&#160;!/bin/rm&#160;%tmpfile

=head1 EXIT CODES

There are three standard exit codes:

=over

=item * 0 -- Indicates successful processing with no warnings or errors.

=item * 1 -- Indicates a fatal error occurred during processing, and
processing was terminated at that point.

=item * 2 -- Indicates an error or warning occurred during processing, but
processing was completed.

=back

=head1 BUGS

=over

=item * #defines can recurse

A limit exists but if the limit is reached
processing will stop, an error will be printed, and the script will exit
without further output.

=item * Sometimes the diagnostic messages aren't very helpful

Especially for recursing macros (the location of the macro being processed has
been lost at that point).

=back

If you spot any other serious bugs, please email the author (see below).

=head1 AUTHOR

Christian J. Robinson E&lt;lt&gt;L&lt;heptite@gmail.com&gt;E&lt;gt&gt;

=head1 COPYRIGHT

Copyright 2003-2010  Christian J. Robinson E&lt;lt&gt;L&lt;heptite@gmail.com&gt;E&lt;gt&gt;

Ideas, but no code borrowed from
Unformat.pl (L&lt;http://adam.legendary.org/index.php/Unformat&gt;)
written by Adam Dray E&lt;lt&gt;L&lt;adam@legendary.org&gt;E&lt;gt&gt;.

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT
ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 675 Mass
Ave, Cambridge, MA 02139, USA.

Or on the web:

 HTML: http://www.gnu.org/copyleft/gpl.html
 Text: http://www.gnu.org/copyleft/gpl.txt

=cut

# vim:ts=2:sw=2:et:

</pre>

<!-- 
NewPP limit report
CPU time usage: 0.019 seconds
Real time usage: 0.022 seconds
Preprocessor visited node count: 4/1000000
Preprocessor generated node count: 24/1000000
Postâ€expand include size: 0/2097152 bytes
Template argument size: 0/2097152 bytes
Highest expansion depth: 2/40
Expensive parser function count: 0/100
-->

<!-- Saved in parser cache with key homesitewiki-mw_:pcache:idhash:625-0!*!*!*!*!*!* and timestamp 20160813145630
 -->
</div>								<div class="printfooter">
				Retrieved from "<a href="http://www.gwen-morse.com/wiki/index.php?title=Mushpp&amp;oldid=2407">http://www.gwen-morse.com/wiki/index.php?title=Mushpp&amp;oldid=2407</a>"				</div>
												<div id='catlinks' class='catlinks catlinks-allhidden'></div>												<div class="visualClear"></div>
							</div>
		</div>
		<div id="mw-navigation">
			<h2>Navigation menu</h2>
			<div id="mw-head">
				<div id="p-personal" role="navigation" class="" aria-labelledby="p-personal-label">
	<h3 id="p-personal-label">Personal tools</h3>
	<ul>
<li id="pt-login"><a href="/wiki/index.php?title=Special:UserLogin&amp;returnto=Mushpp" title="You are encouraged to log in; however, it is not mandatory [o]" accesskey="o">Log in</a></li>	</ul>
</div>
				<div id="left-navigation">
					<div id="p-namespaces" role="navigation" class="vectorTabs" aria-labelledby="p-namespaces-label">
	<h3 id="p-namespaces-label">Namespaces</h3>
	<ul>
					<li  id="ca-nstab-main" class="selected"><span><a href="/wiki/index.php/Mushpp"  title="View the content page [c]" accesskey="c">Page</a></span></li>
					<li  id="ca-talk" class="new"><span><a href="/wiki/index.php?title=Talk:Mushpp&amp;action=edit&amp;redlink=1"  title="Discussion about the content page [t]" accesskey="t">Discussion</a></span></li>
			</ul>
</div>
<div id="p-variants" role="navigation" class="vectorMenu emptyPortlet" aria-labelledby="p-variants-label">
	<h3 id="mw-vector-current-variant">
		</h3>
	<h3 id="p-variants-label"><span>Variants</span><a href="#"></a></h3>
	<div class="menu">
		<ul>
					</ul>
	</div>
</div>
				</div>
				<div id="right-navigation">
					<div id="p-views" role="navigation" class="vectorTabs" aria-labelledby="p-views-label">
	<h3 id="p-views-label">Views</h3>
	<ul>
					<li id="ca-view" class="selected"><span><a href="/wiki/index.php/Mushpp" >Read</a></span></li>
					<li id="ca-viewsource"><span><a href="/wiki/index.php?title=Mushpp&amp;action=edit"  title="This page is protected.&#10;You can view its source [e]" accesskey="e">View source</a></span></li>
					<li id="ca-history" class="collapsible"><span><a href="/wiki/index.php?title=Mushpp&amp;action=history"  title="Past revisions of this page [h]" accesskey="h">View history</a></span></li>
			</ul>
</div>
<div id="p-cactions" role="navigation" class="vectorMenu emptyPortlet" aria-labelledby="p-cactions-label">
	<h3 id="p-cactions-label"><span>Actions</span><a href="#"></a></h3>
	<div class="menu">
		<ul>
					</ul>
	</div>
</div>
<div id="p-search" role="search">
	<h3><label for="searchInput">Search</label></h3>
	<form action="/wiki/index.php" id="searchform">
				<div id="simpleSearch">
						<input name="search" placeholder="Search" title="Search Gwen Morse's Wiki [f]" accesskey="f" id="searchInput" />						<button type="submit" name="button" title="Search the pages for this text" id="searchButton"><img src="/wiki/skins/vector/images/search-ltr.png?303" alt="Search" width="12" height="13" /></button>								<input type='hidden' name="title" value="Special:Search"/>
		</div>
	</form>
</div>
				</div>
			</div>
			<div id="mw-panel">
					<div id="p-logo" role="banner"><a style="background-image: url(/home/public/wiki/guestbook.jpg);" href="/wiki/index.php/Main_Page"  title="Visit the main page"></a></div>
				<div class="portal" role="navigation" id='p-navigation' aria-labelledby='p-navigation-label'>
	<h3 id='p-navigation-label'>Navigation</h3>
	<div class="body">
		<ul>
			<li id="n-mainpage-description"><a href="/wiki/index.php/Main_Page" title="Visit the main page [z]" accesskey="z">Main page</a></li>
			<li id="n-MUSHing"><a href="/wiki/index.php/MUSHing">MUSHing</a></li>
			<li id="n-Softcode-Archive"><a href="/wiki/index.php/Gwen%27s_MUSHcode_Archive">Softcode Archive</a></li>
			<li id="n-Tinyfugue-Client"><a href="/wiki/index.php/Tinyfugue">Tinyfugue Client</a></li>
			<li id="n-Tabletop-Roleplay"><a href="/wiki/index.php/Roleplay">Tabletop Roleplay</a></li>
			<li id="n-Sims"><a href="/wiki/index.php/Sims">Sims</a></li>
			<li id="n-Linux"><a href="/wiki/index.php/Linux">Linux</a></li>
			<li id="n-Health-and-Beauty"><a href="/wiki/index.php/HABA">Health and Beauty</a></li>
			<li id="n-Recipes"><a href="/wiki/index.php/Recipes">Recipes</a></li>
		</ul>
	</div>
</div>
<div class="portal" role="navigation" id='p-Wiki_Help' aria-labelledby='p-Wiki_Help-label'>
	<h3 id='p-Wiki_Help-label'>Wiki Help</h3>
	<div class="body">
		<ul>
			<li id="n-portal"><a href="/wiki/index.php/Gwen_Morse%27s_Wiki:Community_portal" title="About the project, what you can do, where to find things">Community portal</a></li>
			<li id="n-currentevents"><a href="/wiki/index.php/Gwen_Morse%27s_Wiki:Current_events" title="Find background information on current events">Current events</a></li>
			<li id="n-recentchanges"><a href="/wiki/index.php/Special:RecentChanges" title="A list of recent changes in the wiki [r]" accesskey="r">Recent changes</a></li>
			<li id="n-randompage"><a href="/wiki/index.php/Special:Random" title="Load a random page [x]" accesskey="x">Random page</a></li>
			<li id="n-help"><a href="https://www.mediawiki.org/wiki/Special:MyLanguage/Help:Contents" rel="nofollow" title="The place to find out">Help</a></li>
		</ul>
	</div>
</div>
<div class="portal" role="navigation" id='p-tb' aria-labelledby='p-tb-label'>
	<h3 id='p-tb-label'>Tools</h3>
	<div class="body">
		<ul>
			<li id="t-whatlinkshere"><a href="/wiki/index.php/Special:WhatLinksHere/Mushpp" title="A list of all wiki pages that link here [j]" accesskey="j">What links here</a></li>
			<li id="t-recentchangeslinked"><a href="/wiki/index.php/Special:RecentChangesLinked/Mushpp" title="Recent changes in pages linked from this page [k]" accesskey="k">Related changes</a></li>
			<li id="t-specialpages"><a href="/wiki/index.php/Special:SpecialPages" title="A list of all special pages [q]" accesskey="q">Special pages</a></li>
			<li id="t-print"><a href="/wiki/index.php?title=Mushpp&amp;printable=yes" rel="alternate" title="Printable version of this page [p]" accesskey="p">Printable version</a></li>
			<li id="t-permalink"><a href="/wiki/index.php?title=Mushpp&amp;oldid=2407" title="Permanent link to this revision of the page">Permanent link</a></li>
			<li id="t-info"><a href="/wiki/index.php?title=Mushpp&amp;action=info">Page information</a></li>
		</ul>
	</div>
</div>
			</div>
		</div>
		<div id="footer" role="contentinfo">
							<ul id="footer-info">
											<li id="footer-info-lastmod"> This page was last modified on 25 July 2014, at 07:44.</li>
											<li id="footer-info-viewcount">This page has been accessed 805 times.</li>
									</ul>
							<ul id="footer-places">
											<li id="footer-places-privacy"><a href="/wiki/index.php/Gwen_Morse%27s_Wiki:Privacy_policy" title="Gwen Morse's Wiki:Privacy policy">Privacy policy</a></li>
											<li id="footer-places-about"><a href="/wiki/index.php/Gwen_Morse%27s_Wiki:About" title="Gwen Morse's Wiki:About">About Gwen Morse&#039;s Wiki</a></li>
											<li id="footer-places-disclaimer"><a href="/wiki/index.php/Gwen_Morse%27s_Wiki:General_disclaimer" title="Gwen Morse's Wiki:General disclaimer">Disclaimers</a></li>
									</ul>
										<ul id="footer-icons" class="noprint">
					<li id="footer-poweredbyico">
						<a href="//www.mediawiki.org/"><img src="/wiki/skins/common/images/poweredby_mediawiki_88x31.png" alt="Powered by MediaWiki" width="88" height="31" /></a>
					</li>
				</ul>
						<div style="clear:both"></div>
		</div>
		<script>/*<![CDATA[*/window.jQuery && jQuery.ready();/*]]>*/</script><script>if(window.mw){
mw.loader.state({"site":"loading","user":"ready","user.groups":"ready"});
}</script>
<script>if(window.mw){
mw.loader.load(["mediawiki.action.view.postEdit","mediawiki.user","mediawiki.hidpi","mediawiki.page.ready","mediawiki.searchSuggest","skins.vector.collapsibleNav"],null,true);
}</script>
<script>if(window.mw){
mw.loader.state({"site":"ready"});
}</script>
<!-- Served in 0.417 secs. -->
	</body>
</html>

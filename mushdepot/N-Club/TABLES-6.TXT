
@create table_parent
@link table_parent = #22
@lock/Basic table_parent=#3
@set table_parent = LINK_OK
@set table_parent = VISUAL
@set table_parent = NO_COMMAND
@set table_parent = MONITOR
&TTEMIT table_parent=$tt/emit *:@switch v(seat_%#)=,{@pemit %#=You're not at a table.},{@dolist [setq(0,At your table, %0)][v(table[v(seat_%#)])]=@pemit ##=r(0)}
&SWEEP table_parent=$sweep tables:@dolist [lattr(me/seat_#*)]={&table[v(##)] me=[switch([and(hasflag(owner(after(##,_)),connect),strmatch(locate(%!,after(##,_),n),after(##,_)))],0,[remove(v(table[v(##)]),after(##,_))],[v(table[v(##)])])];&## me=[switch(and(hasflag(owner(after(##,_)),connect),strmatch(locate(%!,after(##,_),n),after(##,_))),0,,[v(##)])]};@emit/room Tables have been cleared.
&AUTHOR table_parent=Susan@Amarynth and a lot of other places
&CONFIGURE table_parent=$configure tables:@switch %#=owner(me),{@set me=monitor;&hasdis me=v(hasdis);&hasleft me=v(hasleft);@pemit %#=Tables now ready to be set up.}
&MOVECHAIRS table_parent=$shift * chairs from * to *:@switch %# = owner(me),{@switch and(isnum(%0),and(member(v(tlist),table[trim(%1,b, )]),member(v(tlist),table[trim(%2,b, )])))=1,{@switch gt(%0,v(table[trim(%1,b, )]_limit))=1,@pemit %# = There are not that many chairs to take.,{&table[trim(%1,b, )]_limit me=[sub(v(table[trim(%1,b, )]_limit),%0)];&table[trim(%2,b, )]_limit me=[add(v(table[trim(%2,b, )]_limit),%0)];@emit %N moves [trim(%0,b, )] from table #[trim(%1,b, )] to table #[trim(%2,b, )].}},@pemit %# = You need to use real table numbers and amounts.},@pemit %# = Huh?%b%b(Type "help" for help)
&DESCTABLE table_parent=$desctable *=*:@switch %#=owner(me),{@switch gt(member(v(tlist),table[trim(%0,b, )]),0)=1,{&table[trim(%0,b, )]_desc me = [trim(%1,b, )];@pemit %# = Table #[trim(%0,b, )] is now [trim(%1,b, )]},@pemit %# = That is not a valid table.},@pemit %# = Huh?%b%b(Type "help" for help)
&ADDTABLE table_parent=$addtable *=*:@switch %#=owner(me),{@switch isnum(%1)=1,{&tlist me=[v(tlist)] table[add(1,words(v(tlist)))];&table[words(v(tlist))]_desc me=[trim(%0,b, )];&table[words(v(tlist))]_limit me=[trim(%1,b, )];@pemit %# = You have added [trim(%0,b, )] as table #[words(v(tlist))].},{@pemit %# = The limit must be a number.}},@pemit %# = Huh?%b%b(Type "help" for help)
&REMOVETABLE table_parent=$removetable *:@switch %#=owner(me),{@switch member(v(tlist),table[%0])=0,{@pemit %# = That's not a valid table number.},{&tlist me = [remove(v(tlist),table[%0])];@dolist lnum(sub(add(1,words(v(tlist))),%0)) = {&table[add(##,%0)]_desc me = [v(table[add(1,add(##,%0))]_desc)];&table[add(##,%0)]_limit me = [v(table[add(1,add(##,%0))]_limit)];&tlist me=[replace(v(tlist),add(##,%0),table[add(##,%0)])]; @switch eq(add(%0,##),words(v(tlist)))=0,,{&table[add(1,words(v(tlist)))]_desc me; &table[add(1,words(v(tlist)))]_limit me;@pemit %# = Table #[%0] is now removed.}}}},@pemit %# = Huh?%b%b(type "help" for help)
&T2 table_parent=$tables:@dolist lnum(words(v(tlist)))={@pemit %#=[setq(0,add(##,1))][capstr(v(table%q0_desc))] (#%q0) has [sub(v(table%q0_limit),words(v(table%q0)))] seats left. [switch(comp(words(u(andlist,%q0)),0),1,%r%b%bSeated are: [u(andlist,%q0)])]}
&ANDLIST table_parent=switch(words(v(table%0)),0,,1,name(v(table%0)),2,[name(first(v(table%0)))] and [name(rest(v(table%0)))].,iter(v(table%0),switch(eq(words(v(table%0)),member(v(table%0),##)),1,and [name(##)].,[[name(##)],])))
@ADESCRIBE table_parent=@switch %#=[owner(me)],{@pemit %# = %r[repeat(*,75)]%rYou, as the owner, are the only one that can see this or use these commands. The allow you to alter the number and descriptions of the tables.%r%raddtable <name of the table> = <limit that can sit there>%rremovetable <number of the table>%rdesctable <# of table to work on> = <new name for the table>%rshift <# of chairs> from <table #> to <table #>%rconfigure tables%r[repeat(*,75)]}
&HASLEFT table_parent=^* has left.*:@switch strmatch(v(seat_%#),)=1,,{&table[v(seat_%#)] me=[remove(v(table[v(seat_%#)]),%#)];&[seat_%#] me=}
&HASDIS table_parent=^* has disconnected.*:@switch strmatch(v(seat_%#),)=1,,{&table[v(seat_%#)] me=[remove(v(table[v(seat_%#)]),%#)];&seat_%# me=}
@DESCRIBE table_parent=The furniture here keeps in theme with the expensive tastes. It's all made of wood, finely crafted, and comfortable to sit in. ('tablehelp' for table commands)
&TABLEHELP table_parent=$tablehelp:@pemit %# = %rThe tables here are virtual, that is they are not objects in the room. When sitting at a table, however, you can speak quietly to those sitting with you so that no one else hears. The table related commands are:%r%rsit at #<table number> -- to sit at a table.%rsit with <name> -- to sit at <name>'s table.%r%rtables -- to list all the tables and show who is sitting where.%r%rtt <message> -- to talk quietly at your table.%rtt/<language> -- to talk quietly at your table in a different language%r(Ex: tt/german Hello!)%r%rsweep tables - typing this command in will clear any players that are at tables but are not actually there or are logged off. This can happen if the MUSH goes down.
&STAND table_parent=$stand:@switch strmatch(v(seat_%#),)=1,,{&table[v(seat_%#)] me=remove(v(table[v(seat_%#)]),%#);@dolist v(table[v(seat_%#)])=@pemit ## = %N leaves %p seat at your table.;@pemit %# = You stand up from your seat.;&seat_%# me=}
&SIT table_parent=$sit at #*:@switch eq(v(seat_%#),0)=1,,{@dolist v(table[v(seat_%#)])=@pemit ## = %N leaves %p seat at your table.;&table[v(seat_%#)] me=remove(v(table[v(seat_%#)]),%#);&seat_%# me=;@emit %N switches tables.};@switch lte(%0,words(v(tlist)))=1,{@switch eq(words(v(table%0)),v(table%0_limit))=1,@pemit %# = That table is full.,{&table%0 me=cat(v(table%0),%#);&seat_%# me=%0;@trigger me/mess=%#,%0}},@pemit %# = I don't think that is a valid table.
&TT table_parent=$tt ?*:@switch v(seat_%#)=,{@pemit %#=You're not at a table.},{@dolist [setq(0,switch(%0,:,{At your table, %N %1},\;,{At your table, %N%1},{At your table, %N says "%0%1"}))] [v(table[v(seat_%#)])]=@pemit ## = r(0)}
&MESS table_parent=@oemit %0=[name(%0)] sits down at [lcstr(v(table%1_desc))].;@pemit %0=You sit down at [lcstr(v(table%1_desc))].;@dolist v(table%1)={@pemit ## = [name(%0)] sits and joins you.}
&SITWITH2 table_parent=@switch strmatch(v(seat_%1),)=1,,{@dolist v(table[v(seat_%1)])=@pemit ## = [name(%1)] leaves %p seat at your table.;&table[v(seat_%1)] me=remove(v(table[v(seat_%1)]),%1);@emit [name(%1)] switches tables.};@switch lte(%0,words(v(tlist)))=1,{@switch eq(words(v(table%0)),v(table%0_limit))=1,@pemit %1 = That table is full.,{&table%0 me = [cat(v(table%0),%1)];&seat_%1 me=%0;@trigger me/mess=%1,%0}},@pemit %1 = I don't think that is a valid table.
&SITWITH table_parent=$sit with *:@switch eq(v(seat_[locate(%!,%0,n)]),)=1,@pemit %# = That person is not at a table here.,{@trigger me/sitwith2=[v(seat_[locate(%!,%0,n)])],%#}
&ORDER table_parent=$order *:@switch lt(v(seat_%#),2)=1,@emit %N whispers to a waiter who returns shortly with [switch(member(a e i o u,mid(%0,0,1)),0,a,an)] %0 in a take-out container.,{@emit %N orders [switch(member(a e i o u,mid(%0,0,1)),0,a,an)] %0 from a waiter standing nearby, and the order is brought out and placed at [lcstr(v(table[v(seat_%#)]_desc))] in front of %o.}
&ORDERINST table_parent=$order:@pemit %# = The order command will let you order something. order <name> (i.e. order glass of water)

@create PARENT: Desk
@link PARENT: Desk = #76
@set PARENT: Desk = LINK_OK
@set PARENT: Desk = HALT
@DESCRIBE PARENT: Desk=%r[space(5)]+[repeat(-,30)]+[space(5)]Simplistic in design, this standard%r[space(4)]/[space(30)]/|[space(5)]issue desk is constructed%r[space(3)]/[space(30)]/%b|[space(5)]of a material that resembles smoked%r[space(2)]+[repeat(=,18)]+[repeat(=,11)]+[space(2)]|[space(5)]glass but is both completely opaque%r[space(2)]|[space(2)]|[space(15)]|[space(3)]o---o[space(3)]|[space(2)]|[space(5)]and much sturdier. %bAn overly large%r[space(2)]|[space(2)]|[space(15)]|[space(11)]|[space(2)]|[space(5)]storage drawer is the perfect place%r[space(2)]|[space(2)]|[space(15)]|[space(11)]|[space(2)]|[space(5)]to store files as well as items you%r[space(2)]|[space(2)]|[space(15)]|[space(11)]|[space(2)]|[space(5)]wish to keep out of view. An almost%r[space(2)]|[space(2)]|[space(15)]|[space(11)]|[space(2)]|[space(5)]unnoticeable indentation in the top%r[space(2)]|[space(1)]/[space(16)]|[repeat(-,11)]|[space(1)]/[space(6)]of the desk holds a computer snugly.%r[space(2)]|/[space(17)]|/[space(10)]|/[space(7)]A comfortable chair sits behind it.%r
@set PARENT: Desk/DESCRIBE=no_command visual
&ISFED PARENT: Desk=[switch(masterorg(%#),#9375,1,0)]
&LOCK PARENT: Desk=isfed/1
&SWING PARENT: Desk=$swing:@switch comp([v(vz)], %#) = 0, {@pemit/list [remove(lcon(loc(me)),%#)]=[name(%#)] swings %p chair around as a childish twinkle comes to %p eyes.;@pe/silent %#=You swing your chair around like a little kid.},{@pemit/silent %#=You can't swing in the chair if you aren't sitting in it.}
&CLOCK_HELP PARENT: Desk=$clock help:@pemit %#=%R[ansi(hb,repeat(-=-,25))]%R[ansi(hr,center([name(me)] Clock Help Commands,75))]%R[ansi(hb,repeat(-=-,25))]%R[ansi(y,[ljust(Clock,30)]Display digital clock.%R[ljust(TList,30)]Displays available clock time zones.%R[ljust(Tzone <Zone>,30)]Change clock time zone.)]%r[ansi(hb,repeat(-=-,25))]
&UPRIGHT PARENT: Desk=$upright:@switch comp([v(vz)], %#) = 0, {@pemit/list [remove(lcon(loc(me)),%#)] =[name(%#)] drops %o foot to the floor, bringing the chair upright.;@pem/silent %#=You drop your foot to the floor, bringing your chair upright.},{@pemit/silent %#=You must have the chair tilted back before you can bring it upright.}
&DO_TLIST PARENT: Desk=$Tlist:@pe %#=Available Time Zone: [ansi(m,v(TIMEZONE))]
&DO_TZONE PARENT: Desk=$tzone *:@swi member(v(TIMEZONE),UCSTR(%0))=0,{@pe %#=This is not a valid Time Zone. If you want to add a new one, contact [ansi(r,[class(*julienna)] Julienna)] or [ansi(c,[class(*julianna)] Julianna)].},{&tzone me=[UCSTR(%0)];@pe %#=Time zone set to [ansi(g,UCSTR(%0))].}
&TIMEZONE PARENT: Desk=PST CST EST CEDT
&TIMEZONE_RATIO PARENT: Desk=-2 -1 0 7
&TZONE PARENT: Desk=CST
&F_CLOCK PARENT: Desk=[setq(0,mid(%0,0,1))][setq(1,mid(%0,1,1))][setq(2,mid(%0,2,1))][setq(3,mid(%0,3,1))]%b[space(25)][repeat(_,25)]%b%r[space(25)]|[u(FIRST,r(0))][u(FIRST,r(1))] [u(FIRST,r(2))][u(FIRST,r(3))]|%r[space(25)]|[u(SECOND,r(0))][u(SECOND,r(1))]o[u(SECOND,r(2))][u(SECOND,r(3))]|%r[space(25)]|[u(THIRD,r(0))][u(THIRD,r(1))]o[u(THIRD,r(2))][u(THIRD,r(3))]|%r[space(25)]|[repeat(_,25)]|%R
&DO_CLOCK PARENT: Desk=$clock:@pe %#=u(f_clock,[add(mid(time(),11,2),[EXTRACT(v(timezone_ratio),element(v(timezone),v(tzone), ),1)])][mid(time(),14,2)])
&THIRD PARENT: Desk=[switch(%0,9,%b__/%b%b,7,%b%b%b/%b%b,6,%b/_/%b%b,5,%b__/%b%b,4,%b%b%b/%b%b,3,%b__/%b%b,2,%b/_%b%b%b,1,%b%b%b/%b%b,%b/_/%b%b)]
&FIRST PARENT: Desk=[switch(%0,4,%b%b%b%b%b%b,1,%b%b%b%b_%b,%b%b%b__%b)]
&SECOND PARENT: Desk=[switch(%0,9,%b%b/_/%b,8,%b%b/_/%b,7,%b%b%b%b/%b,6,%b%b/_%b%b,5,%b%b/_%b%b,4,%b%b/_/%b,3,%b%b%b_/%b,2,%b%b__/%b,1,%b%b%b%b/%b,%b%b/%b/%b)]
&DO_UNPROTECT PARENT: Desk=$unprotect log *:@swi comp([v(vz)],%#) = 0,{@swi [gte(member(lattr(me/log*),LOG[extract(v(NUMLOGS),%0,1)]),1)]=1,{@swi member(v(protected),[extract(v(NUMLOGS),%0,1)])=0,{@pe %#=Log [ansi(g,%0)] is not protected against deletion.},{@pe %#=Log [ansi(y,%0)] is now unprotected.;&protected me=[remove(v(protected),[extract(v(NUMLOGS),%0,1)])]}},{@pemit %#=Computer says, "I show no record of a log number %0, %N.%r}},{@pemit/silent %#=You must be sitting at the desk to unprotect a log.%r}
&DO_PROTECT PARENT: Desk=$protect log *:@swi comp([v(vz)],%#) = 0,{@swi [gte(member(lattr(me/log*),LOG[extract(v(NUMLOGS),%0,1)]),1)]=1,{@swi member(v(protected),[extract(v(NUMLOGS),%0,1)])=0,{&protected me=[SETUNION(v(protected),[extract(v(NUMLOGS),%0,1)])];@pe %#=Log [ansi(m,%0)] protected against deletion.},{@pe %#=Log [ansi(c,%0)] is already protected.}},{@pemit %#=Computer says, "I show no record of a log number %0, %N.%r}},{@pemit/silent %#=You must be sitting at the desk to protect a log.%r}
@AHEAR PARENT: Desk=@sel [%1]=left,{@switch comp([v(vz)], %#)=0,{@vz me=},{}},disconnected,{@switch comp([v(vz)], %#)=0,{@vz me=},{}},partially disconnected,{@switch comp([v(vz)], %#)=0,{@vz me=},{}},arrived,{@switch comp([v(vz)], )=0,{@pemit %#=No one is currently sitting at [name(me)].},{@pemit %#=You see [name(v(vz))] sitting at [name(me)].}}
@set PARENT: Desk/AHEAR=no_command
&INSTALL PARENT: Desk=$desk install:@set me=opaque;@set me=safe;@listen me=* has *.;@lock me=me;@elock me=me;@ulock me=[owner(me)];@pe %#=To allow some of the commands to work, you will need to [ansi(hg,@set %!=Inherit)]. Also, set the type of logs by typing [ansi(hy,&MYNAME %!=Security)] for example."
&HELP_MSG PARENT: Desk=[ansi(hb,repeat(-=-,25))]%R[ansi(hr,center([name(me)] Help Commands,75))]%r[ansi(hb,repeat(-=-,25))]%R[ansi(y,[ljust(Desk Install,30)]Installs Desk parent to your desk object.)]%r[ansi(y,[ljust(Sit,30)]Sit down behind [name(me)].%R[ljust(Tilt,30)]Tilt chair back.%R[ljust(Stand,30)]Rise from [name(me)].%R[ljust(Contents,30)]List contents of [name(me)] drawer.%R[ljust(List Logs,30)]List all log entries.%R[ljust(Del Log #,30)]Delete a log.%R[ljust(View Log #,30)]View contents of a log.%R[ljust(Sig Help,30)]Display Mail Sig install help.%R[ljust(Clock Help,30)]Display [name(me)] Clock Help commands.%R[ljust(Put <Object>,30)]Place object in [name(me)] drawer.)]%R[ansi(y,[ljust(Pull <Object>,30)]Remove object from [name(me)] drawer.%R[ljust(Protect Log #,30)]Locks a file against accidental deletion.%R[ljust(Unprotect Log #,30)]Unlocks a file for deletion.%R[ljust(NewLog=<Text>,30)]Enter a new log.%R[ljust(Logmail <Players>=<Subject>/<Log #>,30)]Mail a log to specific players.%R[ljust(Swivel <Person/&Direction>,30)]Swivel chair and face Person/Direction.)]%r[ansi(hb,repeat(-=-,25))]
&DO_CONTENTS PARENT: Desk=$contents:@swi comp([v(vz)],%#) = 0,{@pemit %# = %R[ansi(w,You pull open)] [ansi(hr,[name(me)])] [ansi(w,drawer and see%,..)]%R%R[ansi(hb,repeat(=-=,25))]%R%B[ansi(y,[iter([lcon(me)],[name(##)](##[flags(##)])%R)])][ansi(hb,repeat(=-=,25))]%R%B[ansi(hc,Total items: [words([lcon(me)])])]},{@pemit/silent %#=You must be sitting at [name(me)] before you can check the contents of the drawer.}
&DO_PULL PARENT: Desk=$pull *:@swi comp([v(vz)],%#) = 0,{@swi [locate(me,%0,*)]=#-1,{@pe %#=There is no [ansi(c,%0)] in [name(me)] drawer.},{give %#=%0;@oemit %L/%# = [name(owner(me))] takes [art([name(locate(%#,%0,*))])] [ansi(g,name(locate(%#,%0,*)))] from [name(me)] drawer.;@pemit %# = You remove [art([name(%0)])] [ansi(g,name(locate(%#,%0,*)))] from [name(me)] drawer.}},{@pe %#=You must be sitting at [name(me)] to pull something from the drawer.}
&DO_PUT PARENT: Desk=$put *:@swi comp([v(vz)],%#) = 0,{@swi [locate(%#,%0,*)]=#-1,{@pe %#=You don't have [ansi(c,%0)].},{@fo %#=give %!=%0;@oemit %L/%# = %N slips [art([name(locate(%#,%0,*))])] [ansi(m,name(locate(%#,%0,*)))] into [name(me)] drawer.;@pemit %# = You slip [art([name(locate(%#,%0,*))])] [ansi(m,name(locate(%#,%0,*)))] into [name(me)] drawer.}},{@pe %#=You must be sitting at [name(me)] to put something in the drawer.}
&STAND PARENT: Desk=$stand:@switch comp([v(vz)], %#) = 0,{@pemit/list [remove(lcon(loc(me)),%#)] = [name(%#)] gets to %p feet and steps away from [name(me)].;@pemit/silent %#=Shoving the chair back, you rise from [name(me)].;@vz me=},{@pemit/silent %#=You are not sitting at [name(me)]!}
&SIT PARENT: Desk=$sit:@switch comp([v(vz)], )= 0,{@pemit/list [remove(lcon(loc(me)),%#)]= [name(%#)] plops down in the chair behind [name(me)].;@vz me=%#;@pemit/silent %#=You plop down in the chair behind [name(me)].%R%R[ansi(hy,<)] [ansi(c,Type)] [ansi(hc,Desk Help)] [ansi(c,for a list of desk commands.)] [ansi(hy,>)]},{@switch comp([v(vz)], %#) = 0,{@pemit/silent %#=You are already sitting at [name(me)]!},{@pemit/silent %#=Unless you plan on sitting on [name(v(vz))]'s lap, now is not the right time to sit down at [name(me)].;@pemit/silent [v(vz)]=%N almost sat down on your lap!}
&TURN PARENT: Desk=$swivel *:@switch comp([v(vz)], %#) = 0, {@pemit/list [remove(lcon(loc(me)),%#)] =[name(%#)] swivels in %p chair and faces %0.;@pemit/silent %#=You swivel your chair around and face %0.}, {@pemit/silent %#=Most people sit in a chair before they swivel around in it.}
&NUM PARENT: Desk=0
&ADDLOG PARENT: Desk=$newlog=*:@switch comp([u(vz)], %#) = 0,{&num me=add(u(num),1);&numlogs me=sort(setunion(u(numlogs),u(num)));&LOG[u(num)] me=%#|[time()]|[edit(%0,%r,%%r)];@pemit/silent %#=Turning to the computer, you say, "[ansi(hr,[v(myname)]'s log)], [ansi(hb,Stardate [secs2sd(secs())])].%r[ansi(y,%0)]"%R%RYou touch a button on the computer, storing the log.},{@pemit/silent %#=You must be sitting at [name(me)] to enter a new log.}
&LISTLOG PARENT: Desk=$list logs:@switch comp([v(vz)], %#) = 0,{@pemit %#=%r[ansi(hb,repeat(-=-,25))]%r[ansi(hr,[center([v(MYNAME)]'s Logs,75)])]%r[ansi(hb,repeat(-=-,25))];@swi [gte(v(NUM),1)]=1,{@pemit %#=%r%b[ansi(hw,[ljust(Number,10)][ljust(Stardate,20)][ljust(Date,29)][ljust(Protected,10)])]%r%b[ansi(hb,[ljust(^^^^^^,10)][ljust(^^^^^^^^^^^^,20)][ljust(^^^^^^^^^^^^^^^^^^^^^^^^,29)][ljust(^^^^^^^^^,10)])]%r%b[ansi(y,[iter(v(numlogs),[ljust(member(v(numlogs),##),10)][ljust(secs2sd(convtime(index(v(log[##]),|,2,1))),20)][ljust(index(v(log[##]),|,2,1),29)][center([switch(member(v(protected),##),0,No,Yes)],10)]%r)])][ansi(hb,repeat(-=-,25))]%r},0,{@pemit %#=Computer says, "I cannot find any logs for you, %N."%r[ansi(hb,repeat(-=-,25))]%r}},{@pemit/silent %#=You must be sitting at [name(me)] to list the logs.%r}
&READLOG PARENT: Desk=$view log *:@switch comp([v(vz)], %#) = 0,{@swi [gte(member(lattr(me/log*),LOG[extract(v(NUMLOGS),%0,1)]),1)]=1,{@pemit %#=%r[ansi(b,repeat(-=-,25))]%r%b%b[ansi(hr,v(MYNAME)'s Log)][space(3)]-[space(3)][ansi(hb,Stardate [secs2sd(convtime(index(v(log[extract(v(NUMLOGS),%0,1)]),|,2,1)))][space(3)]%([index(v(log[extract(v(NUMLOGS),%0,1)]),|,2,1)]%))]%r[ansi(b,repeat(-=-,25))]%r[ansi(y,[index(eval(me,log[extract(v(NUMLOGS),%0,1)]),|,3,1)])]%r%r[ansi(hw,[class([index(v(log[extract(v(NUMLOGS),%0,1)]),|,1,1)])]%b[name([index(v(log[extract(v(NUMLOGS),%0,1)]),|,1,1)])])][switch(u(HASATTR,SENT[extract(v(NUMLOGS),%0,1)]),1,%R)][ansi(g,[u(sent[extract(v(NUMLOGS),%0,1)])])]%r[ansi(b,repeat(-=-,25))] },{@pemit %#=Computer says, "I show no record of a log number %0, %N.%r}},{@pemit/silent %#=You must be sitting at [name(me)] to view a log.%r}
&DELLOG PARENT: Desk=$del log *:@swi comp([v(vz)],%#) = 0,{@swi [gte(member(lattr(me/log*),LOG[extract(v(NUMLOGS),%0,1)]),1)]=1,{@swi member(v(protected),[extract(v(NUMLOGS),%0,1)])=0,{@pemit %#=Computer says, "Log entry %0 deleted, %N.";&log[extract(v(NUMLOGS),%0,1)] me=;&sent[extract(v(NUMLOGS),%0,1)] me;&numlogs me=sort(setdiff(v(numlogs),[extract(v(NUMLOGS),%0,1)]))},{@pe %#=Log [ansi(r,%0)] is protected against deletion.}},{@pemit %#=Computer says, "I cannot find a log number %0 for you, %N."%r}},{@pemit/silent %#=You must be sitting at the desk to delete a log.%r}
&MYNAME PARENT: Desk=Personal
&DESKHELP PARENT: Desk=$desk help:@pem/silent %#=[u(help_msg)]
&TILT PARENT: Desk=$tilt:@switch comp([v(vz)], %#) = 0, {@pemit/list [remove(lcon(loc(me)),%#)] =[name(%#)] tilts the desk chair back slowly, resting %p foot on the bottom part of the desk.;@pem/silent %#=Perching your foot on the bottom of the desk, you tilt the chair back slightly.},{@pemit/silent %#=Most people sit in a chair before they try to tilt it back.}
&DO_LOGMAIL PARENT: Desk=$logmail *=*/*:@swi comp([v(vz)],%#) = 0,{@swi [gte(member(lattr(me/log*),LOG[extract(v(NUMLOGS),%2,1)]),1)]=1,{@dolist [SETUNION(u(playerlist,%0),)]=@fo owner(me)=@mail ##=%1/%[ansi(y,ljust(To,9))]: [class(pmatch(##))] [name(pmatch(##))]%%R%[ansi(y,ljust(Stardate,9))]: [secs2sd(secs())]%[space(4)]([time()])%%R%[ansi(y,ljust(Subject,9))]: %1%%R%%R[escape([index(eval(me,log[extract(v(NUMLOGS),%2,1)]),|,3,1)])]%%R%%R%[ansi(g,%{[switch(u(hasattr,LINE1),1,[u(LINE1)])][switch(u(hasattr,LINE2),1,%%R[u(LINE2)])][switch(u(hasattr,LINE3),1,%%R[u(LINE3)])]%})]%%R%%R%[ansi(c,CC:)] [setdiff(SETUNION(u(playerlist,%0),),##)];&sent[extract(v(NUMLOGS),%2,1)] me=[v(sent[extract(v(NUMLOGS),%2,1)])]%%RLog has been mailed to '[SETUNION(u(playerlist,%0),)]' [time()]},{@pe %#=Log [ansi(m,ucstr(%2))] doesn't exist.}},{@pemit/silent %#=You must be sitting at [name(me)] to @mail a log.%r}
&PLAYERLIST PARENT: Desk=[iter(%0,switch(pmatch(##),#-1,,fullname(*##)))]
&HASATTR PARENT: Desk=[switch(sign(member(lattr(me),%0)),1,1,0)]
&DESK_MAILSIG PARENT: Desk=$Sig help:@pe %#=[ansi(hb,repeat(=,75))]%RBelow, you will find an example on how to set-up your 'MAIL SIG'.%R%RSuppose you are a Security Officer onboard the USS Enterprise.%rYou would enter the following:%R%R[ansi(hw,&LINE1 [num(me)])][ansi(hc,=)][class(%#)] [name(%#)]%R[ansi(hw,&LINE2 [num(me)])][ansi(hc,=)]Security Officer%R[ansi(hw,&LINE3 [num(me)])][ansi(hc,=)]USS Enterprise%R%RYour mail signature would appear as:%R%R[ansi(g,[class(%#)] [name(%#)]%RSecurity Officer%RUSS Enterprise)]%R[ansi(hb,repeat(=,75))]

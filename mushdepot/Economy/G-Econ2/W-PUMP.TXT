@create Water Pumping Station Parent
@lock/Use Water Pumping Station Parent=canuse/1
@lock/Basic Water Pumping Station Parent==me
@set Water Pumping Station Parent = WIZARD
@set Water Pumping Station Parent = SAFE
@set Water Pumping Station Parent = NO_COMMAND
&CMD_DESTINATION_LIST Water Pumping Station Parent=$list destinations:th [pemit(%#,[ansi(v(border_color),[repeat(=,76)])]%r[center([ansi(v(title_color),[name(me)] Destination List)],76)]%r[ansi(v(border_color),[repeat(=,76)])]%r [iter([edit([v(destination_list)],|,%b)],[ljust(##,8)][name(##)]%r)][ansi(v(border_color),[repeat(=,76)])])]
&CMD_PUMP Water Pumping Station Parent=$pump *=*:th [switch([u(fun_issystem,[secure(%0)])][lte(%1,[add([v(remaining_power)],[v(WATER_[v(SYSTEM_%0)])])])][isnum(%1)],111,[switch([gte(%1,0)],1,[u(fun_transfer_power,[secure(%0)],[secure(%1)])],0,[pemit(%#,Number must be above 0.)])],011,[pemit(%#,That is not a system.)],101,[pemit(%#,You do not have that much free water.)],001,[pemit(%#,That is not a system.)],[pemit(%#,Argument must be a number.)])]
&CMD_SET_DESTINATION Water Pumping Station Parent=$set destination *:th [switch([strmatch([v(destination_list)],*|%0|*)],1,[set(me,destination:%0)][pemit(%#,The water packs are now transferred to [name(%0)] hourly.)],0,[pemit(%#,Invalid destination.)])]
&CMD_SET_MAX_WATER Water Pumping Station Parent=$set max water *:th [switch([hasflag(%#,Wiz)],1,[set(me,max_reactor:%0)][pemit(%#,[name(me)] can now generate up to %0 water.)],0,[pemit(%#,Permission denied.)])]
&CMD_SET_PUMP Water Pumping Station Parent=$set pump *:th [switch([hasflag(%#,Wiz)],1,[switch([strmatch([zone([loc(%0)])],[zone([loc(me)])])],1,[u(fun_set_pump,%0)],0,[pemit(%#,Error: Zones do not match.)])],0,[pemit(%#,Permission denied.)])]
&CMD_SET_WATER Water Pumping Station Parent=$set water *:th [switch([lte(%0,[v(max_reactor)])][gte(%0,0)],01,{[pemit(%#,The water generation plant cannot produce that much output.%rPlease chose a number between 0 and [v(max_reactor)].)]},10,[pemit(%#,Setting must be above 0.)],00,[pemit(%#,Setting must be above 0.)],11,{[u(fun_set_reactor,[secure(%0)])]})]
&CMD_TOGGLE_BATTERY_PRODUCTION Water Pumping Station Parent=$toggle water store production:th [if(hasattr(me,build_batteries),[pemit(%#,Already producing packaged water.)],[if(strmatch(v(destination),!!!!!!!4862!!!!!),[pemit(%#,No available destination.)],[set(me,build_batteries:yes)][pemit(%#,Now producing packaged water with left-over water.)])])]
&CMD_UNSET_PUMP Water Pumping Station Parent=$unset pump *:th [switch([hasflag(%#,Wiz)],1,[switch([hasattr(me,WATER_%0)],1,[wipe(me/water_%0)][pemit(%#,No longer able to pump water to [name(%0)] %(%0%))],0,[pemit(%#,Not currently pumping water to there.)])],0,[pemit(%#,Permission denied.)])]
&CMD_UNTOGGLE_BATTERY_PRODUCTION Water Pumping Station Parent=$untoggle water store production:th [if(hasattr(me,build_batteries),[wipe(me/build_batteries)][pemit(%#,No longer producing packaged water.)])]
&CREDITS Water Pumping Station Parent=Dave@GalacticMUSH [EMAIL: davemilford@home.com] [ICQ: 8516328]
&CURRENT_POWER_NUM Water Pumping Station Parent=1
@DESCRIBE Water Pumping Station Parent=[set(me,REMAINING_POWER:[v(reactor_setting)])][set(me,SYSTEMS:)]%b[ansi(v(border_color),[repeat(_,74)]%r/)]%b[ljust([ansi(v(name_color),[v(type)] Water Pumping Station)],73)][ansi(v(border_color),\\%r|[repeat(-,74)]|%r|)]%b[ljust([ansi(v(title_color),Owner:)] [v(owner)],73)][ansi(v(border_color),|%r|)]%b[ljust([ansi(v(title_color),Maximum Output:)] [v(max_reactor)],73)][ansi(v(border_color),|%r|[repeat(-,74)]|%r|)]%b[ljust([ansi(v(title_color),Current Output:)] [v(reactor_setting)]%b%([mul([fdiv([v(reactor_setting)],[v(max_reactor)])],100)]%%%),73)][ansi(v(border_color),|%r|)]%b[ljust([ansi(v(title_color),Pumping To:)],73)][if([lattr(me/WATER_*)],{[ansi(v(border_color),|%r|%b)][iter([lattr(me/WATER_*)],[set(me,SYSTEM_[v(current_power_num)]:[edit(##,WATER_,)])][ljust({[ansi(ny,[v(current_power_num)]:)] [name([edit(##,WATER_,)])]},39)][ljust([ansi(nc,[u(##)])],4)][rjust([ansi(v(border_color),|%r|)],34)][u(fun_calc_power,[u(##)])][set(me,SYSTEMS:[v(systems)] [v(current_power_num)])][set(me,CURRENT_POWER_NUM:[inc([v(current_power_num)])])])]},{[ansi(v(border_color),|%r|%b)][ljust(None.,73)][ansi(v(border_color),|%r|)]})][ansi(v(border_color),[repeat(-,74)]|%r|)]%b[ljust([ansi(v(title_color),Remaining Water:)]%b[v(remaining_power)],73)][ansi(v(border_color),|%r[if(hasattr(me,build_batteries),[ansi(hb,|[repeat(-,74)]|%r|%b)][ljust(ansi(h,%[)[ansi(hg,X)][ansi(h,%])] Extra Water Diverted to Water Store Production,73)][ansi(hb,|%r|)]%b[ljust([ansi(h,Destination:)]%b[name(v(destination))]%b%b%([v(destination)]%),73)][ansi(hb,%|%r)])][ansi(hb,\\[repeat(_,74)]/)][set(me,CURRENT_POWER_NUM:1)]
@set Water Pumping Station Parent/DESCRIBE=no_command visual
&DESTINATION Water Pumping Station Parent=[v(none)]
&DESTINATION_LIST Water Pumping Station Parent=|
&FUN_BUILD_BATTERIES Water Pumping Station Parent=[if(gte(v(remaining_power),0),[setq(0,[div(v(remaining_power),400)])][setq(1,[mod(v(remaining_power),400)])][if(gte(%q0,1),[set(me,storage_water:%q0)])][set(me,leftover_energy:[add(v(leftover_energy),%q1)])][if(gte(v(leftover_energy),400),[if(hasattr(me,storage_water),[set(me,storage_water:[inc(v(storage_water))])],[set(me,storage_water:1)])][set(me,leftover_energy:[sub(v(leftover_energy),400)])])])]
&FUN_CALC_POWER Water Pumping Station Parent=[set(me,REMAINING_POWER:[sub([v(remaining_power)],%0)])]
&FUN_CHECK_POWER Water Pumping Station Parent=[iter([lattr(me/WATER_*)],[set(me,REMAINING_POWER:[sub([v(remaining_power)],[u(##)])])])]
&FUN_ISSYSTEM Water Pumping Station Parent=[hasattr(me,SYSTEM_%0)]
&FUN_MOVE_COMMODS Water Pumping Station Parent=[iter([lattr(me/STORAGE_*)],[set([v(destination)],##:[switch([hasattr([v(destination)],##)],1,[add([get([v(destination)]/##)],[v(##)])],0,[v(##)])])][wipe(me/##)])][trigger([v(destination)]/fun_calc_capacity_used)][trigger([v(destination)]/fun_calc_capacity_remaining)]
&FUN_SET_PUMP Water Pumping Station Parent=[set(me,water_%0:0)][pemit(%#,[name(me)] can now pump water to [name(%0)] %(%0%))]
&FUN_SET_REACTOR Water Pumping Station Parent=[set(me,reactor_setting:%0)][pemit(%#,Water generation plant now producing %0 water.%rWater output now at [mul([fdiv([v(reactor_setting)],[v(max_reactor)])],100)]%%.)][set(me,REMAINING_POWER:%0)][u(fun_check_power)][switch([lte([v(remaining_power)],0)],1,[iter([lattr(me/WATER_*)],[set(me,##:0)])][pemit(%#,Global water shortage. Resetting allocations.)])]
&FUN_TRANSFER_POWER Water Pumping Station Parent=[set(me,WATER_[v(SYSTEM_%0)]:%1)][pemit(%#,System %0 %([name([v(SYSTEM_%0)])]%) is now pumped %1 water.)][set(me,REMAINING_POWER:[v(reactor_setting)])][u(fun_check_power)]
&LEFTOVER_ENERGY Water Pumping Station Parent=0
&MAX_REACTOR Water Pumping Station Parent=250
&OWNER Water Pumping Station Parent=Administration Water Corp.
&REACTOR_SETTING Water Pumping Station Parent=250
&REMAINING_POWER Water Pumping Station Parent=250
&SET_MAX_CAPACITY Water Pumping Station Parent=150
&SWEEP Water Pumping Station Parent=[if(hasattr(me,build_batteries),[u(fun_build_batteries)][wait(1,[lit([u(fun_move_commods)])])])]
&SYSTEMS Water Pumping Station Parent=
&TYPE Water Pumping Station Parent=Type VI
&VERSION Water Pumping Station Parent=3.0

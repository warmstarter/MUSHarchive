@create IAV Explorer-1
@link IAV Explorer-1=[owner(me)]
@lock/Zone IAV Explorer-1==[owner(me)]
@lock IAV Explorer-1==[owner(me)]
@set IAV Explorer-1 = INHERIT
@set IAV Explorer-1 = OPAQUE
@set IAV Explorer-1 = WIZARD
@set IAV Explorer-1 = SAFE
@set IAV Explorer-1 = MONITOR
@set IAV Explorer-1 = !NO_COMMAND
&SALERT IAV Explorer-1=GREEN
&SHIPNAME IAV Explorer-1=IAV Explorer-1
&RED-1 IAV Explorer-1=$red alert:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Initiating [ansi(fhr,RED)] alert.;@zemit %!=[ansi(fhr,=== RED ALERT === === RED ALERT === === RED ALERT ===)]%rRed alert has been initiated by %n. All hands to battlestations!;&salert me=[ansi(fhr,RED ALERT)]}
&BLUE-1 IAV Explorer-1=$blue alert:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Initiating [ansi(hb,BLUE)] alert.;@zemit %!=[ansi(hb,=== BLUE ALERT ===)]%rBlue alert has been initiated by %n. All decks, prepare for atmospheric entry.;&salert me=[ansi(hb,BLUE ALERT)]}
&YELLOW-1 IAV Explorer-1=$yellow alert:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Initiating [ansi(hy,YELLOW)] alert.;@zemit %!=[ansi(hy,=== YELLOW ALERT === === YELLOW ALERT ===)]%rYellow alert has been initiated by %n. Please be prepared in the event that an emergency situation arises.;&salert me=[ansi(hy,YELLOW ALERT)]}
&GREEN-1 IAV Explorer-1=$green alert:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Initiating [ansi(hg,GREEN)] alert.;@zemit %!=[ansi(hg,=== GREEN ALERT ===)]%rGreen alert has been initiated by %n. Standing down from alert status.;&salert me=[ansi(hg,GREEN)]}
&INTRUDER-1 IAV Explorer-1=$intruder alert:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Initiating [ansi(hr,INTRUDER)] alert.;&salert me=[ansi(hr,INTRUDER ALERT)];@zemit %!=[ansi(hr,=== INTRUDER ALERT === === INTRUDER ALERT ===)]%rIntruder alert has been initiated by %n. Security personnel please prepare for a combat situation.}
&INTERCOM-1 IAV Explorer-1=$int *:@emit [ansi(hb,<)][ansi(hy,Intercom)][ansi(hb,>)] [switch(mid(%0,0,1),:,[ansi(hg,[name(%#)])]%b[ansi(hr,[mid(%0,1,strlen(%0))])],;,[ansi(hg,[name(%#)])][ansi(hr,[mid(%0,1,strlen(%0))])],{[ansi(hg,[name(%#)])] [ansi(r,says ")][ansi(hr,%0)][ansi(r,")]})]
&CREWLIST IAV Explorer-1=$crew list:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Crew Access List for [name(me)] follows:[iter(v(crewlist),%r[get(##/class)] [name(##)](##[flags(##)]).)]%r--- end ---}
&COM-LIST IAV Explorer-1=$command list:@switch [u(CommandAccess,%#)]=0, {@pemit %#=[u(AccessDenied)]}, {@pemit %#=Command Access List for [name(me)] follows:[iter(v(commandlist),%r[get(##/class)] [name(##)](##[flags(##)]).)]%r--- end ---}
&CREW-2 IAV Explorer-1=$crew del *:@switch [u(CommandAccess,%#)][locate(%#,%0,*)]=#-1,{@pemit %#=I don't see that here.},#-2,{@pemit %#=I don't know which one you mean!},0*,{@pemit %#=[u(AccessDenied)]},{&temp me=[locate(%#,%0,*)];&crewlist me=[switch(u(CrewAccess,v(temp)),0,pemit(%#,[name(v(temp))] is not on the Crew List.),pemit(%#,[name(v(temp))] has been removed from the Crew List.))][remove(v(crewlist),v(temp))]}
&CREW-1 IAV Explorer-1=$crew add *:@switch [u(CommandAccess,%#)][locate(%#,%0,*)]=#-1,{@pemit %#=I don't see that here.},#-2,{@pemit %#=I don't know which one you mean!},0*,{@pemit %#=[u(AccessDenied)]},{&temp me=[locate(%#,%0,*)];&crewlist me=[switch(u(CrewAccess,v(temp)),1,[v(crewlist)][pemit(%#,[name(v(temp))] is already on the Crew List.)],[v(crewlist)] [v(temp)][pemit(%#,[name(v(temp))] has been added to the Crew List.)])]}
&COMMAND-2 IAV Explorer-1=$command del *:@switch [u(CommandAccess,%#)][locate(%#,%0,*)]=#-1,{@pemit %#=I don't see that here.},#-2,{@pemit %#=I don't know which one you mean!},0*,{@pemit %#=[u(AccessDenied)]},{&temp me=[locate(%#,%0,*)];&commandlist me=[switch(u(CommandAccess,v(temp)),0,pemit(%#,[name(v(temp))] is not on the Command List.),pemit(%#,[name(v(temp))] has been removed from the Command List.))][remove(v(commandlist),v(temp))]}
&COMMAND-1 IAV Explorer-1=$command add *:@switch [u(CommandAccess,%#)][locate(%#,%0,*)]=#-1,{@pemit %#=I don't see that here.},#-2,{@pemit %#=I don't know which one you mean!},0*,{@pemit %#=[u(AccessDenied)]},{&temp me=[locate(%#,%0,*)];&commandlist me=[switch(u(CommandAccess,v(temp)),1,[v(commandlist)][pemit(%#,[name(v(temp))] is already on the Command List.)],[v(commandlist)] [v(temp)][pemit(%#,[name(v(temp))] has been added to the Command List.)])]}
&CHELP-1 IAV Explorer-1=$chelp:@pemit %#=[repeat(=,79)]%r[center([name(me)] Commands Help,79)]%r[repeat(=,79)]%r[u(chelp,<alert> ALERT,{Initiates <alert> alert, where <alert> is RED, YELLOW, GREEN, BLUE, or INTRUDER.})]%r[u(chelp,INT <message>,Sends <message> throughout the ship using the Intercom system.)]%r[u(chelp,CREW LIST,Displays the list of people with crew access.)]%r[u(chelp,COMMAND LIST,Displays the list of people with command access.)]%r[u(chelp,CREW ADD <person>,Adds <person> to the crew list.)]%r[u(chelp,CREW DEL <person>,Deletes <person> from the crew list.)]%r[u(chelp,COMMAND ADD <person>,Adds <person> to the crew list.)]%r[u(chelp,COMMAND DEL <person>,Deletes <person> from the crew list.)]%r%rAll commands except for INT are restricted to personnel with Command Access%ronly.%r[repeat(=,79)]
&LOCATE IAV Explorer-1=$locate *:[pemit(%#,You say, "Computer, locate %0.")][oemit(%L/%#,%n says, "Computer, locate %0.")][switch([isinzone([num(*%0)],[zone(loc(*%#))])],1,{[remit([loc(%#)],[ansi(b,The computer chirps, ")][ansi(g,[class(%0)] [name(*%0)] is located at [name([loc(*%0)])])][ansi(b,")].)]},0,{[switch([hastype(*%0,player)],1,[remit([loc(%#)],[ansi(b,The computer chirps, ")][ansi(g,[class(%0)] [name(*%0)] is not onboard the [v(shipname)])][ansi(b,.")])],#-1,[remit([loc(%#)],The computer chirps, "[ansi(c,%0 is not onboard the [v(shipname)])].")])]})]
&ASLEEP IAV Explorer-1=[ljust(Asleep,7)][ansi(g,|)][ljust(mid(cat(class(%0),name(%0)),0,20),20)][ansi(g,|)][ljust(get(%0/race),10)][ansi(g,|)][mid(name(loc(%0)),0,26)]%r
&AWAKE IAV Explorer-1=[ansi(hc,[ljust(Awake,7)])][ansi(g,|)][ansi(hc,[ljust(mid(cat(class(%0),name(%0)),0,20),20)])][ansi(g,|)][ansi(hc,[ljust(get(%0/race),10)])][ansi(g,|)][ansi(hc,[ljust([mid(name(loc(%0)),0,26)],26)]%r)]
&LISTON IAV Explorer-1=$list onboard:@pe %#=You say, "Computer, list onboard.";@oemit %L/%#=%n says, "Computer, list onboard";@pe %#=[u(bar)]%r%b[ljust(Status,7)][ansi(g,|)][ljust(Name,20)][ansi(g,|)][ljust(Race,10)][ansi(g,|)] Location%r[ansi(g,--------+[repeat(-,20)]+[repeat(-,10)]+[repeat(-,27)])];@pe %#=[space(1)][iter(pinzone([zone(loc(%#))]),[ifelse(isconn(##),u(awake,##),u(asleep,##))])];@wait 1=@pe %#=[u(endbar)]
&LISTON2 IAV Explorer-1=$$List onboard:@pe %#=You say, "Computer, list onboard.";@oemit %L/%#=%n says, "Computer, list onboard";@pe %#=[u(bar)]%r%b[u(entry)]%r[u(endbar)]
&BAR IAV Explorer-1=%b[ansi(b,[repeat(_,5)])][space(45)][ansi(b,[repeat(_,17)]%r[ansi(b,\()][ansi(b,[repeat(_,5)])][ansi(b,|)]%b[ansi(r,Internal Sensors)]%b[ansi(g,[time()])]%b[ansi(b,|)][ansi(b,[repeat(_,17)])][ansi(b,\))]%r
&ENDBAR IAV Explorer-1=%b[ansi(b,[repeat(_,5)])][space(13)][ansi(b,[repeat(_,22)][space(14)][ansi(b,[repeat(_,9)])]%r[ansi(b,\()][ansi(b,[repeat(_,5)])][ansi(b,|)]%b[ansi(g,[get(me/shipname)])]%b[ansi(b,|)][ansi(b,[repeat(_,22)])][ansi(b,|)]%b[ansi(g,LCARS [rand(9)][rand(9)][rand(9)][rand(9)]%b[ansi(b,|)][ansi(b,[repeat(_,9)])][ansi(b,\))]
&ENTRY IAV Explorer-1=[iter([pinzone(me)],[ansi(g,[name(##)])]%r)]
&LIGHTS_ON IAV Explorer-1=$raise:@remit %l=[name(%#)] says, "Computer raise lights.";@swi [true(xget(%l,DIM_STAT))][true(xget(%l,desc_copy))]=0*,{&DIM_STAT %l=Bright},*0,{@cpa %l/desc=%l/desc_copy},**0,;@swi [setq(0,[xget(%l,DIM_STAT)])][strmatch(%q0,Dim)][strmatch(%q0,Dark)]=00,{@remit %l=The Computer says, "[ansi(c,Illumination already at it's maximum intensity.)]"},01,{@remit %l=The Computer says, "[ansi(c,Illumination intensity increased to 50%%.)]" as the lights of the room increase.;&DIM_STAT %l=Dim;@desc %l=[xget(%l,dim_desc)];@set %l=!dark},10,{@remit %l=The Computer says, "[ansi(c,Illumination raised maximum.)]" as the lights increase to full lighting.;&DIM_STAT %l=Bright;@desc %l=[xget(%l,desc_copy)];@wipe %l/describe;@set %l=dark}
&LIGHTS_OFF IAV Explorer-1=$dim:@remit %l=[name(%#)] says, "Computer dim lights.";@swi [true(xget(%l,DIM_STAT))][true(xget(%l,desc_copy))]=0*,{&DIM_STAT %l=Bright},*0,{@cpa %l/desc=%l/desc_copy},;@swi [setq(0,[xget(%l,DIM_STAT)])][strmatch(%q0,Dim)][strmatch(%q0,Dark)]=00,{&dim_stat %l=Dim;@desc %l=[xget(%l,dim_desc)];@remit %l=The Computer says, "[ansi(c,Illumination intensity decreased to 50%%.)]" as the lights to the room dim.},10,{@remit %l=The Computer says, "[ansi(c,Illumination intensity decreased to 0%%.)]" as the lights shifts off and darkness shrouds the room.;&DIM_STAT %l=Dark;@set %l=dark;@desc %l=Darkness prevails everywhere.},01,{@remit %l=The computer says, "[ansi(c,Illumination already at is lowest setting.)]"}
&CHELP IAV Explorer-1=[ljust(%0,20)] - [indent(23,56,{%1})]
&INTRUDER IAV Explorer-1=^* has arrived.:@switch [u(CrewAccess,%#)]=0, {&salert me=[ansi(hr,INTRUDER ALERT)];@zemit %!=[ansi(hr,=== INTRUDER ALERT === === INTRUDER ALERT ===)]%r[switch(type(%#),PLAYER,%n(%#[flags(%#)]) detected in [name(loc(%#))]([loc(%#)][flags(loc(%#))])!,%n(%#[flags(%#)]) owned by [name(owner(%#))]([owner(%#)][flags(owner(%#))]) detected in [name(loc(%#))]([loc(%#)][flags(loc(%#))])!)]%rAll security personnel please prepare for a possible combat situation.}
&CREWACCESS IAV Explorer-1=[or(or(member(v(crewlist),%0),member(v(commandlist),%0)),or(hasflag(%0,WIZARD),switch(get(%0/empire),Federation,1,0)))]
&COMMANDACCESS IAV Explorer-1=[t(member(v(commandlist),%0))]
&ACCESSDENIED IAV Explorer-1=[ansi(r,ACCESS DENIED.)] That command is restricted to Command Personnel only.
&COMMANDLIST IAV Explorer-1=#3
&ROOM_PARENT IAV Explorer-1=#4979
&TEMP IAV Explorer-1=#2292
&LIST_MOTD IAV Explorer-1=$+motd:@pe %#=[ansi(hb,repeat(~*,39))]%r[v(motd)]%rType '+Motd' to see this message again.%r[ansi(hb,repeat(~*,39))]
&SET_MOTD IAV Explorer-1=$+setmotd *:@switch [get([v(room_parent)]/commandlist)]=*%#*,{&motd me=%0;@pe %#=MOTD Changed.},{@pe %#=Access Denied, %N}
@DESCRIBE IAV Explorer-1=This is the Zone for the [v(shipname)].

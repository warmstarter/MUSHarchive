@create Starship Room Parent
@link Starship Room Parent=[owner(me)]
@set Starship Room Parent=!NO_COMMAND
@set Starship Room Parent=HAVEN
@set Starship Room Parent=LINK_OK
@set Starship Room Parent=WIZARD
@set Starship Room Parent=SAFE
@set Starship Room Parent=ROYALTY
&POS_#39 Starship Room Parent=Crewman
&POS_#75 Starship Room Parent=Crewman
&LHELP_+ROSTER Starship Room Parent=+Roster shows the roster of the starbase.
&+POSITION Starship Room Parent=$+position:@pemit %#=%r[center([ansi(hb,-=*Positions*=-)],79)]%r-Position-[space(8)]-Abbreviation-%r[ljust(Commanding Officer,18)][space(6)]CO%r[ljust(Executive Officer,18)][space(6)]XO%r[ljust(Second Officer,18)][space(6)]SO%r[ljust(Navigation Officer,18)][space(6)]NO%r[ljust(Helmsman,18)][space(6)]HO%r
&LHELP_+POSITION Starship Room Parent=+position tells you what the position abreviation means.
&LHELP_ Starship Room Parent=+POSITION
&POS_#40 Starship Room Parent=XO
&POS_#17 Starship Room Parent=SO
&RST Starship Room Parent=#28 #40 #17 #75
&POS_#28 Starship Room Parent=CO
&COMMAND Starship Room Parent= #17  #95 #40
&RECOGNIZE Starship Room Parent=Steve Aramut mToqA Darkt Mithrandir Trillian
@DESCRIBE Starship Room Parent=[ansi(v(linecol),----)] [edit(edit(name(zone(me)),%bZone,),%bZMO,)] [ansi(v(linecol),[repeat(-,[sub(73,strlen(edit(edit(name(zone(me)),Zone,),ZMO,)))])])]%r[ansi(wh,[eval(me,desC_Store)])]%r%r[ansi(bh,-)] [ansi(ch,People)] [ansI(bh,-)] [squish([iter(lcon(me),switch(type(##),PLAYER,switch(idle(##),-1,,switch(hasflag(##,Dark),0,edit(name(##),Console,) [switch(hasflag(##,IC),1,(IC))] [switch(hasattr(##,CAPTION),1,{Brandishing: [get(##/CAPTION)]})]
&ALERT_STATUS Starship Room Parent=[ansi(hg,Green)]
&ROSTER Starship Room Parent=$+roster:@remit me=%n uses the computer terminal.;@pemit %#=%r<>==-- [ansi(h,v(shipname))] --==<>%r%r[ljust(Crew members:,25)][ljust(Position:,25)][iter([v(rst)],%r[ljust([get(##/class)] [name(##)],25)][v(pos_[##])])]
&RED Starship Room Parent=$+red:@pemit %#=You push a button on the comm panel and trigger the red alert.;@dolist [v(room_list)]=@remit ##=[ansi(hr,[center(-=- -=- -=- Red Alert -=- -=- -=- Red Alert -=- -=- -=- Red Alert -=- -=- -=-,80)][center(All Hands Report To Your battle Stations,80)][center(Prepare For Combat,80)])][ansi(hr,*<PA System>*)] From the [name(loc(%#))]%, %n says%, "Red Alert!!! All hands battle stations.";&alert_status me=%[ansi(hr,Red)];&alert_by me=%#
&YELLOW Starship Room Parent=$+yellow:@pemit %#=You push a button on the comm panel and trigger the yellow alert.;@dolist [v(room_list)]=@remit ##=[ansi(hy,[center(::::::::: Yellow Alert ::::::::: Yellow Alert ::::::::: Yellow Alert ::::::::,80)][center(All Hands Report To Battle Stations,80)][center(The Ship Is Now On Yellow Alert,80)][center(Assume Battle Readiness,79)])];&alert_status me=%[ansi(hy,Yellow)];&alert_by me=%#
&BLUE Starship Room Parent=$+blue:@pemit %#=You push a button on the comm panel and trigger the blue alert.;@dolist [v(room_list)]=@remit ##=[ansi(hb,[center(<*> <*> <*> Blue Alert <*> <*> <*> Blue Alert <*> <*> <*> Blue Alert <*> <*> <*>,80)][center(* * * All Hands Report To Your Stations * * *,79)][center(* * * Prepare To Dock * * *,79)])];&alert_status me=%[ansi(hb,Blue)];&alert_by me=%#
&GREEN Starship Room Parent=$+green:@pemit %#=You push a button on the comm panel and trigger the green alert.;@dolist [v(room_list)]=@remit ##=[ansi(hg,[center(<O><O><O> Green Alert <O><O><O> Green Alert <O><O><O> Green Alert <O><O><O>,80)][center(All Hands Resume Normal Duty Stations,80)][center(Previous Alert Canceled,79)])];&alert_status me=%[ansi(hg,Green)];&alert_by me=%#
&PA_ON Starship Room Parent=$+int pa:@switch [not(not(match(v(room_int),[loc(*%#)])))]=1,{&int me=pa;@remit me=%n activates the ship-wide PA system.},@pemit %#=This room doesn't have an intercom.
&INT_OFF Starship Room Parent=$+int off:@switch [not(not(match(v(room_int),[loc(*%#)])))]=1,{&int me=;@remit me=%n deactivates the intercom.},@pemit %#=This room has no intercom to deactivate.
&ALL_INT_OFF Starship Room Parent=$+all int off:@switch [v(command)]=*%#*,{@dolist [v(room_list)]=&int ##=;@remit me=%n punches a few buttons and deactivates the ship's entire intercom system.},@pemit %#=ACCESS DENIED.
&INT_ON Starship Room Parent=$+int *:@switch [isnum(%0)]=1,{@switch [not(not(match(v(room_int),[loc(*%#)])))]=1,{&int me=[extract(v(room_list),%0,1)];@remit me=%n activates the intercom to the [name(extract(v(room_int),%0,1))].},@pemit %#=This room doesn't have an intercom.}
&INT_LIST Starship Room Parent=$+int:@pemit %#=The following are valid room numbers:[iter(v(room_int),%r[ljust(match(v(room_int),##),3)] [name(##)])]
&BIO Starship Room Parent=$+bio:@remit me=%n uses the computer terminal.;@pemit %#=%r<>==-- [ansi(h,v(shipname))] --==<>%r%r[ljust(Bio Signature:,25)]Location:[iter(iter(iter(v(room_list),lcon(##)),[switch(type(##),PLAYER,##)]),%r[switch(not(not(match(v(recognize),##))),1,[ljust([get(##/class)] [name(##)],25)][name(loc(##))],0,[ljust([xget(##,sex)] [xget(##,race)],25)][name(loc(##))])])]
&BIO+ Starship Room Parent=$bio+ *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&recognize me=[edit(v(recognize),[num(*%0)],)];@wait 1=&recognize me=[v(recognize)] [num(*%0)]},@pemit %#=ACCESS DENIED!
&BIO- Starship Room Parent=$bio- *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&recognize me=[edit(v(recognize),[num(*%0)],)]},@pemit %#=ACCESS DENIED!
&CMD+ Starship Room Parent=$cmd+ *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&command me=[edit(v(command),[num(*%0)],)];@wait 1=&command me=[v(command)] [num(*%0)]},@pemit %#=ACCESS DENIED!
&CMD- Starship Room Parent=$cmd- *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&command me=[edit(v(command),[num(*%0)],)]},@pemit %#=ACCESS DENIED!
&BRD+ Starship Room Parent=$brd+ *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&board me=[edit(v(board),[num(*%0)],)];@wait 1=&board me=[v(board)] [num(*%0)]},@pemit %#=ACCESS DENIED!
&BRD- Starship Room Parent=$brd- *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&board me=[edit(v(board),[num(*%0)],)]},@pemit %#=ACCESS DENIED!
&RST+ Starship Room Parent=$rst+ *=*:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&rst me=[edit(v(rst),[num(*%0)],)];@wait 1=&rst me=[v(rst)] [num(*%0)];&pos_[num(*%0)] me=%1},@pemit %#=ACCESS DENIED!
&RST- Starship Room Parent=$rst- *:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;&rst me=[edit(v(rst),[num(*%0)],)];&pos_[num(*%0)] me=},@pemit %#=ACCESS DENIED!
&LOCKDOWN Starship Room Parent=$+lockdown:@swi [v(lockdown_status)]=off,{@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;@wait 2=&lockdown_by me=%#;@wait 2=&lockdown_status me=on;@wait 2=@dolist [iter(v(room_list),lexits(##))]={&old_lock ##=[lock(##)];@lock ##=[owner(##)];@remit [home(##)]=[name(##)] locks with a click.}},@pemit %#=ACCESS DENIED!},on,@pemit %#=The lockdown is already activated.
&UNLOCKDOWN Starship Room Parent=$+unlockdown:@swi [v(lockdown_status)]=on,{@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;@wait 2=&lockdown_status me=off;@wait 2=@dolist [iter(v(room_list),lexits(##))]={@switch [get(##/old_lock)]=#95,@lock ##=,@lock ##=[get(##/old_lock)];@remit [home(##)]=[name(##)] unlockswith a click.}},@pemit %#=ACCESS DENIED!},off,@pemit %#=The lockdown is alreadyde-activated.
&LOCKDOWN_STATUS Starship Room Parent=off
&COMMANDS Starship Room Parent=$commands:@pemit %#=[ansi(hm,<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>)]%r[ansi(hr,[center(Restricted Commands:,80)])]%rcmd<+/-> <Player>%t -Add or remove <player> from the command list.%rbio<+/-> <Player>%t -Add or remove <player> from the bio-recognition list.%rbrd<+/-> <Player>%t -Add or remove <player> from the boarding list.%rrst<+/-> <Player>%t -Add or remove <player> from the crew roster.%r+all int off%t%t -Turn off all intercoms ship-wide.%r+setmotd <New MOTD>%t -Set a new MOTD.%r+<un>lockdown%t%t -Activate or deactivate the security lockdown.%r+sec%t%t%t -Bringup a security status report.%r[ansi(hm,~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~)]%r[ansi(hg,[center(Un-Restricted Commands:,80)])]%r+roster%t%t%t -Bring up the crew roster.%r+bio%t%t%t -Use the ship's internal sensors to see who's aboard.%r+int%t%t%t -Bring up a room list with intercom numbers.%r+<red/yellow/blue/green> -Change the alert status.%r+int pa%t%t%t -Activate the ship-wide pa.%r+int <room number>%t-Activate the intercom to room <room number>.%r+int off%t%t -Deactivate the room's intercom or pa.%rint <message>%t%t -To talk into the intercom/pa.%r+MOTD%t%t%t -Display the MOTD.%r[ansi(hm,<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>*<*>)]
&INFORMATION Starship Room Parent=$info:@pemit %#=%rAttributes to set on your ZMO coded by Steve:%r@parent <ZMO>=#657%r&room_parent <ZMO>=<Room Parent's Dbref>%r%rAttributes to set on your room parent coded by N'tok:%r@parent <room parent>=#628%r&zone <room parent>=<Dbref of ZMO>%r&room_list <room_parent>=<A list of all rooms on the ship>%r&room_int <room parent>=<A list of rooms with intercoms>%r&command <room parent>=<BC's Dbref>%r&board <room parent>=<BC's Dbref>%r&shipname <room parent>=<Name of Ship>%r&clock <room parent>=<on/off> (can be changed in each room also)%r%rAfter Completion:%r+Setup Ship%r%rReport all errors to Steve.
&SEC Starship Room Parent=$+sec:@swi [v(command)]=*%#*,{@remit me=%n uses the computer terminal.;@pemit %#=%r<>==-- [ansi(h,v(shipname))] --==<>%rSecurity Status Report:%r%rCommand Access:[iter(v(command),%r[get(##/class)] [name(##)])]%r%rBoarding Access:[iter(v(board),%r[get(##/class)] [name(##)])]%r%rShip Alert Change By:%r[get(v(alert_by)/class)] [name(v(alert_by))]%r%rLockdown Status:%r[switch(v(lockdown_status),on,[ansi(hr,<ACTIVE>)]%r%rLockdown Activated By:%r[iter(v(lockdown_by),name(##))],off,ansi(hg,<INACTIVE>))]},@pemit %#=ACCESS DENIED!
&SEND Starship Room Parent=$int *:@switch [v(int)]=pa,{@dolist [v(room_list)]=@remit ##=[ansi(hr,*<PA System>*)] From the [name(me)]%, %n says%, "%0";@remit [loc(%#)]=%n says, "%0" into the intercom.},#*,{@remit [v(int)]=[ansi(hr,*<Intercom>*)] From the [name(me)]%, %n says%, "%0";@remit [loc(%#)]=%n says, "%0" into the intercom.}
&CLOCK Starship Room Parent=off
@SUCCESS Starship Room Parent=[switch(v(clock),on,The Clock Reads: [extract(time(),4,1)]%r)]Alert Status: [u(alert_status)]

@create Universe
@link Universe=#0
@set Universe=WIZARD
@set Universe=ROYALTY
@set Universe=SAFE
@set Universe=!NO_COMMAND
@DESCRIBE Universe=The Universal Functions object. To make a function, set the FUN_<function name> attribute of this object to the function.
&FUN_POSITION Universe=[switch(%0,me,[get(%#/title)],[get(*%0/title)])]
&FUN_TRUE Universe=[switch(%0,0,0,1)]
&FUN_ISBUILDER Universe=[haspower([num(*%0)],builder)]
&FUN_ISCONN Universe=hasflag(secure(pmatch(%0)),CONNECTED)
&FUN_ISDARK Universe=hasflag(secure(%0),DARK)
&FUN_LNUMA Universe=[lnum(1,%0)]
&FUN_TIMECONV Universe=[Edit(s({[Rjust(match(Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec,Delete(Delete([time()],0,4),3,17)),2)]/[Delete(Delete([time()],0,8),2,14)]/[Delete([time()],0,22)]}),Spc(1),0)] [Delete(Delete([time()],0,11),5,8)]
&FUN_ISIC Universe=[switch([get(*%0/playstatus)],IC,1,0)]
&FUN_DIVISION Universe=[switch(%0, me, [name([get(%#/org)])], [name([get(*%0/org)])])]
&FUN_EMPIRE Universe=[switch(%0, me, [name([get(%#/masterorg)])], [name([get(*%0/masterorg)])])]
&FUN_MASTERORGNAME Universe=[switch(%0, me, [name([get(%#/masterorg)])], [name([get(*%0/masterorg)])])]
&FUN_ORGNAME Universe=[switch(%0, me, [name([get(%#/org)])], [name([get(*%0/org)])])]
&NEG Universe=[add(%0,[mul(-2,%0)])]
&FUN_PLAYSTATUS Universe=[switch(locate(%#,%0,PT*),#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,get(#$/playstatus))]
&U_INDENT Universe=[setq(0, 0)][iter(u(esc_paren,u(esc_comma,escape(edit(%2,%r,%bXXX-NEWLINE%b)))),[switch(##,XXX-NEWLINE,%r[space(%0)][setq(0,0)],[switch(gt(r(0),0),1,%b)][setq(0,add(r(0),add(strlen(##),1)))][switch(gt(r(0),%1),1,%r[space(%0)][edit({##},XXX-COMMA,{,})][setq(0,strlen(##))],[edit({##},XXX-COMMA,{,})])])],,)]
&FUN_COMMENT Universe=[]
&FUN_EALIAS Universe=[switch([setq(0,locate(%#,%0,E*))]%q0,#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,switch(or(u(Can_Examine,%#,#$),nearby(%#,#$)),0,#-1 TOO FAR AWAY TO SEE,u(%q0/ealias,%#)))]
&CAN_EXAMINE Universe=[or(controls(%0,%1),or(haspower(%0,see_all),orflags(%0,Wr)))]
&GARBAGE_CAN Universe=#101
&FUN_NUKE Universe=[switch(locate(%@,%0,*),#-1,#-1 INVALID OBJECT,#-2,#-2 AMBIGUOUS OBJECT,switch(or(controls(%@,#$),hasflag(#$,Destroy_Ok)),0,#-1 PERMISSION DENIED,[set(v(Garbage_Can),Nuke_List:[get(v(Garbage_Can)/Nuke_List)] #$)][tel(#$,v(Garbage_Can))]))]
&FUN_IFELSE Universe=[switch(t(%0),1,%1,%2)]
&FUN_IF Universe=[switch(t(%0),1,%1)]
&FUN_ECONS Universe=[switch(locate(%#,%0,PT*),#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,get(#$/econs))]
&FUN_ICTIME Universe=[setq(0,time())][left(%q0,19)] [add(right(%q0,4),376)]
&FUN_NTH Universe=[switch(%1,<1,#-1 ELEMENT NUMBER MUST BE 1 OR GREATER,1,first(%0,%2),nth(rest(%0,%2),sub(%1,1),%2))]
&INT_LPLAYERS Universe=[iter(lcon(%q0),switch(type(##),PLAYER,##))]
&FUN_LPLAYERS Universe=[setq(0,locate(%#,%0,PRT*))][switch(%q0,#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,switch(u(can_lcplayer,%#,%q0),0,#-1 PERMISSION DENIED,squish(u(int_lplayers,%q0))))]
&FUN_CLASS Universe=[setq(0,locate(%#,%0,PT*))][switch(%q0,#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,get(%q0/class))]
&INT_LCPLAYERS Universe=[iter(lcon(%q0),switch(conn(##),-1,,##))]
&SEE_ALL Universe=[or(or(hasflag(%0,wizard),hasflag(%0,royalty)),haspower(%0,see_all))]
&CAN_LCPLAYER Universe=[or(u(see_all,%0),controls(%0,%1))]
&POWERS_LIST Universe=announce boot builder cemit chat_privs functions guest halt hide idle login long_fingers no_pay no_quota pemit_all poll player_create queue quota search see_all see_queue tport_anything tport_anywhere unkillable
&FUN_POWERS Universe=[switch(locate(%#,%0,*),#-1,#-1,[setq(0,#$)][filter(haspower_filter,v(powers_list))])]
&HASPOWER_FILTER Universe=[haspower(r(0),%0)]
&FUN_LCPLAYER Universe=[squish(iter(lcon(locate(%#,%0,*)),ifthen(u(isconplayer,##),##)))]
&FUN_LCPLAYERS Universe=[setq(0,locate(%#,%0,PRT*))][switch(%q0,#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,switch(u(can_lcplayer,%#,%q0),0,#-1 PERMISSION DENIED,squish(u(int_lcplayers,%q0))))]
&FUN_ISCONPLAYER Universe=[and(and(hastype(%0,Player),orflags(%0,c)),not(hasflag(%0,Dark)))]
@STARTUP Universe=@dolist [lattr(me/FUN_*)]=@function [after(##,FUN_)]=me,##
&FUN_INDENT Universe=[ulocal(u_indent,%0,%1,%2,%3)]
&ESC_PAREN Universe=[set(me,tmp_esc_paren:[edit(%0,{\(},{\\\(})])][edit(v(tmp_esc_paren),{\)},{\\\)})]
&ESC_COMMA Universe=[edit({%0},{,},{XXX-COMMA})]
&FUN_SELECT Universe=[extract(%0,add(rand(words(%0,%1)),1),1,%1)]
&FUN_SECOND Universe=[first(rest(%0,%1),%1)]
&FUN_THIRD Universe=[first(rest(rest(%0,%1),%1),%1)]
&FUN_LTJUST Universe=[ljust(left(%0,%1),%1)]
&FUN_RACE Universe=[switch(locate(%#,%0,PT*),#-1,#-1 NO SUCH OBJECT VISIBLE,#-2,#-1 AMBIGUOUS MATCH,get(#$/race))]
&FUN_FOURTH Universe=[first(rest(rest(rest(%0,%1),%1),%1),%1)]
&FUN_FIFTH Universe=[first(rest(rest(rest(rest(%0,%1),%1),%1),%1),%1)]
&FUN_WIZARDGET Universe=[switch(hasflag(owner(%@),WIZARD),0,#-1 PERMISSION DENIED,get([locate(%@,%0,*)]/%1))]
&FUN_WIZARDFLAGS Universe=[switch(hasflag(owner(%@),WIZARD),0,#-1 PERMISSION DENIED,flags(locate(%@,%0,*)))]
&FUN_RANKNAME Universe=[switch(%0, me, [get([get(%#/org)]/rank[get(%#/rank)])], [get([get(*%0/org)]/rank[get(*%0/rank)])])]
&FUN_ORG Universe=[switch(%0, me, [get(%#/org)], [get(*%0/org)])]
&FUN_MASTERORG Universe=[switch(%0, me, [get(%#/masterorg)], [get(*%0/masterorg)])]
&FUN_TITLE Universe=[switch(%0, me, [get(%#/title)], [get(*%0/title)])]
&FUN_RANK Universe=[switch(%0, me, [get(%#/rank)], [get(*%0/rank)])]
&FUN_HASPARENT Universe=[t(member(lparent(%0),%1))]
&FUN_RACENAME Universe=[get(%0/race)]
&FUN_IFTHEN Universe=[switch([t(%0)],1,%1)]
&FUN_ALERT Universe=[get([zone([loc(%#)])]/alert)]
&FUN_INZONE Universe=[switch(%0,here,[lsearch(all,zone,[zone(loc(%#))])],[lsearch(all,zone,%0)])]
&FUN_USERDBREF Universe=ifelse(match(me,secure(%0)),%#,pmatch(secure(%0)))

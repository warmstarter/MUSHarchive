@set me=quiet
@pemit me=[repeat(=,78)]%r[center(Constructing Myrddin's BBS v3.0d,78)]%r[repeat(-,78)]%rPlease wait...%r

&BBTIME #111=extract(time(), 1, 3)
&GET_GROUP #111=switch(isnum(%0),1,extract(v(groups),%0,1),locate(#111,%0,ni))
&NXT_MESS #111=1
&VALID_GROUPS #111=iter(v(groups),switch(and(u(##/can%1,%0),not(member(get(%0/bb_omit),##))),1,##))
&ISREAD #111=switch(member(get(%0/bb_read),%1),0,U)
&ISGROUPREAD #111=switch(setdiff(%1,get(%0/bb_read)),,,U)
&UNREADNUMS #111=sort(iter(setdiff(get(%1/mess_lst),get(%0/bb_read)),member(get(%1/mess_lst),##)))
&FN_MAKELIST #111=iter(%0,switch(strmatch(##,*-*),1,lnum(before(##,-),after(##,-)),##))
@Desc #111=[iter(setdiff(lattr(me),Desc),ljust(##,18))]
@set #111=INHERIT
@set #111=SAFE


@name #222=BBS - Myrddin's Global BBS v3.0d
@Desc #222=[iter(setdiff(lattr(me),Desc),ljust(##,18))]
@Fail #222=It's bolted down... you aint budgin' it.
&CMD_+BBNEWGROUP #222=$+bbnewgroup *:@switch hasflag(%#,wizard)=1, {@create %0; @wait 1={@switch [setq(0,num(%0))]%q0=#-1,{@pemit %#=That's not a good name for a group.},{&groups #111=[switch(words(v(groups)),0,,[v(groups)]%b)]%q0; &own %q0=%#; @set %q0=safe; @set %q0=inherit; &last_mod %q0=[u(bbtime)]; &CANREAD %q0=1; &CANWRITE %q0=1; @pemit %#=Group number [member(v(groups),%q0)] added as '%0'}}}, {@pemit %#=You can't add groups to the message base.}
&CMD_+BBLOCK #222=$+bblock *=*/*:@switch hasflag(%#,wizard)=1,{@switch member(v(groups),[setq(0,u(get_group,%0))]%q0)=0,{@pemit %#=No such group as '%0'.},{@switch %1=flag,{&CANREAD %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANREAD %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @mvattr %q0=CANREAD,CANREAD,CANWRITE; @pemit %#=Group '%0' locked. Only people with %1=%2 can access.}},{@pemit %#=You cannot lock message groups.}
&CMD_+BBCLEARGROUP #222=$+bbcleargroup *:@switch or(hasflag(%#, wizard),member(v(groups),[setq(0,u(get_group,%0))]%q0))=1, {&delete_grp #111=%q0; @pemit %#=%r[space(15)]******** Warning ********%r%rDeleting Group #%0 will also delete the [words(get(%q0/mess_lst))] messages in that group.%rIf you still wish to proceed, type '+confirm %0'%r%r[space(15)][repeat(*,25)]%r}, {&delete_grp #111=; @pemit %#=You can't remove that group (either that group does not exist, or you are not the owner).}
&DO_CLEARGROUP #222=$+confirm *:@switch v(DELETE_GRP)=[setq(0,u(get_group,%0))]%q0, {&groups #111=[setdiff(v(groups),%q0)]; &master_lst #111=[setdiff(v(master_lst),get(%q0/mess_lst))]; @nuke %q0; &delete_grp #111=; @pemit %#=Group number %0 removed.}, {&delete_grp #111=; @pemit %#=That group has not been marked for deletion. Use '+bbcleargroup <#>' to mark a group for deletion.}
&CMD_+BBPOST #222=$+bbpost */*=*:@switch member(u(valid_groups,%#,write),[setq(0,u(get_group,%0))]%q0)=0, {@pemit %#=Either you do not subscribe to Group #%0, or are unable to post to it.}, {&mess_lst %q0=[sort(setunion(get(%q0/mess_lst),[setq(1,v(nxt_mess))]%q1))]; &master_lst #111=[setunion(v(master_lst),%q1)]; &hdr_%q1 %q0=[mid(%1, 0, 34)]|[u(bbtime)]|[mid(name(%#),0,24)]|%#; &last_mod %q0=[u(bbtime)]; &bdy_%q1 %q0=[%2][switch(hasattr(%#,bb_sig),1,%r[ulocal(%#/bb_sig)])]; @pemit %#=You post your note about '%1' in group [member(v(groups),%q0)] ([name(%q0)]) as message #[member(get(%q0/mess_lst),%q1)]; &bb_read %#=[setunion(get(%#/bb_read),%q1)]; &nxt_mess #111=[add(%q1,1)]}
&CMD_+BBREMOVE #222=$+bbremove */*:@dolist [revwords(sort(u(fn_makelist,%1)))][setq(0,u(get_group,%0))]={@switch or(strmatch(index(get(%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),##,1))]%q1),|,4, 1),%#), hasflag(%#,wizard))=1, {&HDR_%q1 %q0=; &BDY_%q1 %q0=; &MESS_LST %q0=[sort(setdiff(get(%q0/MESS_LST),%q1))]; &MASTER_LST #111=[setdiff(v(MASTER_LST),%q1)]; @pemit %#=Message ## removed from group #[member(v(groups),%q0)] ([name(%q0)]).}, {@pemit %#=Either message ## does not exist, or you were not the original poster.}}
&CMD_+BBREAD2 #222=$+bbread *:@switch strmatch([%0],*/*)=0,{@switch member(u(valid_groups,%#,read),[setq(0,u(get_group,%0))]%q0)=0,{@pemit %#=You do not subscribe to that Group.},{@pemit %#=[repeat(=,78)]%r[center(**** [name(%q0)] ****,78)]%r[space(8)]Message[space(28)]Posted[space(8)]By%r[repeat(-,78)]; @dolist get(%q0/MESS_LST)={@pemit %#=[ljust([member(v(groups),%q0)]/[member(get(%q0/MESS_LST),##)],6)][ljust(u(isread,%#,##),2)][ljust(index([setq(1,get(%q0/HDR_##))]%q1,|,1,1),35)][ljust(index(%q1,|,2,1),14)][index(%q1,|,3,1)][switch(member(get(%q0/MESS_LST),##),words(get(%q0/MESS_LST)),%r[repeat(=,78)])]}}},{@switch member(u(valid_groups,%#,read),[setq(0,u(get_group,[setq(2,first(%0,/))]%q2))]%q0)=0,{@pemit %#=You do not subscribe to that Group.},{@dolist [setq(4,member(v(groups),%q0))][switch(rest(%0,/),u,u(unreadnums,%#,%q0),u(fn_makelist,rest(%0,/)))]={@switch [and(lte(##,words(get(%q0/MESS_LST))),isnum(##))]=0,{@pemit %#=Message %q4/## ([name(%q0)]/##) does not exist.},{@pemit %#
=%r[center(= [name(%q0)] =,78,=)]%r[ljust(Message: %q4/##,35)]Posted[space(8)]Author%r[ljust(index([setq(3,get(%q0/HDR_[extract(get(%q0/MESS_LST),##,1)]))]%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][index(%q3,|,3,1)]%r[repeat(-,78)]%r[get(%q0/BDY_[extract(get(%q0/MESS_LST),##,1)])]%r[repeat(=,78)]; &bb_read %#=[setunion(get(%#/bb_read),[extract(get(%q0/MESS_LST),##,1)])]}}; &bb_read %#=[setinter(get(%#/bb_read),v(master_lst))]}}
&CMD_+BBEDIT #222=$+bbedit */*=*/*:@switch strmatch(index(get([setq(0,u(get_group,%0))]%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),%1,1))]%q1),|,4, 1),%#)=1,{&BDY_%q1 %q0=[edit(get(%q0/BDY_%q1),{%2},{%3})]; @pemit %#=[repeat(=,78)]%rMessage [member(v(groups),%q0)]/%1 ([name(%q0)]/%1) now reads:%r[repeat(-,78)]%r[get_eval(%q0/BDY_%q1)]%r[repeat(=,78)]}, {@pemit %#=Either that message does not exist, or you were not the original poster.}
&CMD_+BBLEAVE #222=$+bbleave *:@switch member(u(valid_groups,%#,read),[setq(0,u(get_group,%0))]%q0)=0,{@pemit %#=You aren't currently subscribing to Board #%0. No ommission necessary.},{&bb_omit %#=[setunion(get(%#/bb_omit),%q0)]; @pemit %#=You have removed yourself from the [name(%q0)] board.}
&CMD_+BBJOIN #222=$+bbjoin *:@switch member(NULL [get(%#/bb_omit)],[setq(0,u(get_group,%0))]%q0)=0,{@pemit %#=[switch(member(u(valid_groups,%#,read),%q0),0,{Sorry, you don't have access to that board.},{You are already a member of the [name(%q0)] board.})]},{&bb_omit %#=[setdiff(get(%#/bb_omit),%q0)]; @pemit %#=You have joined the [name(%q0)] board.}
&CMD_+BBSCAN #222=$+bbscan:@switch hasattr(#111,bb_post_hdr_%#)=1,{@pemit %#=** BB Warning: You are in the middle of writing a bbpost. **}; @switch words(setdiff(iter(u(valid_groups,%#,read),get(##/mess_lst)),get(%#/bb_read)))=0,{@pemit %#=There are no unread postings on the Global Bulletin Board.},{@pemit %#=[center(- Unread Postings on Global Bulletin Board -,78,-)]; @dolist [setq(0,u(valid_groups,%#,read))][setq(1,get(%#/bb_read))]%q0 END={@switch ##=END,@notify me,{@switch [setq(2,words(setdiff(get(##/mess_lst),%q1)))]%q2=0,,{@pemit %#=There are %q2 unread postings on the [name(##)] (#[member(v(groups),##)]) board.}}}; @wait me={@pemit %#=[repeat(-,78)]}}
&CMD_+BBLIST #222=$+bblist:@pemit %#=[repeat(=,78)]%r[ljust(Available Bulletin Board Groups,37)]Member?%r[repeat(-,78)]%r%b[iter(v(groups),switch(u(##/canread,%#),1,{%r%b[trim([ljust(member(v(groups),##),5)][ljust(name(##),32)][switch(member(get(%#/bb_omit),##),0,Yes,No)])]}))]%r[repeat(-,78)]%rTo join groups, type '+bbjoin <group number or name>'%r[repeat(=,78)]
&CMD_+BBWIZHELP #222=$+bbwizhelp:@pemit %#=[switch(hasflag(%#,wizard),1,{[repeat(=,78)]%r[center(Wizard Commands for Bulletin Board 2.2+,78)]%r[repeat(-,78)]%r[space(5)]+bbnewgroup <title>[space(13)]Creates a new group.%r[space(5)]+bbcleargroup <#>[space(15)]Clears group <#> and all it's messages.%r[space(5)][ljust(+bblock <#>=<lock>/<key>,32)]Locks group <#>. Lock can be 'flag' or%r[space(39)]an attribute name.%r[space(39)]Examples: +bblock 4=flag/wizard%r[space(49)]+bblock 5=race/were%r[space(5)][ljust(+bbwritelock <#>=<lock>/<key>,32)]Same as above, but controls who may write%r[space(39)]to the group.%r%r[space(5)][ljust(+bbclean,15)]Removes obsolete 'message serial numbers' from bb related%r[space(20)]attributes. Run this command once every couple of weeks.%r[repeat(-,78)]%r[center({Written by WyldeFyre (Myrddin@Requiem/Elysium)},78)]%r[repeat(=,78)]},{Huh?%b%b(Type "help" for help.)})]
&CREDITS #222=Myrddin's BBS v3.0d was written by Myrddin@Dreaming/Elysium (email: merlin@best.com). Several more additions will be made before 3.1 is put out for general release. This code is available (in some form) at http://www.best.com/~merlin/mushcode.html, or ftp.best.com:/pub/merlin. Please refer interested parties to myself or those addresses, thank you. :)
&CMD_+BBCATCHUP #222=$+bbcatchup *:@switch lcstr(%0)=all,{&bb_read %#=[v(master_lst)]; @pemit %#=All postings on all boards marked as read.},{@dolist [setq(1,u(valid_groups,%#,read))][u(fn_makelist,%0)]={@switch member(%q1,[setq(0,u(get_group,##))]%q0)=0,{@pemit %#=You do not subscribe to Group ##.},{ &bb_read %#=[setunion(get(%#/bb_read),get(%q0/MESS_LST))]; @pemit %#=All postings on Group #[member(v(groups),%q0)] ([name(%q0)]) marked as read.}}}
&CMD_+BBMOVE #222=$+bbmove */* to *:@switch and(member(u(valid_groups,%#,read),[setq(0,u(get_group,%0))]%q0),member(u(valid_groups,%#,write),[setq(2,u(get_group,%2))]%q2))=0,{@pemit %#=One of the selected groups is not valid (either it doesn't exist or you can't write to it).},{@switch or(strmatch(index(get(%q0/HDR_[setq(1,extract(get(%q0/MESS_LST),%1,1))]%q1),|,4, 1),%#), and(hasflag(%#,wizard),member(get(%q0/MESS_LST),%q1)))=1,{@set %q2=HDR_%q1:_%q0/HDR_%q1; @set %q2=BDY_%q1:_%q0/BDY_%q1; &MESS_LST %q2=[sort(setunion(get(%q2/mess_lst),%q1))]; &MESS_LST %q0=[sort(setdiff(get(%q0/MESS_LST),%q1))]; &HDR_%q1 %q0; &BDY_%q1 %q0; @pemit %#=Message '%1' removed from group '%0' and added to group '%2' as message #[member(get(%q2/mess_lst),%q1)]},{@pemit %#=Not a valid message number for that group.}
&CMD_+BBWRITELOCK #222=$+bbwritelock *=*/*:@switch hasflag(%#,wizard)=1,{@switch member(v(groups),[setq(0,u(get_group,%0))]%q0)=0,{@pemit %#=No such group as '%0'.},{@switch %1=flag,{&CANWRITE %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANWRITE %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @pemit %#=Group '%0' locked. Only people with %1=%2 can write.}},{@pemit %#=You cannot lock message groups.}
&CMD_+BBPOST2 #222=$+bbpost */*:@switch hasattr(#111,bb_post_hdr_%#)=1,{@pemit %#=You are already in the middle of writing a bbpost.},{@switch strmatch(%1,*=*)=0,{@switch member(u(valid_groups,%#,write),[setq(0,u(get_group,%0))]%q0)=0,{@pemit %#=Either you do not subscribe to Group #%0 or are unable to post to it.},{&bb_post_hdr_%# #111=%q0|%1; @pemit %#=%rYou start your posting to Group #[member(v(groups),%q0)] ([name(%q0)]).%rYou can now compose the body of the post by using '+bbwrite <text>'%ror '+bb <text>'. When you are finished, type '+bbpost' by itself.}}}
&CMD_+BBWRITE #222=$+bbwrite *:@switch hasattr(#111,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# #111=[v(bb_post_bdy_%#)]%0; @pemit %#=Text added to bbpost.}
&CMD_+BBPROOF #222=$+bbproof:@switch hasattr(#111,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{@pemit %#=[center(= BB Post in Progress =,78,=)]%rGroup: %b[name(index(v(bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(v(bb_post_hdr_%#),|,2,1)]%r[repeat(-,78)]%r[trim(v(bb_post_bdy_%#))]%r[repeat(=,78)]}
&CMD_+BBPOST-POST #222=$+bbpost:@switch [hasattr(#111,bb_post_hdr_%#)]:[hasattr(#111,bb_post_bdy_%#)]=0:0,{@pemit %#=You do not have a bbpost in progress.},1:0,{@pemit %#=Your post is empty. Please add text with the '+bbwrite <text>' command or discard the posting with the '+bbtoss' command.},{&mess_lst [setq(0,index(v(bb_post_hdr_%#),|,1,1))]%q0=[sort(setunion(get(%q0/mess_lst),[setq(1,v(nxt_mess))]%q1))]; &master_lst #111=[setunion(v(master_lst),%q1)]; &hdr_%q1 %q0=[index(v(bb_post_hdr_%#),|,2,1)]|[u(bbtime)]|[mid(name(%#),0,24)]|%#; &last_mod %q0=[u(bbtime)]; &bdy_%q1 %q0={[trim(v(bb_post_bdy_%#))][switch(hasattr(%#,bb_sig),1,{%r[u(%#/bb_sig)]})]}; @pemit %#=You post your note about '[index(v(bb_post_hdr_%#),|,2,1)]' in group '[name(%q0)]' as message #[member(get(%q0/mess_lst),%q1)]; &bb_read %#=[setunion(get(%#/bb_read),%q1)]; &nxt_mess #111=[add(%q1,1)]; &bb_post_bdy_%# #111; &bb_post_hdr_%# #111}
&CMD_+BBREAD #222=$+bbread:@pemit %#=[repeat(=,78)]%r[space(7)]Group Name[space(20)]Last Post[space(6)]# of messages%r[repeat(-,78)][iter(u(valid_groups,%#,read),%r[rjust(member(v(groups),##),2)][center([switch([get(##/CANREAD)]:[get(##/CANWRITE)]:[u(##/CANWRITE,%#)],1:1:1,,1:*:1,{(-)},1:*:0,-,*)],5)][ljust(name(##),30)][ljust(get(##/LAST_MOD),20)][rjust(words([setq(0,get(##/MESS_LST))]%q0),3)]%b[ljust(u(isgroupread,%#,%q0),2)])]%r[repeat(-,78)]%r[center('*' = restricted[space(5)]'-' = read only[space(5)]'\(-\)' = read only\, but you can write,78)]%r[center(=[switch(hasflag(%#,wizard),1,{= BBS at [round(mul(fdiv(strlen(v(master_lst)),4000),100),1)]\\% capacity =},=)]=,78,=)]
&CMD_+BBTOSS #222=$+bbtoss:@switch hasattr(#111,bb_post_hdr_%#)=0,{@pemit %#=You do not have bbpost in progress.},{&bb_post_hdr_%# #111; &bb_post_bdy_%# #111; @pemit %#=Your bbpost has been discarded.}
&CMD_+BB #222=$+bb *:@switch hasattr(#111,bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# #111=[v(bb_post_bdy_%#)]%b%0; @pemit %#=Text added to bbpost.}
&CMD_+BBEDIT2 #222=$+bbedit *=*/*:@switch strmatch([setq(0,ucstr(%0))]%q0,*/*)=0,{@switch %q0=TEXT,{@edit #111/bb_post_bdy_%#={%1},{%2}},TITLE,{&bb_post_hdr_%# #111=[index(v(bb_post_hdr_%#),|,1,1)]|[edit(index(v(bb_post_hdr_%#),|,2,1),{%1},{%2})]}; @wait 0={@pemit %#=[center(= BB Post in Progress =,78,=)]%rGroup: %b[name(index(v(bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(v(bb_post_hdr_%#),|,2,1)]%r[repeat(-,78)]%r[trim(v(bb_post_bdy_%#))]%r[repeat(=,78)]}}
&CMD_+BBHELP #222=$+bbhelp:@pemit %#=Please use '+help bb'.
@parent #222=#111
@set #222=INHERIT
@set #222=SAFE
@set #222=!halted

&bb_read me=1
&bb_read me
&bb_omit me=1
&bb_omit me

@switch %#=#1,{@attribute/access bb_read=hidden wizard; @attribute/access bb_omit=hidden wizard},{@pemit me=Reminder! Make sure #1 executes the following:%r%b%b@attribute/access bb_read=hidden wizard%r%b%b@attribute/access bb_omit=hidden wizard}

@pemit me=%rBulletin Board construction complete. Place the bbpocket inside the Global BB, then place the Global BB in the Master Room. Enjoy!%r[repeat(=,78)]

@set me=!quiet
